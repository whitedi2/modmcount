
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	БизнесПроцесс = Параметры.БизнесПроцесс;
	ТекЗаявка = Параметры.ТекЗаявка;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Задача.Ссылка,
	               |	Задача.Дата,
	               |	Задача.ВРаботе,
	               |	РАЗНОСТЬДАТ(Задача.Дата, &ТекДата, МИНУТА) КАК ВремяНахождения,
	               |	Задача.Исполнитель КАК ТекущийИсполнитель,
	               |	РАЗНОСТЬДАТ(Задача.Дата, &ТекДата, ЧАС) КАК ВремяЧас
	               |ИЗ
	               |	Справочник.Задача КАК Задача
	               |ГДЕ
	               |	Задача.Выполнена = ЛОЖЬ
	               |	И Задача.ПометкаУдаления = ЛОЖЬ
	               |	И Задача.БизнесПроцесс = &БизнесПроцесс";
	
	Запрос.УстановитьПараметр("БизнесПроцесс", Параметры.БизнесПроцесс);
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТекущиеИсполнители.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		
		НоваяСтрока.ВремяНахождения = Строка(Цел(Выборка.ВремяНахождения / 60)) + " ч " + Строка(Окр((Выборка.ВремяНахождения / 60 - Цел(Выборка.ВремяНахождения / 60)) * 60, 0)) + " мин";
		
	КонецЦикла;
	
	АдресВозврата = "Автору";
КонецПроцедуры

&НаКлиенте
Процедура ВернутьНаДоработку(Команда)
	
	Если Не ЗначениеЗаполнено(ПричинаОтмены) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Укажите причину возврата!";
		Сообщение.Поле = "ПричинаОтмены";
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли;
	Если АдресВозврата = "Пользователю" И НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Укажите пользователя!";
		Сообщение.Поле = "Пользователь";
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли;
	
	Удачно = БПСервер.ИзменитьЗадачу(БизнесПроцесс, ТекЗаявка, РазрешитьРедактирвованиеДокумента, ПричинаОтмены, АдресВозврата, Пользователь);
	Если Удачно Тогда
		ОповеститьОбИзменении(ТекЗаявка);
		ОповеститьОбИзменении(Тип("СправочникСсылка.Задача"));
		Оповестить("РазрешитьРедактированиеФормы", Новый Структура("РазрешитьРедактирвованиеДокумента", РазрешитьРедактирвованиеДокумента) );
		Оповестить("ОбновитьСписокЗадач");
		БПСервер.ЗаписатьДействиеПользователяВЛог(БизнесПроцесс, "ВернутьНаДоработкуЧерезФормуВозврата");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресВозвратаПриИзменении(Элемент)
	Если АдресВозврата = "Пользователю" Тогда
		Элементы.Пользователь.Видимость = Истина;
	Иначе
		Элементы.Пользователь.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	АдресВозвратаПриИзменении(Неопределено);
	ЭтаФорма.ЗаблокироватьДанныеФормыДляРедактирования();
	
КонецПроцедуры


