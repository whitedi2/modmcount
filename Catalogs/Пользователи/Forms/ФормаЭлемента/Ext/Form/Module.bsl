
&НаСервере
Процедура УУ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ТекЭл = Справочники.сабМониторВнедрения.НайтиПоНаименованию("ИспользоватьБизнесПроцессы", Истина);
	Если ЗначениеЗаполнено(ТекЭл) И ТекЭл.Значение Тогда
		
		Страница = Элементы.Добавить(
		"УпрГруппа",
		Тип("ГруппаФормы"),
		Элементы.Страницы);
		
		Страница.Заголовок = "Реквизиты (Управленка)";
		
		ПолеВвода = Элементы.Добавить("ДоступныПредприятияИзСписка", Тип("ПолеФормы"), Страница);
		ПолеВвода.Вид = ВидПоляФормы.ПолеФлажка;
		ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		ПолеВвода.ПутьКДанным = "Объект.ДоступныПредприятияИзСписка";
		
		ПолеВвода = Элементы.Добавить("ОграничениеИсточников", Тип("ПолеФормы"), Страница);
		ПолеВвода.Вид = ВидПоляФормы.ПолеФлажка;
		ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		ПолеВвода.ПутьКДанным = "Объект.ОграничениеИсточников";
		
		ПолеВвода = Элементы.Добавить("ВидимостьПоСогласованию", Тип("ПолеФормы"), Страница);
		ПолеВвода.Вид = ВидПоляФормы.ПолеФлажка;
		ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		ПолеВвода.ПутьКДанным = "Объект.ВидимостьПоСогласованию";
		
		ПолеВвода = Элементы.Добавить("ДопПодтверждениеНажатияКнопок", Тип("ПолеФормы"), Страница);
		ПолеВвода.Вид = ВидПоляФормы.ПолеФлажка;
		ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		ПолеВвода.ПутьКДанным = "Объект.ДопПодтверждениеНажатияКнопок";
		
		ПолеВвода = Элементы.Добавить("ОповещениеEmail", Тип("ПолеФормы"), Страница);
		ПолеВвода.Вид = ВидПоляФормы.ПолеФлажка;
		ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		ПолеВвода.ПутьКДанным = "Объект.ОповещениеEmail";
		
		Группа = Элементы.Добавить(
		"саб_Таблицы",
		Тип("ГруппаФормы"),
		Страница);
		
		Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Группа.Поведение  = ПоведениеОбычнойГруппы.Свертываемая;
		Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		Группа.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;
		Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
		
		ТаблицаУслуги = Элементы.Добавить("саб_ДоступныеСтатьи", Тип("ТаблицаФормы"), Группа);
		ТаблицаУслуги.ПутьКДанным = "Объект.ДоступныеСтатьи";
		ТаблицаУслуги.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		
		ЭлементТаблицыФормы = Элементы.Добавить("саб_СтатьяДДСДоступныеСтатьи", Тип("ПолеФормы"),ТаблицаУслуги);
		ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементТаблицыФормы.ПутьКДанным = "Объект.ДоступныеСтатьи.СтатьяДДС";
		
		ТаблицаУслуги = Элементы.Добавить("саб_НедоступныеСтатьи", Тип("ТаблицаФормы"), Группа);
		ТаблицаУслуги.ПутьКДанным = "Объект.НедоступныеСтатьи";
		ТаблицаУслуги.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		
		ЭлементТаблицыФормы = Элементы.Добавить("саб_СтатьяДДСНедоступныеСтатьи", Тип("ПолеФормы"),ТаблицаУслуги);
		ЭлементТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементТаблицыФормы.ПутьКДанным = "Объект.НедоступныеСтатьи.СтатьяДДС";
		
		
		ГруппаДолжностиИОргСтруктура = Элементы.Добавить(
		"ГруппаДолжностиИОргСтруктура",
		Тип("ГруппаФормы"),
		Страница);
		ГруппаДолжностиИОргСтруктура.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаДолжностиИОргСтруктура.Заголовок = "Должности и орг. структура";
		
		ТипСтрока = Новый ОписаниеТипов("Строка");
		ТипДата = Новый ОписаниеТипов("Дата",,,,,Новый КвалификаторыДаты(ЧастиДаты.Дата));
		РеквизитДолжностьНаДату = Новый РеквизитФормы("РеквизитДолжностьНаДату", ТипДата, "", "На дату:");
		РеквизитТекущаяДолжностьПользователя = Новый РеквизитФормы("РеквизитТекущаяДолжностьПользователя",Новый ОписаниеТипов("СправочникСсылка.Д_Должности"), "", "Текущая дожность:");
		РеквизитРуководительПользователя = Новый РеквизитФормы("РеквизитРуководительПользователя",Новый ОписаниеТипов("СправочникСсылка.Пользователи"), "", "Руководитель:");
		
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(РеквизитДолжностьНаДату);
		ДобавляемыеРеквизиты.Добавить(РеквизитТекущаяДолжностьПользователя);
		ДобавляемыеРеквизиты.Добавить(РеквизитРуководительПользователя);
		ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
		ЭлементФормыДолжностьНаДату = Элементы.Добавить("ЭлементФормыДолжностьНаДату", Тип("ПолеФормы"), ГруппаДолжностиИОргСтруктура);
		ЭлементФормыДолжностьНаДату.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементФормыДолжностьНаДату.ПутьКДанным = "РеквизитДолжностьНаДату";
		
		Команда = Команды.Добавить(
		"ИсторияДолжностейПользователя");
		Команда.Заголовок = "История";
		Команда.Действие  = "КомандаИсторияДолжностейПользователя";
		КнопкаФормы = Элементы.Добавить(
		"КнопкаИсторияДолжностейПользователя",
		Тип("КнопкаФормы"),
		ГруппаДолжностиИОргСтруктура);
		КнопкаФормы.ИмяКоманды = "ИсторияДолжностейПользователя";
		КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
		
		ЭлементФормыТекущаяДолжностьПользователя = Элементы.Добавить("ЭлементФормыТекущаяДолжностьПользователя", Тип("ПолеФормы"), ГруппаДолжностиИОргСтруктура);
		ЭлементФормыТекущаяДолжностьПользователя.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементФормыТекущаяДолжностьПользователя.ПутьКДанным = "РеквизитТекущаяДолжностьПользователя";
		
		ЭлементФормыРуководительПользователя = Элементы.Добавить("ЭлементФормыРуководительПользователя", Тип("ПолеФормы"), ГруппаДолжностиИОргСтруктура);
		ЭлементФормыРуководительПользователя.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементФормыРуководительПользователя.ПутьКДанным = "РеквизитРуководительПользователя";
		
		ЗаполнитьДолжностиИОргСтруктура();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДолжностиИОргСтруктура()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДолжностиПользователяСрезПоследних.Период КАК Период,
		|	ДолжностиПользователяСрезПоследних.Должность КАК Должность,
		|	ДолжностиПользователяСрезПоследних.Руководитель КАК Руководитель
		|ИЗ
		|	РегистрСведений.ДолжностиПользователя.СрезПоследних КАК ДолжностиПользователяСрезПоследних
		|ГДЕ
		|	ДолжностиПользователяСрезПоследних.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЭтаФорма.РеквизитДолжностьНаДату = ВыборкаДетальныеЗаписи.Период;
		ЭтаФорма.РеквизитТекущаяДолжностьПользователя = ВыборкаДетальныеЗаписи.Должность;
		ЭтаФорма.РеквизитРуководительПользователя = ВыборкаДетальныеЗаписи.Руководитель;		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаИсторияДолжностейПользователя(Команда)
	Отбор = Новый Структура("Пользователь", Объект.Ссылка);  
	ОткрытьФорму("РегистрСведений.ДолжностиПользователя.ФормаСписка", Новый Структура("Отбор", Отбор));
КонецПроцедуры

&НаСервере
Процедура УУ_ПередЗаписьюНаСервереПосле(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	сабПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
КонецПроцедуры

&НаСервере
Процедура сабПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьДоступныеПредприятия(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеПредприятия(ТекущийОбъект)
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОсновныеДолжностиПредприятия.Предприятие КАК Предприятие
	|ИЗ
	|	РегистрСведений.ОсновныеДолжностиПредприятия КАК ОсновныеДолжностиПредприятия
	|ГДЕ
	|	ОсновныеДолжностиПредприятия.Сотрудник В(&Сотрудник)";
	
	Запрос.УстановитьПараметр("Сотрудник", ТекущийОбъект.Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если НЕ ТекущийОбъект.ДоступныПредприятияИзСписка Тогда
		ТекущийОбъект.ДоступныеПредприятия = "Все, ";
	ИначеЕсли Не Выборка.Количество() И ТекущийОбъект.ДоступныПредприятияИзСписка Тогда
		ТекущийОбъект.ДоступныеПредприятия = "Нет доступных, ";
	ИначеЕсли Выборка.Количество() Тогда  
		ТекущийОбъект.ДоступныеПредприятия = "";
		Пока Выборка.Следующий() Цикл
			ТекущийОбъект.ДоступныеПредприятия = ТекущийОбъект.ДоступныеПредприятия + Строка(Выборка.Предприятие) + ", ";		
		КонецЦикла;
	КонецЕсли;
	
	Если Не ТекущийОбъект.ДоступныеПредприятия = "" Тогда
		ТекущийОбъект.ДоступныеПредприятия = Лев(ТекущийОбъект.ДоступныеПредприятия, СтрДлина(ТекущийОбъект.ДоступныеПредприятия) - 2);
	КонецЕсли;

КонецПроцедуры
