#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Обработчик подсистемы "Дополнительные отчеты и обработки".
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Владелец") Тогда

		Владелец            = Параметры.Отбор.Владелец;
		ВладелецДляОтбора   = Неопределено;
		ОбщиеХарактеристики = Неопределено;

		Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
			Если Владелец.ВидНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ИндивидуальныеДляНоменклатуры Тогда
				ВладелецДляОтбора = Владелец;
				ОбщиеХарактеристики = Ложь;
			ИначеЕсли Владелец.ВидНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры Тогда
				ВладелецДляОтбора = Владелец.ВидНоменклатуры;
				ОбщиеХарактеристики = Истина;
			КонецЕсли;
			ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы.Список.ПодчиненныеЭлементы,"Владелец", "Заголовок", "Номенклатура");
		ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
			Если Владелец.ИспользованиеХарактеристик = Перечисления.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры Тогда
				ВладелецДляОтбора = Владелец;
				ОбщиеХарактеристики = Истина;
			КонецЕсли;
			ОбщегоНазначенияРТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы.Список.ПодчиненныеЭлементы,"Владелец", "Заголовок", "Виды номенклатуры");
		КонецЕсли;

		Если ВладелецДляОтбора = Неопределено Тогда

			ТекстЗаголовка = НСтр("ru = 'Для элемента: ""%Владелец%"" характеристики не используются'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Владелец%", Строка(Владелец));

			АвтоЗаголовок = Ложь;
			Заголовок     = ТекстЗаголовка;

			Элементы.Список.ТолькоПросмотр = Истина;
		Иначе
			
			ТекстЗаголовка = НСтр("ru = 'Характеристики номенклатуры (%Владелец%)'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Владелец%", Строка(Владелец));

			АвтоЗаголовок = Ложь;
			Заголовок     = ТекстЗаголовка;
			
		КонецЕсли;

		Если ОбщиеХарактеристики = Истина Тогда
			Элементы.ДекорацияПредупреждение.Видимость = Истина;
		КонецЕсли;
		
		Параметры.Отбор.Владелец = ВладелецДляОтбора;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не ЗначениеЗаполнено(Владелец) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Создавать характеристики можно только через карточку владельца (номенклатуры или вида номенклатуры).'"));
		Отказ = Истина;
		Возврат
	КонецЕсли;

КонецПроцедуры

#КонецОбласти