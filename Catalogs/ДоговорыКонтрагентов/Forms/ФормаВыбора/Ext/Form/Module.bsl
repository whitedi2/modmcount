
&НаСервере
Процедура УУ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ИзменениеПоступленияНаРасчетныйСчет тогда 
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементОтбора.ПравоеЗначение = Параметры.СписокДоговоров; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
&Вместо("СтруктураОтборовСписка")
Функция УУ_СтруктураОтборовСписка()
	
	СтруктураОтборов = Новый Структура;
	
	Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		
		Если НЕ ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			
			Для Каждого ЭлементОтбораГруппы Из ЭлементОтбора.Элементы Цикл
				
				Если ЭлементОтбораГруппы.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
					СтруктураОтборов.Вставить(Строка(ЭлементОтбораГруппы.ЛевоеЗначение), ЭлементОтбораГруппы.ПравоеЗначение);
				ИначеЕсли ЭлементОтбораГруппы.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
					СтруктураОтборов.Вставить(Строка(ЭлементОтбораГруппы.ЛевоеЗначение), ЭлементОтбораГруппы.ПравоеЗначение.ВыгрузитьЗначения());
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				СтруктураОтборов.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение);
			ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
				СтруктураОтборов.Вставить(Строка(ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение.ВыгрузитьЗначения());
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураОтборов;
	
КонецФункции
