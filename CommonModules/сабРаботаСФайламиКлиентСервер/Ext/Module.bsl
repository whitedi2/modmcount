#Область ПрограммныйИнтерфейс

// Возвращает фильтр файлов изображений для использования в конструкторе ДиалогВыбораФайла.
// 
// Возвращаемое значение:
// 	Строка - см. описание свойства Фильтр объекта ДиалогВыбораФайла в синтакс-помощнике.
Функция ФильтрФайловИзображений() Экспорт
	
	ВариантыФильтров = Новый Массив;
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Все картинки'"),
		"*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Все файлы'"), "*.*");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Формат bmp'"), "*.bmp;*.dib;*.rle");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Формат GIF'"), "*.gif");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Формат JPEG'"), "*.jpeg;*.jpg");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Формат PNG'"), "*.png");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Формат TIFF'"), "*.tif");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Формат icon'"), "*.ico");
	ДобавитьВариантФильтра(ВариантыФильтров, НСтр("ru = 'Формат метафайл'"), "*.wmf;*.emf");

	Результат = СтрСоединить(ВариантыФильтров, "|");
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьВариантФильтра(ВариантыФильтров, Представление, Маски)
	ДобавляемыйВариант = СтрШаблон("%1(%2)|%2", Представление, Маски);
	ВариантыФильтров.Добавить(ДобавляемыйВариант);
КонецПроцедуры

#КонецОбласти