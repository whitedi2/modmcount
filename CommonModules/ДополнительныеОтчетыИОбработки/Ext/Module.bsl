
&Вместо("ВыполнитьОбработкуПоРегламентномуЗаданию")
Процедура УУ_ВыполнитьОбработкуПоРегламентномуЗаданию(ВнешняяОбработка, ИдентификаторКоманды)
	Если ТипЗнч(ИдентификаторКоманды) = Тип("ПеречислениеСсылка.РегламентныеЗаданияРасширения")  Тогда
		
		Если ИдентификаторКоманды = Перечисления.РегламентныеЗаданияРасширения.ЗагрузкаВыписокИзКлиентБанка Тогда
			
			РегламентныеЗаданияРасширения.ВыполнитьРегламентнуюЗадачуЗагрузкаВыписокИзКлиентБанка();
			
		ИначеЕсли ИдентификаторКоманды = Перечисления.РегламентныеЗаданияРасширения.РассылкаОповещенийОСозданииДокументовНаЭлектроннуюПочту Тогда
			
			РегламентныеЗаданияРасширения.ВыполнитьРегламентнуюЗадачуРассылкаОповещенийОСозданииДокументовНаЭлектроннуюПочту();
			
		ИначеЕсли ИдентификаторКоманды = Перечисления.РегламентныеЗаданияРасширения.ЗаполнениеРегистраКэшОстатков Тогда
			
			РегламентныеЗаданияРасширения.ВыполнитьРегламентнуюЗадачуЗаполнениеРегистраКэшОстатков();
			
		КонецЕсли;
		
	Иначе
		
		ПродолжитьВызов(ВнешняяОбработка, ИдентификаторКоманды);
		
	КонецЕсли;
КонецПроцедуры

&Вместо("ПредставлениеОбъектаМетаданных")
Функция УУ_ПредставлениеОбъектаМетаданных(Объект)
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных")
		Или ТипЗнч(Объект) = Тип("СправочникСсылка.ИдентификаторыОбъектовРасширений") Тогда
		ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Объект, Ложь);
		
		Если ТипЗнч(ОбъектМетаданных) <> Тип("ОбъектМетаданных") Тогда
			Возврат НСтр("ru = '<Не существует>'");
		КонецЕсли; 
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ОбъектМетаданных") Тогда
		ОбъектМетаданных = Объект;
	Иначе
		ОбъектМетаданных = Метаданные.Подсистемы.Найти(Объект);
	КонецЕсли;   
	
	Возврат ОбъектМетаданных.Представление();
	
КонецФункции
