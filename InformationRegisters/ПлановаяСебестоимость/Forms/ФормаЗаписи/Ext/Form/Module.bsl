
&НаКлиенте
Процедура ПолучитьФакт(Команда)
	Запись.СуммаФакт = РассчитатьСебестоимость(Запись.Номенклатура,Запись.Период,Запись.Предприятие);
КонецПроцедуры

&НаСервереБезКонтекста
Функция РассчитатьСебестоимость(ВыбНоменклатура,ВыбПериод,ВыбПредприятие)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныйОборотыКоличество.КоличествоОборот,
		|	УчетныйОборотыСумма.СуммаОборот
		|ИЗ
		|	РегистрБухгалтерии.Учетный.Обороты(
		|			&ДатаНачала,
		|			&ДатаКонца,
		|			,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Учетный.ВыпускПродукции),
		|			,
		|			Предприятия = &ВыбПредприятие
		|				И Субконто1 = &ВыбПродукция,
		|			КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Учетный.ОсновноеПрво),
		|			) КАК УчетныйОборотыСумма
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Учетный.Обороты(
		|				&ДатаНачала,
		|				&ДатаКонца,
		|				,
		|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Учетный.ГотоваяПродукция),
		|				,
		|				Субконто1 = &ВыбПродукция
		|					И Предприятия = &ВыбПредприятие,
		|				КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Учетный.ВыпускПродукции),
		|				) КАК УчетныйОборотыКоличество
		|		ПО УчетныйОборотыСумма.Субконто1 = УчетныйОборотыКоличество.Субконто1
		|			И УчетныйОборотыСумма.Предприятия = УчетныйОборотыКоличество.Предприятия";

	Запрос.УстановитьПараметр("ВыбПредприятие", ВыбПредприятие);
	Запрос.УстановитьПараметр("ВыбПродукция", ВыбНоменклатура);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(ВыбПериод));
	Запрос.УстановитьПараметр("ДатаКонца", КонецМесяца(ВыбПериод));

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ФактСебестоимость = ?(ВыборкаДетальныеЗаписи.КоличествоОборот = 0,0,ВыборкаДетальныеЗаписи.СуммаОборот/ВыборкаДетальныеЗаписи.КоличествоОборот);
	Иначе
		ФактСебестоимость = 0;
	КонецЕсли;
	Возврат ФактСебестоимость;
КонецФункции


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если БюджетныйНаСервере.РольДоступнаСервер("Администратор") Тогда
		Элементы.СуммаФакт.Доступность = Истина;
	Иначе
		ГлобальнаяДата = УЧ_Сервер.ДатаГлобальногоЗапрета();
		ДатаЗакрытияУчетПредприятие = УЧ_Сервер.ДатаЛокальногоЗапрета(Запись.Предприятие);
		ДатаЗакрытия = Макс(ГлобальнаяДата,ДатаЗакрытияУчетПредприятие);
		ИсточникДата = Запись.Период;	
		Если ИсточникДата <= ДатаЗакрытия  Тогда
			ЭтаФорма.ТолькоПросмотр = Истина
		КонецЕсли;

		Элементы.СуммаФакт.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если НЕ БюджетныйНаСервере.РольДоступнаСервер("Администратор") Тогда
		ГлобальнаяДата = УЧ_Сервер.ДатаГлобальногоЗапрета();
		ДатаЗакрытияУчетПредприятие = УЧ_Сервер.ДатаЛокальногоЗапрета(Запись.Предприятие);
		ДатаЗакрытия = Макс(ГлобальнаяДата,ДатаЗакрытияУчетПредприятие);
		ИсточникДата = Запись.Период;	
		Если ИсточникДата <= ДатаЗакрытия  Тогда
			Сообщить("Нельзя сохранять в закрытом периоде!");
			Отказ = Истина
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

