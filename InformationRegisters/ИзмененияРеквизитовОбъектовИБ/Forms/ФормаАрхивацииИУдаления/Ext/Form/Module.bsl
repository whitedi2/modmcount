
&НаКлиенте
Процедура ВыполнитьАрхивацию(Команда)
	
	ВыполнитьАрхивациюНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуАрхиваНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	Диалог.ПолноеИмяФайла	= ПутьКФайлуАрхива;
	Диалог.Фильтр			= "Архив zip (*.zip)|*.zip";

	Результат = Диалог.Выбрать();

	Если Результат Тогда
		ПутьКФайлуАрхива = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьАрхивациюНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ *
	|ИЗ
	|	РегистрСведений.ИзмененияРеквизитовОбъектовИБ КАК Регистр
	|ГДЕ
	|	Регистр.Период МЕЖДУ &НачПериода И &КонПериода";
	Запрос.УстановитьПараметр("НачПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонПериода", КонецДня(Период.ДатаОкончания));
	
	СтруктураЗаписи = Новый Структура("Период, Объект, ИмяРеквизита, ИмяТабличнойЧасти, НомерСтрокиТЧ, СтароеЗначение, НовоеЗначение, Пользователь, ТипДействияПользователя");
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Если Архивировать Тогда
		КаталогВременныхФайлов = КаталогВременныхФайлов();
		// Запись в XML
		ИмяФайла	= КаталогВременныхФайлов + "\Reg " + СтрЗаменить(Строка(ТекущаяДата()), ":", " ");
		ИмяФайлаХМЛ	= ИмяФайла + ".xml";
		
		ЗаписьXML = Новый ЗаписьXML();
		ЗаписьXML.ОткрытьФайл(ИмяФайлаХМЛ);
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("Начало");
		
		ТекущийНомерЗаписи = 0;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекущийНомерЗаписи = ТекущийНомерЗаписи + 1;
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("Запись" + Формат(ТекущийНомерЗаписи, "ЧГ=0"));
			
			Для Каждого КлючИЗначение Из СтруктураЗаписи Цикл
				Ключ = КлючИЗначение.Ключ;
				
				ЗаписьXML.ЗаписатьНачалоЭлемента(Ключ);
				ЗаписатьXML(ЗаписьXML, Выборка[Ключ]);
				ЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЦикла;
			
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла;

		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.Закрыть();
		
		// Архивация	
		ЗаписьЗИП = Новый ЗаписьZipФайла(?(Прав(ПутьКФайлуАрхива, 4) = ".zip", ПутьКФайлуАрхива, ПутьКФайлуАрхива + ".zip"));
		ЗаписьЗИП.Добавить(ИмяФайлаХМЛ);
		ЗаписьЗИП.Записать();
		
		// Удаление файла ХМЛ
		Попытка
			УдалитьФайлы(ИмяФайлаХМЛ);
		Исключение
			Сообщить("Не удалось удалить файл " + ИмяФайлаХМЛ + "." + Символы.ПС +
					ОписаниеОшибки());
		КонецПопытки;
		
	КонецЕсли;
	
	Если Удалять Тогда
		НачатьТранзакцию();
		
		// Создаем и заполняем структуру для отбора набора записей регистра сведений
		СтруктураОтбора		= Новый Структура;
		МетаданныеРегистра	= Метаданные.РегистрыСведений.ИзмененияРеквизитовОбъектовИБ;
		
		Для Каждого Измерение ИЗ МетаданныеРегистра.Измерения Цикл
			СтруктураОтбора.Вставить(Измерение.Имя);
		КонецЦикла;
		
		Для Каждого Реквизит ИЗ МетаданныеРегистра.СтандартныеРеквизиты Цикл
			СтруктураОтбора.Вставить(Реквизит.Имя);		
		КонецЦикла;
		
		// создаем набор записей регистра
		НаборЗаписей = РегистрыСведений.ИзмененияРеквизитовОбъектовИБ.СоздатьНаборЗаписей();
		Выборка		 = Результат.Выбрать();	
		
		Пока Выборка.Следующий() Цикл
			
			Для Каждого Элемент Из СтруктураОтбора Цикл
				НаборЗаписей.Отбор[Элемент.Ключ].Установить(Выборка[Элемент.Ключ]);
			КонецЦикла;
			
			НаборЗаписей.Записать();
			НаборЗаписей.Очистить();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьУдалениеАрхивациюЗаписейРегистра

&НаКлиенте
Процедура ПросмотрСодержимогоФайлаАрхива(Команда)
		
	ИмяФайла = ПутьКФайлуАрхива;
	ТекФайл	 = Новый Файл(ИмяФайла);
	
	Если Не ТекФайл.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	ЧтениеЗИП = Новый ЧтениеZipФайла(ИмяФайла);
	
	Если ЧтениеЗИП.Элементы.Количество() = 0 Тогда
		Предупреждение("Выбранный архив не содержит ни одного файла", 120);
		Возврат;
	КонецЕсли;
	
	ЭлементЗИП = ЧтениеЗИП.Элементы[0];
	
	Если Не ЭлементЗИП.Расширение  = "xml" Тогда
		Предупреждение("Первый файл в архиве не файл xml. Это не архив изменений." +
				Символы.ПС + "Архив открыт не будет", 120);
		Возврат;
	КонецЕсли;
	
	КаталогВременныхФайлов = КаталогВременныхФайлов();
	ЧтениеЗИП.Извлечь(ЭлементЗИП, КаталогВременныхФайлов, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	
	ИмяФайла= КаталогВременныхФайлов + "\" + ЭлементЗИП.Имя;
	ТекФайл	= Новый Файл(ИмяФайла);
	
	Если Не ТекФайл.Существует() Тогда
		Возврат;
	КонецЕсли;
		
	ПросмотрСодержимогоФайлаАрхиваНаСервере(ИмяФайла);
	
	Элементы.Список.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПросмотрСодержимогоФайлаАрхиваНаСервере(ИмяФайла)
	
	ЧтениеХМЛ = Новый ЧтениеXML;
	ЧтениеХМЛ.ОткрытьФайл(ИмяФайла);
	
	ТаблицаЗначенийИзФайла	= Новый ТаблицаЗначений;
	КолонкиТЗ				= ТаблицаЗначенийИзФайла.Колонки;
	
	Пока ЧтениеХМЛ.Прочитать() Цикл
		Если ЧтениеХМЛ.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяУзла = ЧтениеХМЛ.Имя;
			
			Если ИмяУзла = "Начало" Тогда
				Продолжить;
			КонецЕсли;
			
			Если Найти(ИмяУзла, "Запись") > 0 Тогда
				СтрокаТЗ = ТаблицаЗначенийИзФайла.Добавить();
				ЧтениеХМЛ.Прочитать();
			КонецЕсли;
			
			ИмяУзла = ЧтениеХМЛ.Имя;
			Если КолонкиТЗ.Найти(ИмяУзла) = Неопределено Тогда
				КолонкиТЗ.Добавить(ИмяУзла);
			КонецЕсли;
			
			ЧтениеХМЛ.Прочитать();
			
			СтрокаТЗ[ИмяУзла] = ПрочитатьXML(ЧтениеХМЛ);
			
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаЗначенийИзФайла, "Список");
		
КонецПроцедуры	

&НаКлиенте
Процедура ПутьКФайлуАрхиваОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	Если НЕ ПустаяСтрока(ПутьКФайлуАрхива) Тогда
		ЗапуститьПриложение("explorer " + ПутьКФайлуАрхива);
	КонецЕсли;
	
КонецПроцедуры
