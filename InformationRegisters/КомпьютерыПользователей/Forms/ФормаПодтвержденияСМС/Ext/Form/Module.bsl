/////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Хозяин") Тогда
		Хозяин = Параметры.Хозяин;
	Иначе
		Хозяин = "";
	КонецЕсли;	
	ТекущийПользователь = сабОбщегоНазначения.ТекущийПользователь(); 
	Телефон = ЗашифроватьНомерТелефона(ТекущийПользователь.Телефон);
	
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Строка(Элементы.Декорация1.Заголовок), "%");
	МассивДляФорматированнойСтроки = новый Массив;
	ЖирныйШрифт = новый Шрифт(,,Истина);
	Для Каждого ТекПодстрока Из МассивПодстрок Цикл
		
		Если ТекПодстрока = "Хозяин" Тогда
			МассивДляФорматированнойСтроки.Добавить(новый ФорматированнаяСтрока(Строка(Хозяин), ЖирныйШрифт));
		ИначеЕсли ТекПодстрока = "ТекущийПользователь" Тогда
			МассивДляФорматированнойСтроки.Добавить(новый ФорматированнаяСтрока(Строка(ТекущийПользователь), ЖирныйШрифт));
		ИначеЕсли ТекПодстрока = "Телефон" Тогда
			МассивДляФорматированнойСтроки.Добавить(новый ФорматированнаяСтрока(Телефон,, новый Цвет(74, 87, 143)));
		Иначе
			МассивДляФорматированнойСтроки.Добавить(ТекПодстрока);
		КонецЕсли;
		
	КонецЦикла;			
	Элементы.Декорация1.Заголовок = новый ФорматированнаяСтрока(МассивДляФорматированнойСтроки);
	
	Подтвержден = Ложь;
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Отказ = Не МожноЗакрыватьФорму;
КонецПроцедуры

/////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

&НаСервере
Процедура ПолучитьКодНаСервере()
	
	ГСЧ = новый ГенераторСлучайныхЧисел;
	Код = ГСЧ.СлучайноеЧисло(1000, 9999);
	ПравильныеКоды.Добавить(Формат(Код, "ЧГ=0"));
	
	СписокСообщений = Новый Массив;
	Структура = Новый Структура;
	Структура.Вставить("GUID",            Строка(новый УникальныйИдентификатор));
	Структура.Вставить("НомерПолучателя", ПривестиНомерТелефона(сабОбщегоНазначения.ТекущийПользователь().Телефон));
	Структура.Вставить("ТекстСообщения",  Формат(Код, "ЧГ=0"));
	Структура.Вставить("КодОшибки",       0);
	СписокСообщений.Добавить(Структура);
	
	Результат = смсКоммуникатор.Подключиться();
	Если Результат = 1 Тогда 
		//Предупреждение(НСтр("ru = 'Вы успешно подключились к серверу SMS4B!'"));
		ПараметрыСессии = Новый Структура;
		смсКоммуникатор.ПолучитьПараметрыСессии(ПараметрыСессии);
		смсРаботаССообщениями.ЗаписатьПараметры(ПараметрыСессии);
	Иначе
		Сообщить("Ошибка при попытке подключения: " + смсКоммуникатор.ОписаниеОшибокВебСервиса(Результат)+"'");
	КонецЕсли;
	
	КодОшибки = смсКоммуникатор.ОтправитьСообщения(СписокСообщений, "1C Doc", "", ТекущаяДата(),,,,);
	Если КодОшибки < 0 Тогда
		Сообщить("Действие не выполнено! " + смсКоммуникатор.ОписаниеОшибокВебСервиса(КодОшибки) + "(" + КодОшибки + ")");
		Сообщить("Обратитесь в службу техподдержки");
	Иначе 
		Сообщить("СМС отправлена, ждите доставки"); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКод(Команда)
	ПолучитьКодНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Подтвердить(Команда)
	
	Если ПравильныеКоды.НайтиПоЗначению(ВведенныйКод) <> Неопределено Тогда
		Подтвержден = Истина;
		МожноЗакрыватьФорму = Истина;
	Иначе
		Предупреждение("Неверный код");
	КонецЕсли;
	
	ОбновитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСеанс(Команда)
	Закрыть(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрепитьКомпьютерЗаМной(Команда)
	
	ЗакрепитьКомпьютерЗаМнойСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьСеанс(Команда)
	МожноЗакрыватьФорму = Истина;
	Закрыть(Ложь);
КонецПроцедуры

/////////////////////////////////////////////////
// Служебные

Функция ПривестиНомерТелефона(НомерТелефона)
	
	ДопустимыеСимволы = "+1234567890";
	СтрокаРезультат = "";
	Для Ы = 1 По СтрДлина(НомерТелефона) Цикл
		ТекСимвол = Сред(НомерТелефона, Ы, 1);
		Если Найти(ДопустимыеСимволы, ТекСимвол) <> 0 Тогда
			СтрокаРезультат = СтрокаРезультат + ТекСимвол;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрокаРезультат;
	
КонецФункции

Функция ЗашифроватьНомерТелефона(знач НомерТелефона)
	
	НомерТелефона   = ПривестиНомерТелефона(НомерТелефона);
	СтрокаРезультат = "";
	Для Ы = 1 По СтрДлина(НомерТелефона) Цикл
		Если Ы >= СтрДлина(НомерТелефона) - 6 и Ы <= СтрДлина(НомерТелефона) - 2 Тогда
			СтрокаРезультат = СтрокаРезультат + "*" 
		Иначе
			ТекСимвол = Сред(НомерТелефона, Ы, 1);
			СтрокаРезультат = СтрокаРезультат + ТекСимвол;
		КонецЕсли;
	КонецЦикла;	
	
	Возврат СтрокаРезультат;	
		
КонецФункции

Процедура ОбновитьДоступность()
	
	Элементы.НачатьСеанс.Доступность        = Подтвержден;
	Элементы.ПривязатьКомпьютер.Доступность = Подтвержден;
	
	Элементы.Группа1.Видимость = Не Подтвержден;
	Элементы.ДекорацияПодтверждено.Видимость = Подтвержден;
	
КонецПроцедуры

&НаСервере
Процедура ЗакрепитьКомпьютерЗаМнойСервер()
	
	ТекущийПользователь = сабОбщегоНазначения.ТекущийПользователь();
	
	НаборЗаписей = РегистрыСведений.КомпьютерыПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Компьютер.Установить(ИмяКомпьютера());
	НаборЗаписей.Прочитать();
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Запись.Пользователь = ТекущийПользователь Тогда
			Запись.Основной = Истина;
		Иначе
			Запись.Основной = Ложь;
		КонецЕсли;
	КонецЦикла;
	НаборЗаписей.Записать();
	
КонецПроцедуры

