#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция получает данные по штрихкодам.
//
Функция ПолучитьДанныеПоШтрихкодам(Штрихкоды) Экспорт

	ДанныеПоШтрихкодам = Новый Соответствие;

	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	Штрихкоды.Владелец КАК Номенклатура,
	|	Штрихкоды.Характеристика КАК Характеристика,
	|	Штрихкоды.Упаковка КАК Упаковка
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК Штрихкоды
	|ГДЕ
	|	Штрихкоды.Штрихкод В(&МассивШтрихкодов)
	|	И Штрихкоды.Владелец ССЫЛКА Справочник.Номенклатура");

	МассивШтрихкодов = Новый Массив;

	Для Каждого ТекШтрихкод Из Штрихкоды Цикл
		МассивШтрихкодов.Добавить(ТекШтрихкод);

		ДанныеПоШтрихкодам.Вставить(ТекШтрихкод, Новый Структура);
	КонецЦикла;

	Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекДанные = ДанныеПоШтрихкодам[Выборка.Штрихкод];
		ТекДанные.Вставить("Номенклатура"  , Выборка.Номенклатура);
		ТекДанные.Вставить("Характеристика", Выборка.Характеристика);
		ТекДанные.Вставить("Упаковка"      , Выборка.Упаковка);
	КонецЦикла;

	Возврат ДанныеПоШтрихкодам;

КонецФункции

#КонецОбласти

#КонецЕсли
