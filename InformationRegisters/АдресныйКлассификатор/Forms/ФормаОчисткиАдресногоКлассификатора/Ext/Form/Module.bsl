////////////////////////////////////////////////////////////////////////////////
// Блок функций - обработчиков событий
//

&НаСервере
// Обработчик события "при создании на сервере" формы
// Заполняет таблицу по макету
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуПоМакетуАдресныхОбъектов();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "выбор" элемента формы АдресныеОбъекты
// Инвертирует флаг выбора адресного объекта
//
Процедура АдресныеОбъектыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Элемент.ТекущиеДанные.Пометка = НЕ Элемент.ТекущиеДанные.Пометка;
КонецПроцедуры

&НаКлиенте
// Обработчик события нажатия на кнопку командной панели
// элемента управления АдресныеОбъекты.
// Устанавливает флаг выбора всем адресным объектам в списке
//
Процедура ВыделитьВсеВыполнить()
	
	Для Каждого ЭлементАдресныйОбъект Из АдресныеОбъекты Цикл
		ЭлементАдресныйОбъект.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события нажатия на кнопку командной панели
// элемента управления АдресныеОбъекты.
// Снимает флаг выбора у всех адресных объектов в списке
//
Процедура ОтменитьВыделитьВсеВыполнить()
	
	Для Каждого ЭлементАдресныйОбъект Из АдресныеОбъекты Цикл
		ЭлементАдресныйОбъект.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события нажатия на кнопку "Очистить"
// Проверяет корректность данных и вызывает диалог поддверждения
// очистки адресных сведений.
//
Процедура ОчиститьВыполнить()
	
	ОчиститьСообщения();
	
	Если КоличествоВыделенныхАдресныхОбъектов() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		              НСтр("ru = 'Необходимо выбрать как минимум один адресный объект'"),
		              ЭтаФорма,
		              "АдресныеОбъекты");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Адресные сведения по выбранным объектам будут удалены. Вы уверены, что хотите продолжить?'");
	КодВозврата = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	
	Если КодВозврата = КодВозвратаДиалога.ОК Тогда
		ОчиститьАдресныеСведения();
		Предупреждение(НСтр("ru = 'Адресные сведения успешно удалены'"),,
		               НСтр("ru = 'Удаление адресных сведений'"));
		Закрыть(Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Блок сервисных функций
//

&НаСервере
// Заполняет переданную таблицу значений по значениям таблицы адресных объектов.
// Выбирается код, наименование и сокращение типа объекта.
//
Процедура ЗаполнитьТаблицуПоМакетуАдресныхОбъектов()
	
	АдресныеОбъекты.Очистить();
	
	КлассификаторАдресныхОбъектовXML =
	   РегистрыСведений.АдресныйКлассификатор.ПолучитьМакет("КлассификаторАдресныхОбъектовРоссии").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторАдресныхОбъектовXML).Данные;
	
	Для Каждого АдресныйОбъект Из КлассификаторТаблица Цикл
		
		Наименование = Лев(АдресныйОбъект.Code, 2)
		             + " - "
		             + АдресныйОбъект.Name
		             + " "
		             + АдресныйОбъект.Socr;
		
		НоваяСтрока = АдресныеОбъекты.Добавить();
		НоваяСтрока.Пометка             = Ложь;
		НоваяСтрока.НаименованиеАдресногоОбъекта = Наименование;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Возвращает количество помеченных адресных объектов
//
Функция КоличествоВыделенныхАдресныхОбъектов()
	
	КоличествоВыделенныхАдресныхОбъектов = 0;
	
	Для Каждого ЭлементАдресныйОбъект Из АдресныеОбъекты Цикл
		Если ЭлементАдресныйОбъект.Пометка Тогда
			КоличествоВыделенныхАдресныхОбъектов = КоличествоВыделенныхАдресныхОбъектов + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоличествоВыделенныхАдресныхОбъектов;
	
КонецФункции

&НаСервере
// Удалает адресные сведения по выбранным адресным объектам
//
Процедура ОчиститьАдресныеСведения()
	
	// формируем список адресных объектов, для очистки адресных сведений
	
	ВыбранныеАО = Новый Массив;
	
	Для Каждого ЭлементАдресныйОбъект Из АдресныеОбъекты Цикл
		Если ЭлементАдресныйОбъект.Пометка Тогда
			ВыбранныеАО.Добавить(Лев(ЭлементАдресныйОбъект.НаименованиеАдресногоОбъекта, 2));
		КонецЕсли;
	КонецЦикла;
	
	АдресныйКлассификатор.УдалитьАдресныеСведения(ВыбранныеАО);
	АдресныйКлассификатор.ЗагрузитьАдресныеОбъектыПервогоУровня();
	
КонецПроцедуры
