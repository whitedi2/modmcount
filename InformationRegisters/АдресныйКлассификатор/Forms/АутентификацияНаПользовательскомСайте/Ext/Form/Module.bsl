//------------------------------------------------------------------------------
//
// ВОЗВРАЩАЕМОЕ ЗНАЧЕНИЕ
//
// Неопределено - пользователь отказался от ввода пароля
// Структура  - 
//            ключ "Статус", булево - истина или ложь в зависмости от успеха вызова
//            ключ "КодПользователя", строка - в случае если статус Истина содержит
//                                    код пользователя
//            ключ "Пароль", строка - в случае если статус Истина содержит пароль
//
//------------------------------------------------------------------------------

////////////////////////////////////////////////////////////////////////////////
// СЕКЦИЯ ОБРАБОТЧИКОВ СОБЫТИЙ ФОРМЫ И ЭЛЕМЕНТОВ ФОРМЫ
//

&НаСервере
// Обработчик события "при создании на сервере" формы
// 
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СсылкаНаПользовательскийСайт = "Перейти к регистрации на пользовательском сайте";
	
	Результат = ЗагрузитьКодПользователя();
	
	Если ЗначениеЗаполнено(Результат) Тогда
		КодПользователя = Результат;
	Иначе
		КодПользователя = "";
	КонецЕсли;
	
	Результат = ЗагрузитьПароль();
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Пароль = Результат;
		СохранятьПароль = Истина;
	Иначе
		Пароль = "";
		СохранятьПароль = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик события нажатия на кнопку "Продолжить"
//
Процедура СохранитьДанныеАутентификацииИПродолжитьВыполнить()
	
	Если СохранятьПароль Тогда
		СохранитьПараметрыАутентификации(КодПользователя, Пароль);
	Иначе
		СохранитьПараметрыАутентификации(КодПользователя, Неопределено);
	КонецЕсли;
	
	Результат = Новый Структура("Статус, КодПользователя ,Пароль");
	Результат.Статус = Истина;
	Результат.КодПользователя = КодПользователя;
	Результат.Пароль = Пароль;
	
	Закрыть(Результат);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЕКЦИЯ СЕРВИСНЫХ ФУНКЦИЙ
//

&НаСервереБезКонтекста
// Сохраняет код пользователя и пароль в системном хранилище настроек ИБ
//
Процедура СохранитьПараметрыАутентификации(КодПользователя, Пароль)
	
	ХранилищеСистемныхНастроек.Сохранить(
	             "АутентификацияНаПользовательскомСайте",
	             "КодПользователя",
	             КодПользователя,,
	             ПараметрыСеанса.ТекущийПользователь);
	
	ХранилищеСистемныхНастроек.Сохранить(
	             "АутентификацияНаПользовательскомСайте",
	             "Пароль",
	             Пароль,,
	             ПараметрыСеанса.ТекущийПользователь);
	
КонецПроцедуры

&НаСервере
// Получает значение из системного хранилища настроек ИБ
//
Функция ЗагрузитьПароль()
	
	Значение = ХранилищеСистемныхНастроек.Загрузить(
	                  "АутентификацияНаПользовательскомСайте",
	                  "Пароль", ,
	                   ПараметрыСеанса.ТекущийПользователь);
	
	Возврат Значение;
	
КонецФункции

&НаСервере
// Получает значение из системного хранилища настроек ИБ
//
Функция ЗагрузитьКодПользователя()
	
	Значение = ХранилищеСистемныхНастроек.Загрузить(
	                  "АутентификацияНаПользовательскомСайте",
	                  "КодПользователя", ,
	                   ПараметрыСеанса.ТекущийПользователь);
	
	Возврат Значение;
	
КонецФункции

&НаКлиенте
// Реализует переход к пользовательскому сайту 1С
//
Процедура СсылкаНаПользовательскийСайтНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение("http://users.v8.1c.ru/");
КонецПроцедуры
