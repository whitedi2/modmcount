
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДеревоОбъект = Новый ДеревоЗначений;
	ДеревоОбъект.Колонки.Добавить("Имя");
	ДеревоОбъект.Колонки.Добавить("Синоним");
	ДеревоОбъект.Колонки.Добавить("Включить");
	
	ДобавитьСтрокиДереваПоИмениОбъекта("Документ"	, "ы", ДеревоОбъект);
	ДобавитьСтрокиДереваПоИмениОбъекта("Справочник"	, "и", ДеревоОбъект);
	
	ОтметитьВыбранныеРанееОбъектыВДереве(ДеревоОбъект);

    ЗначениеВРеквизитФормы(ДеревоОбъект, "Дерево");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриИзменении(Элемент)
	
	//ТекДанные = Элемент.ТекущиеДанные;
	//
	//УстановитьПометки(ТекДанные.Имя);	
	//
	//Элементы.Дерево.Развернуть(ТекДанные.ПолучитьИдентификатор());
	//Элементы.Дерево.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВерхнийУровень;
	//Элементы.Дерево.Обновить();
	
КонецПроцедуры

// Добавляет верхнюю строку и подчиненные строки в переданное дерево
//
&НаСервере
Процедура ДобавитьСтрокиДереваПоИмениОбъекта(СтрОбъект, ОкончаниеДляМножЧисла, ДеревоОбъект)
	
	ВерхняяСтрока			= ДеревоОбъект.Строки.Добавить();
	ВерхняяСтрока.Имя		= СтрОбъект;
	ВерхняяСтрока.Синоним	= СтрОбъект + ОкончаниеДляМножЧисла;
	
	НижниеСтроки = ВерхняяСтрока.Строки;
	
	Для Каждого ОбъектМетаданных Из Метаданные[ВерхняяСтрока.Синоним] Цикл
		НижняяСтрока		= НижниеСтроки.Добавить();
		НижняяСтрока.Синоним= ОбъектМетаданных.Синоним;
		НижняяСтрока.Имя	= СтрОбъект + "." + ОбъектМетаданных.Имя;
	КонецЦикла;
	
КонецПроцедуры // ДобавитьСтрокиДереваПоИмениОбъекта

// Взводит флаг у объектов из дерева, которые есть в регистре сведений списка объектов
//	
&НаСервере
Процедура ОтметитьВыбранныеРанееОбъектыВДереве(Дерево)
	
	НазваниеКолонки = "Имя";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Регистр.Объект КАК Имя
	|ИЗ
	|	РегистрСведений.ОбъектыДляРегистрацииИзмененийИБ КАК Регистр";
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
		
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаДерева = Дерево.Строки.Найти(Выборка[НазваниеКолонки], НазваниеКолонки, Истина);
		Если Не СтрокаДерева = Неопределено Тогда
			СтрокаДерева.Включить = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Установим пометки для верхних строк дерева
	//Для Каждого ВерхняяСтрока Из Дерево.Строки Цикл
	//	Если ВерхняяСтрока.Строки.Количество() > 0 Тогда
	//		ПерваяНижняяСтрока = ВерхняяСтрока.Строки[0];
	//		УстановитьПометкиРодителей(ПерваяНижняяСтрока);
	//	КонецЕсли;
	//КонецЦикла;
	
КонецПроцедуры // ОтметитьВыбранныеРанееОбъектыВДереве

// Находит текущую строку в дереве для дальнейшего 
//
// Параметры:
//  ИмяСтроки      - Имя строки дерева значений
// 
&НаСервере
Процедура УстановитьПометки(ИмяСтроки)
	
	ДеревоОбъект = РеквизитФормыВЗначение("Дерево");
	
	ТекСтрока = ДеревоОбъект.Строки.Найти(ИмяСтроки, , Истина);
	
	УстановитьПометкиПодчиненных(ТекСтрока);
	
	ЗначениеВРеквизитФормы(ДеревоОбъект, "Дерево");
	
КонецПроцедуры // УстановитьПометкиПодчиненных()

// Устанавливает состояние пометки у подчиненных строк строки дерева значений
// в зависимости от пометки текущей строки
//
// Параметры:
//  ТекСтрока      - Строка дерева значений
// 
&НаСервере
Процедура УстановитьПометкиПодчиненных(ТекСтрока)

	Включить    = ТекСтрока.Включить;
	Подчиненные = ТекСтрока.Строки;

	Если Подчиненные.Количество() > 0 Тогда
		Для каждого Строка из Подчиненные Цикл
			Строка.Включить = Включить;
			УстановитьПометкиПодчиненных(Строка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // УстановитьПометкиПодчиненных()

//Записывает установленные значения в регистр
//
&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаСервере();
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()

	ДеревоОбъект = РеквизитФормыВЗначение("Дерево");
	Набор	= РегистрыСведений.ОбъектыДляРегистрацииИзмененийИБ.СоздатьНаборЗаписей();	
	Набор.Очистить();
	Набор.Записать();
	
	Для Каждого ВерхняяСтрока Из ДеревоОбъект.Строки Цикл
		Для Каждого НижняяСтрока Из ВерхняяСтрока.Строки Цикл
			Если Не НижняяСтрока.Включить Тогда
				Продолжить;
			КонецЕсли;	
			ЗначениеОтбора	= НижняяСтрока.Имя;
			Набор			= РегистрыСведений.ОбъектыДляРегистрацииИзмененийИБ.СоздатьНаборЗаписей();
			Набор.Отбор["Объект"].Установить(ЗначениеОтбора);
			
			Запись 					= Набор.Добавить();
			Запись.Объект 			= ЗначениеОтбора;
			Запись.Регистрировать 	= Истина; 
			
			Набор.Записать();
		КонецЦикла;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры
