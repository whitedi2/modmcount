&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда		
		ТекстСообщения = НСтр(
		"ru='Для данной операции необходимо
		|установить расширение работы с файлами!
		|Установка выполняется в разделе
		|""Сервис и администрирование""/
		|""Настройка сервисных функций"".'"
		);
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(Режим);
	ДиалогВыбораКаталога.Каталог = ПутьКФайлу;
	ДиалогВыбораКаталога.МножественныйВыбор = Ложь;
	ДиалогВыбораКаталога.Заголовок = НСтр("ru = 'Выберите каталог'");
	Если ДиалогВыбораКаталога.Выбрать() Тогда
		ПутьКФайлу = ДиалогВыбораКаталога.Каталог;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлы(Команда)
	
	Разделитель = ";";
	Текст = СоздатьДанныеCSVМЛ(Разделитель);
	ЗаписатьCSV(Текст, ПутьКФайлу + "\ML.csv");
	
КонецПроцедуры

&НаСервере
Функция СоздатьДанныеCSVМЛ(Разделитель)
	
	ТаблицаЗначений = ПолучитьДанныеМЛ();
	
	//заголовок вручную
	текстЗапись = "orderReference;distributionCentreName;distributionCentreAddress;ClientName;contactPerson;contactNumber;contactEmail;customerLocationName;customerLocationAddress;additionalinstructions;latitude;longitude;task;dropDuration;capacity;volume;dropWindowStart;dropWindowEnd;cost;vehicleRequirements";
	
	Для Каждого запись Из таблицаЗначений Цикл
		
		стрЗапись = СоздатьСтрокуCSV(запись,разделитель);
		стрЗапись = СтрЗаменить(стрЗапись, Символы.НПП, "");
		стрЗапись = СтрЗаменить(стрЗапись, Символы.ПС,"");
		стрЗапись = СтрЗаменить(стрЗапись, Символы.ВК,"");
		Если СтрДлина(стрЗапись)>0 Тогда
			текстЗапись = текстЗапись +    Символы.ПС;
		КонецЕсли;    
		текстЗапись = текстЗапись + стрЗапись;
		
	КонецЦикла;        
	
	Возврат текстЗапись;
	
КонецФункции
	
&НаСервере
Функция ПолучитьДанныеМЛ()
	
	Если ВыгружатьПоСписку Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	сабМаршрутныйЛист.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.сабМаршрутныйЛист КАК сабМаршрутныйЛист
		|ГДЕ
		|	сабМаршрутныйЛист.Ссылка В(&СписокМЛ)";
		
		Запрос.УстановитьПараметр("СписокМЛ", СписокМЛ);
		
		ТЗМЛ = Запрос.Выполнить().Выгрузить();
	Иначе 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	сабМаршрутныйЛист.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.сабМаршрутныйЛист КАК сабМаршрутныйЛист
		|ГДЕ
		|	сабМаршрутныйЛист.Проведен = ИСТИНА
		|	И сабМаршрутныйЛист.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
		
		Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		
		ТЗМЛ = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Номер КАК orderReference,
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Склад КАК distributionCentreName,
	               |	"""" КАК distributionCentreAddress,
	               |	"""" КАК ClientName,
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Ответственный КАК contactPerson,
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Ответственный.Телефон КАК contactNumber,
	               |	"""" КАК contactEmail,
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Контрагент КАК customerLocationName,
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Адрес КАК customerLocationAddress,
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Примечание КАК additionalinstructions,
	               |	СОКРЛП(ЛЕВ(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Координаты, СТРНАЙТИ(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Координаты, "","") - 1)) КАК latitude,
	               |	СОКРЛП(ПОДСТРОКА(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Координаты, СТРНАЙТИ(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Координаты, "","") + 1, ДЛИНАСТРОКИ(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.Координаты))) КАК longitude,
	               |	ВЫБОР
	               |		КОГДА ТИПЗНАЧЕНИЯ(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Ссылка) = ТИП(Документ.ЗаказКлиента)
	               |			ТОГДА ""Доставка""
	               |		КОГДА ТИПЗНАЧЕНИЯ(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Ссылка) = ТИП(Документ.ЗаказПоставщику)
	               |			ТОГДА ""Сбор""
	               |	КОНЕЦ КАК task,
	               |	сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.СервисноеВремя КАК dropDuration,
	               |	СТРЗАМЕНИТЬ(СТРОКА(сабМаршрутныйЛистТабличнаяЧасть.ВесЗаказа), ""."", "","") КАК capacity,
	               |	"""" КАК volume,
	               |	ВЫБОР
	               |		КОГДА СТРОКА(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ДатаДоставки) = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА СОКРЛП(ЛЕВ(ПРАВ(СТРОКА(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.ДатаДоставки), 8), 5))
	               |		ИНАЧЕ СОКРЛП(ЛЕВ(ПРАВ(СТРОКА(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ДатаДоставки), 8), 5))
	               |	КОНЕЦ КАК dropWindowStart,
	               |	ВЫБОР
	               |		КОГДА сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ДатаДоставкиДо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ТОГДА СОКРЛП(ЛЕВ(ПРАВ(СТРОКА(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ПодразделениеКонтрагента.ДатаДоставкиДо), 8), 5))
	               |		ИНАЧЕ СОКРЛП(ЛЕВ(ПРАВ(СТРОКА(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.ДатаДоставкиДо), 8), 5))
	               |	КОНЕЦ КАК dropWindowEnd,
	               |	"""" КАК cost,
	               |	"""" КАК vehicleRequirements
	               |ИЗ
	               |	Документ.сабМаршрутныйЛист.ТабличнаяЧасть КАК сабМаршрутныйЛистТабличнаяЧасть
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.сабМаршрутныйЛист КАК сабМаршрутныйЛист
	               |		ПО сабМаршрутныйЛистТабличнаяЧасть.Ссылка = сабМаршрутныйЛист.Ссылка
	               |ГДЕ
	               |	(сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	               |			ИЛИ сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Ссылка ССЫЛКА Документ.ЗаказПоставщику)
	               |	И НЕ сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Ссылка ЕСТЬ NULL
	               |	И сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента.Проведен = ИСТИНА
	               |	И сабМаршрутныйЛист.Ссылка В(&ТЗМЛ)";
	
	Запрос.УстановитьПараметр("ТЗМЛ", ТЗМЛ);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;
КонецФункции

&НаСервере
Функция СоздатьСтрокуCSV(записьТаблицаЗначений, разделитель)    
    
    стрЗапись = "";    
    
    Для каждого поле Из записьТаблицаЗначений Цикл
        
        Если СтрДлина(стрЗапись)>0 Тогда
            стрЗапись = стрЗапись + разделитель;
        КонецЕсли;    
        
        Если ТипЗнч(поле) = Тип("Число") Тогда
            стрПоле = ?(ЗначениеЗаполнено(поле),Формат(поле,"ЧРД=."),"0");
        
        ИначеЕсли ТипЗнч(поле) = Тип("Дата") Тогда 
            стрПоле = Формат(поле,"ДФ=dd.MM.yyyy");
            
        Иначе //Если ТипЗнч(поле) = Тип("Строка") Тогда
            
            стрПоле = СтрЗаменить(Строка(поле),"""","""""");
            
            Если 
                ( СтрНайти(стрПоле," ")>0  ) ИЛИ
                  ( СтрНайти(стрПоле,"""")>0 ) ИЛИ
                  ( СтрНайти(стрПоле,",")>0  ) ИЛИ
                  ( СтрНайти(стрПоле,";")>0  ) ИЛИ
                  ( СтрНайти(стрПоле,Символы.Таб)>0 )  ИЛИ
                  ( СтрНайти(стрПоле,Символы.ВК)>0 )   ИЛИ
                  ( СтрНайти(стрПоле,Символы.ПФ)>0 )   ИЛИ
                  ( СтрНайти(стрПоле,Символы.ВТаб)>0 ) ИЛИ
                  ( СтрНайти(стрПоле,Символы.НПП)>0 )  ИЛИ
                  ( СтрНайти(стрПоле,Символы.ПС)>0 ) 
            Тогда 
                стрПоле = """"+стрПоле+"""";
            КонецЕсли;    
            
        КонецЕсли;     
        
        стрЗапись = стрЗапись + Строка(стрПоле);
        
    КонецЦикла; 
    
    Возврат стрЗапись;
    
	КонецФункции
	
&НаКлиенте
Процедура ЗаписатьCSV(текст,имяФайла)
	
	кодировка = КодировкаТекста.UTF8;
	ТекстовыйФайлЗапись = Новый ЗаписьТекста(имяФайла,Кодировка);           
	ТекстовыйФайлЗапись.ЗаписатьСтроку(текст);
	ТекстовыйФайлЗапись.Закрыть();
	
	Сообщить("Файл " + имяФайла + " создан.");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьПоСпискуПриИзменении(Элемент)
	Элементы.Период.Доступность = НЕ ВыгружатьПоСписку;
КонецПроцедуры
