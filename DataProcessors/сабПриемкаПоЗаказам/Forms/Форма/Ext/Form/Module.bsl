
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДатаНачала = ТекущаяДата();
	ДатаОкончания = ТекущаяДата();
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаНачала", НачалоДня(ДатаНачала));
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ДатаОкончания));
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ВсеКонтрагенты", Истина);
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("Контрагент", Неопределено);
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ВсеЗаказы", Ложь);
	
	Элементы.ПоказыватьЗакрытые.Пометка = Истина;
	
	Скан_РежимРучногоВводаКоличества = Истина;
	ПринятьПоЦене = 2;
	
	Если ЗначениеЗаполнено(ПараметрыСеанса.ТекущееПодразделение) Тогда
		Элементы.СписокЗаказыПодразделение.Видимость = Ложь;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
			
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗаказы;
	ПринятьПоЦенеПриИзменении(Неопределено);
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОбработкиПриИзменении(Элемент)
	
	//надо ли?
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Если ЗначениеЗаполнено(Элементы.СписокЗаказы.ТекущаяСтрока) Тогда
		Если Элементы.СписокЗаказы.ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовПоставщикам.Закрыт") Тогда
			ТекстВопроса = "Данный заказ имеет статус ""Закрыт"", вы уверены, что хотите повторно его обработать?
							|Это будет означать частичное поступление/возврат, если это не так, пометьте на удаление существующий подчиненный документ";
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Проведен = БюджетныйНаСервере.ВернутьРеквизит(Элементы.СписокЗаказы.ТекущаяСтрока, "Проведен");
		Если Не Проведен Тогда
			Предупреждение("Заказ не проведен, создать поступление невозможно");
			Возврат;
		КонецЕсли;
		ОбрабатываемыйДокумент = Элементы.СписокЗаказы.ТекущаяСтрока;
	Иначе
		Возврат;
	КонецЕсли;
	
	ПриемкаВосстановленаИзЧерновика = Ложь;
	Если ЕстьЗаписьВЧерновике(ОбрабатываемыйДокумент) Тогда
		Ответ = Вопрос("Приемка данного документа была сохранена в черновик. Восстановить?", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ВосстановитьИзЧерновикаНаСервере();
			ПриемкаВосстановленаИзЧерновика = Истина;
		КонецЕсли;
	КонецЕсли;
		
	ВидОперации = БюджетныйНаСервере.ВернутьРеквизит(Элементы.СписокЗаказы.ТекущаяСтрока, "ВидОперации");
	Если ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыЗаказов.ВозвратБрака") Тогда
		//Предупреждение("Механизм в разработке. На данный момент заказ обрабатывается управляющим/товароведом Заказ -> Создать на основании");
		//Возврат;
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеВозврата;
		Если Не ПриемкаВосстановленаИзЧерновика Тогда
			ЗаполнитьТаблицуВозврат();
		КонецЕсли;
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыЗаказов.ВнутреннееПеремещение") Тогда
		//Предупреждение("Механизм в разработке. На данный момент заказ обрабатывается управляющим/товароведом Заказ -> Создать на основании");
		//Возврат;
		
		Если Не ЗначениеЗаполнено(БюджетныйНаСервере.ВернутьРеквизит(ОбрабатываемыйДокумент, "ПредприятиеВн")) Или (БюджетныйНаСервере.ВернутьРеквизит(ОбрабатываемыйДокумент, "Предприятие") = БюджетныйНаСервере.ВернутьРеквизит(ОбрабатываемыйДокумент, "ПредприятиеВн")) Тогда
			ПеремещениеИз = ПолучитьПеремещениеНаПодразделение(ОбрабатываемыйДокумент);
			Если ПеремещениеИз = Неопределено Тогда
				Предупреждение("Не найдена отгрузка на ОП по этому заказу, приемка невозможна");
				Возврат;
			КонецЕсли;
		Иначе
			РеализацияВНХ = ПолучитьРеализациюВНХ(ОбрабатываемыйДокумент);
			Если Не ЗначениеЗаполнено(РеализацияВНХ) Тогда
				Предупреждение("Не найдена отгрузка на ОП по этому заказу, приемка невозможна");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПеремещения;
		Если Не ПриемкаВосстановленаИзЧерновика Тогда
			ЗаполнитьТаблицуПеремещение();
		КонецЕсли;
		
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления;
		Если Не ПриемкаВосстановленаИзЧерновика Тогда
			ЗаполнитьПланФакт();
		КонецЕсли;
		ПодключитьОбработчикОжидания("СохранитьВЧерновикНаКлиенте", 90);
		
	КонецЕсли;
		
	//ЭтапСозданиеПоступления = Истина;
	БылВызовПечати = Ложь;
	ЗаполнитьПодвалТЗ();
	УстановитьВидимость();
	
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления Тогда
		
		Если ЗначениеЗаполнено(СозданныйДокумент) И Не БылВызовПечати Тогда
			Ответ = Вопрос("Созданный документ не выводился на печать. Выйти в список заказов?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если  СозданныйДокумент = Неопределено И ТЗ_ПланФакт.Итог("Сумма") <> 0 Тогда
			Ответ = Вопрос("Сохранить в черновик введенные данные?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				СохранитьВЧерновикНаСервере();
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			ОтключитьОбработчикОжидания("СохранитьВЧерновикНаКлиенте");
		Исключение
		КонецПопытки;
		
		СуммаИтогоДок = "";
		НомерТТН = "";
		НомерСчФ = "";
		ДатаТТН = Дата('00010101');
		ДатаСчФ = Дата('00010101');
		ОбрабатываемыйДокумент = Неопределено;
		Элементы.ДекорацияСохранено.Видимость = Ложь;
		Элементы.СоздатьПоступление.Доступность = Истина;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПеремещения Тогда
		Если  СозданныйДокумент = Неопределено И ТЗ_Перемещение.Итог("КоличествоКОформлению") <> 0 Тогда
			Ответ = Вопрос("Сохранить в черновик введенные данные?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				СохранитьВЧерновикНаСервере();
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			ОтключитьОбработчикОжидания("СохранитьВЧерновикНаКлиенте");
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗаказы;
	//ОповеститьОбИзменении(ОбрабатываемыйДокумент);
	
	СозданныйДокумент = Неопределено;
	УстановитьВидимость();
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ПланФактПриИзменении(Элемент)
	
	РассчитатьСуммуПланФакта();
	ЗаполнитьПодвалТЗ();
	Элементы.ДекорацияСохранено.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьПоЦенеПриИзменении(Элемент)
	
	ШрифтЖирный = новый Шрифт(,, Истина);
	ШрифтПоУмолчанию = новый Шрифт();	
	Элементы.ТЗ_ПланФактЦенаЗаказ.Шрифт 		= ?(ПринятьПоЦене = 2, ШрифтЖирный, ШрифтПоУмолчанию);
	Элементы.ТЗ_ПланФактЦенаЗаказ.ШрифтЗаголовка= ?(ПринятьПоЦене = 2, ШрифтЖирный, ШрифтПоУмолчанию);
	Элементы.ТЗ_ПланФактЦенаДок.Шрифт 			= ?(ПринятьПоЦене = 3, ШрифтЖирный, ШрифтПоУмолчанию);
	Элементы.ТЗ_ПланФактЦенаДок.ШрифтЗаголовка	= ?(ПринятьПоЦене = 3, ШрифтЖирный, ШрифтПоУмолчанию);
	Элементы.ТЗ_ПланФактЦенаДокБезНДС.Шрифт 			= ?(ПринятьПоЦене = 3, ШрифтЖирный, ШрифтПоУмолчанию);
	Элементы.ТЗ_ПланФактЦенаДокБезНДС.ШрифтЗаголовка	= ?(ПринятьПоЦене = 3, ШрифтЖирный, ШрифтПоУмолчанию);
	
	ЦветАвто = новый Цвет();
	ЦветСерый = новый Цвет(230, 230, 230);
	Элементы.ТЗ_ПланФактЦенаДок.ЦветФона 			= ?(ПринятьПоЦене = 1, ЦветСерый, ЦветАвто);
	Элементы.ТЗ_ПланФактЦенаДок.ЦветФонаЗаголовка 	= ?(ПринятьПоЦене = 1, ЦветСерый, ЦветАвто);
	Элементы.ТЗ_ПланФактЦенаДок.ЦветФонаПодвала 	= ?(ПринятьПоЦене = 1, ЦветСерый, ЦветАвто);
	Элементы.ТЗ_ПланФактЦенаДокБезНДС.ЦветФона 			= ?(ПринятьПоЦене = 1, ЦветСерый, ЦветАвто);
	Элементы.ТЗ_ПланФактЦенаДокБезНДС.ЦветФонаЗаголовка = ?(ПринятьПоЦене = 1, ЦветСерый, ЦветАвто);
	Элементы.ТЗ_ПланФактЦенаДокБезНДС.ЦветФонаПодвала 	= ?(ПринятьПоЦене = 1, ЦветСерый, ЦветАвто);
	
	Элементы.ТЗ_ПланФактЦенаДок.ТолькоПросмотр 		 = (ПринятьПоЦене = 1);
	Элементы.ТЗ_ПланФактЦенаДокБезНДС.ТолькоПросмотр = (ПринятьПоЦене = 1);
	
	РассчитатьСуммуПланФакта();
	
КонецПроцедуры

#КонецОбласти

#Область ПодключениеИЗапросы


	
#КонецОбласти

#Область ЭтапСозданияПоступления

&НаКлиенте
Процедура ЗаполнитьПланФакт() Экспорт
	
	ЗаполнитьФакт();
	ЗаполнитьТаблицуПланФакт();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПланФакт()
	
	Запрос = новый Запрос;
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	Запрос.УстановитьПараметр("ДокументПлан", ОбрабатываемыйДокумент);
	Запрос.УстановитьПараметр("Предприятие", ОбрабатываемыйДокумент.Предприятие);
	Запрос.УстановитьПараметр("Подразделение", ОбрабатываемыйДокумент.Подразделение);
	Запрос.УстановитьПараметр("Контрагент", ОбрабатываемыйДокумент.Контрагент);
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	Запрос.УстановитьПараметр("ЦенаНаДатуПоставки", ОбрабатываемыйДокумент.Контрагент.УсловияЦенПоставки = Перечисления.УсловияЦенПоставки.НаДатуПоставки);	
	Запрос.УстановитьПараметр("ВТ_Факт", Товары.Выгрузить());	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ВТ_Факт.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	ВТ_Факт.Штрихкод КАК Штрихкод,
	               |	ВТ_Факт.КоличествоФакт КАК Количество
	               |ПОМЕСТИТЬ ВТ_Факт
	               |ИЗ
	               |	&ВТ_Факт КАК ВТ_Факт
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(ЗаказПоставщикуТабличнаяЧасть.Номенклатура, ВТ_Факт.Номенклатура) КАК Номенклатура,
	               |	ЗаказПоставщикуТабличнаяЧасть.Номенклатура.Код КАК Артикул,
	               |	ЗаказПоставщикуТабличнаяЧасть.Номенклатура.Наименование КАК Наименование,
	               |	ЗаказПоставщикуТабличнаяЧасть.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗаказПоставщикуТабличнаяЧасть.СтавкаНДС,
	               |	ЗаказПоставщикуТабличнаяЧасть.СтавкаНДС.Ставка КАК СтавкаНДССтавка,
	               |	ЕСТЬNULL(ЗаказПоставщикуТабличнаяЧасть.Количество, 0) КАК КоличествоЗаказ,
	               |	0 КАК КоличествоДок,
	               |	ЕСТЬNULL(ЗаказПоставщикуТабличнаяЧасть.Цена, 0) КАК ЦенаЗаказ,
	               |	ЕСТЬNULL(ЗаказПоставщикуТабличнаяЧасть.Цена, 0) КАК ЦенаДок,
	               |	ЕСТЬNULL(ВТ_Факт.Количество, 0) КАК КоличествоФакт,
	               |	ЕСТЬNULL(ВТ_Факт.Количество, 0) КАК КоличествоКОформлению,
	               |	ЕСТЬNULL(ВТ_Факт.Количество, 0) * ЕСТЬNULL(ЗаказПоставщикуТабличнаяЧасть.Цена, 0) КАК Сумма,
	               |	ЕСТЬNULL(ВТ_Факт.Количество, 0) * ЕСТЬNULL(ЗаказПоставщикуТабличнаяЧасть.Цена, 0) КАК СуммаДок,
	               |	ЕСТЬNULL(ВТ_Факт.Штрихкод, """") КАК Штрихкод,
	               |	ЗаказПоставщикуТабличнаяЧасть.Склад,
	               |	ВЫБОР
	               |		КОГДА ЗаказПоставщикуТабличнаяЧасть.Номенклатура ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ЕстьВЗаказе,
	               |	ЗаказПоставщикуТабличнаяЧасть.НомерСтроки
	               |ПОМЕСТИТЬ ВТ_Данные
	               |ИЗ
	               |	Документ.ЗаказПоставщику.ТабличнаяЧасть КАК ЗаказПоставщикуТабличнаяЧасть
	               |		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Факт КАК ВТ_Факт
	               |		ПО ЗаказПоставщикуТабличнаяЧасть.Номенклатура = ВТ_Факт.Номенклатура
	               |ГДЕ
	               |	ЗаказПоставщикуТабличнаяЧасть.Ссылка = &ДокументПлан
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МаркетинговыеМероприятия.ЦенаПоступления,
	               |	МаркетинговыеМероприятия.Номенклатура,
	               |	МаркетинговыеМероприятия.Предприятие,
	               |	МаркетинговыеМероприятия.Подразделение,
	               |	МаркетинговыеМероприятия.ДатаДокумента
	               |ПОМЕСТИТЬ ВТ_МаркетинговыеМероприятия
	               |ИЗ
	               |	РегистрСведений.МаркетинговыеМероприятия КАК МаркетинговыеМероприятия
	               |ГДЕ
	               |	МаркетинговыеМероприятия.Предприятие = &Предприятие
	               |	И МаркетинговыеМероприятия.Подразделение = &Подразделение
	               |	И МаркетинговыеМероприятия.ДатаПоставокНач <= &Период
	               |	И МаркетинговыеМероприятия.ДатаПоставокКон >= &Период
	               |	И МаркетинговыеМероприятия.ЦенаПоступления <> 0
	               |	И МаркетинговыеМероприятия.Номенклатура В
	               |			(ВЫБРАТЬ
	               |				ВТ_Данные.Номенклатура
	               |			ИЗ
	               |				ВТ_Данные КАК ВТ_Данные)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Данные.Номенклатура,
	               |	ВТ_Данные.Артикул,
	               |	ВТ_Данные.Наименование,
	               |	ВТ_Данные.ЕдиницаИзмерения,
	               |	ВТ_Данные.СтавкаНДС,
	               |	ВТ_Данные.СтавкаНДССтавка,
	               |	ВТ_Данные.КоличествоЗаказ,
	               |	ВТ_Данные.КоличествоДок,
	               |	ВЫБОР
	               |		КОГДА &ЦенаНаДатуПоставки
	               |			ТОГДА ЕСТЬNULL(ЕСТЬNULL(ВТ_МаркетинговыеМероприятия.ЦенаПоступления, ЦеныНоменклатурыСрезПоследних.Цена), ВТ_Данные.ЦенаЗаказ)
	               |		ИНАЧЕ ВТ_Данные.ЦенаЗаказ
	               |	КОНЕЦ КАК ЦенаЗаказ,
	               |	ВЫБОР
	               |		КОГДА &ЦенаНаДатуПоставки
	               |			ТОГДА ЕСТЬNULL(ЕСТЬNULL(ВТ_МаркетинговыеМероприятия.ЦенаПоступления, ЦеныНоменклатурыСрезПоследних.Цена), ВТ_Данные.ЦенаДок)
	               |		ИНАЧЕ ВТ_Данные.ЦенаДок
	               |	КОНЕЦ КАК ЦенаДок,
	               |	ВТ_Данные.КоличествоФакт,
	               |	ВТ_Данные.КоличествоКОформлению,
	               |	ВЫБОР
	               |		КОГДА &ЦенаНаДатуПоставки
	               |			ТОГДА ВТ_Данные.КоличествоФакт * ЕСТЬNULL(ЕСТЬNULL(ВТ_МаркетинговыеМероприятия.ЦенаПоступления, ЦеныНоменклатурыСрезПоследних.Цена), ВТ_Данные.ЦенаЗаказ)
	               |		ИНАЧЕ ВТ_Данные.Сумма
	               |	КОНЕЦ КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА &ЦенаНаДатуПоставки
	               |			ТОГДА ВТ_Данные.КоличествоФакт * ЕСТЬNULL(ЕСТЬNULL(ВТ_МаркетинговыеМероприятия.ЦенаПоступления, ЦеныНоменклатурыСрезПоследних.Цена), ВТ_Данные.ЦенаДок)
	               |		ИНАЧЕ ВТ_Данные.СуммаДок
	               |	КОНЕЦ КАК СуммаДок,
	               |	ВТ_Данные.Штрихкод,
	               |	ВТ_Данные.Склад,
	               |	ВТ_Данные.ЕстьВЗаказе,
	               |	ВТ_Данные.НомерСтроки
	               |ИЗ
	               |	ВТ_Данные КАК ВТ_Данные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МаркетинговыеМероприятия КАК ВТ_МаркетинговыеМероприятия
	               |		ПО ВТ_Данные.Номенклатура = ВТ_МаркетинговыеМероприятия.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	               |				&Период,
	               |				Предприятие = &Предприятие
	               |					И Подразделение = &Подразделение
	               |					И ВидЦены.ЦенаПоставщика
	               |					И ВидЦены.Поставщик = &Контрагент) КАК ЦеныНоменклатурыСрезПоследних
	               |		ПО ВТ_Данные.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура";
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	ТЗ_ПланФакт.Загрузить(ТаблицаРезультат);
	
	Для Каждого ТекСтрока Из ТЗ_ПланФакт Цикл  //пока так, потом в запросе рассчитать
		ТекСтрока.ЦенаДокБезНДС = ТекСтрока.ЦенаДок / (1 + ТекСтрока.СтавкаНДССтавка/100);
		ТекСтрока.СуммаДок 		= ТекСтрока.КоличествоДок * ТекСтрока.ЦенаДок;
		ТекСтрока.СуммаНДСДок 	= ТекСтрока.СуммаДок / ((100+ТекСтрока.СтавкаНДССтавка)/100) * (ТекСтрока.СтавкаНДССтавка/100);
	КонецЦикла;
	
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФакт()
	
	Товары.Очистить();
	//Загрузка факта с ТСД
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВозврат()
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПоставщикуТабличнаяЧасть.Ссылка,
	               |	ЗаказПоставщикуТабличнаяЧасть.Номенклатура,
	               |	ЗаказПоставщикуТабличнаяЧасть.Склад,
	               |	ЗаказПоставщикуТабличнаяЧасть.Количество КАК КоличествоЗаказ,
	               |	ЗаказПоставщикуТабличнаяЧасть.Количество КАК КоличествоКОформлению,
	               |	ЗаказПоставщикуТабличнаяЧасть.Цена,
	               |	ЗаказПоставщикуТабличнаяЧасть.Сумма,
	               |	ЗаказПоставщикуТабличнаяЧасть.Доставка,
	               |	ЗаказПоставщикуТабличнаяЧасть.СуммаДоставки,
	               |	ЗаказПоставщикуТабличнаяЧасть.ВидДоставки,
	               |	ЗаказПоставщикуТабличнаяЧасть.СуммаБезНДС,
	               |	ЗаказПоставщикуТабличнаяЧасть.СтавкаНДС,
	               |	ЗаказПоставщикуТабличнаяЧасть.СуммаНДС,
	               |	ЗаказПоставщикуТабличнаяЧасть.СтавкаНДС.Ставка КАК СтавкаНДССтавка
	               |ИЗ
	               |	Документ.ЗаказПоставщику.ТабличнаяЧасть КАК ЗаказПоставщикуТабличнаяЧасть
	               |ГДЕ
	               |	ЗаказПоставщикуТабличнаяЧасть.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ОбрабатываемыйДокумент);
	ТЗ_ТоварыВозврат.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПеремещение() 
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПоставщикуТабличнаяЧасть.Ссылка,
	               |	ЗаказПоставщикуТабличнаяЧасть.Номенклатура,
	               |	СУММА(ЗаказПоставщикуТабличнаяЧасть.Количество) КАК Количество,
	               |	СУММА(ЗаказПоставщикуТабличнаяЧасть.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ВТ_Заказ
	               |ИЗ
	               |	Документ.ЗаказПоставщику.ТабличнаяЧасть КАК ЗаказПоставщикуТабличнаяЧасть
	               |ГДЕ
	               |	ЗаказПоставщикуТабличнаяЧасть.Ссылка = &Заказ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаказПоставщикуТабличнаяЧасть.Ссылка,
	               |	ЗаказПоставщикуТабличнаяЧасть.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УЧ_ПеремещениеТоваровТабличнаяЧасть.Ссылка КАК Ссылка,
	               |	УЧ_ПеремещениеТоваровТабличнаяЧасть.Номенклатура КАК Номенклатура,
	               |	УЧ_ПеремещениеТоваровТабличнаяЧасть.Количество КАК Количество,
	               |	ВЫБОР
	               |		КОГДА УЧ_ПеремещениеТоваровТабличнаяЧасть.Количество <> 0
	               |			ТОГДА УЧ_ПеремещениеТоваровТабличнаяЧасть.Сумма / УЧ_ПеремещениеТоваровТабличнаяЧасть.Количество
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Цена,
	               |	УЧ_ПеремещениеТоваровТабличнаяЧасть.Сумма КАК Сумма,
	               |	УЧ_ПеремещениеТоваровТабличнаяЧасть.СчетУчетаБУ КАК СчетУчетаБУ,
	               |	УЧ_ПеремещениеТоваровТабличнаяЧасть.НовыйСчетУчетаБУ КАК НовыйСчетУчетаБУ
	               |ПОМЕСТИТЬ ВТ_ПеремещениеИз
	               |ИЗ
	               |	Документ.УЧ_ПеремещениеТоваров.ТабличнаяЧасть КАК УЧ_ПеремещениеТоваровТабличнаяЧасть
	               |ГДЕ
	               |	УЧ_ПеремещениеТоваровТабличнаяЧасть.Ссылка.ДокОснование = &Заказ
	               |	И НЕ УЧ_ПеремещениеТоваровТабличнаяЧасть.Ссылка.ПометкаУдаления
	               |	И УЧ_ПеремещениеТоваровТабличнаяЧасть.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыПеремещений.ПеремещениеНаОбособленноеПодразделение)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УЧ_РеализацияРеализация.Ссылка,
	               |	УЧ_РеализацияРеализация.Наименование КАК Номенклатура,
	               |	УЧ_РеализацияРеализация.Количество,
	               |	УЧ_РеализацияРеализация.СуммаОтгрузки,
	               |	ВЫБОР
	               |		КОГДА УЧ_РеализацияРеализация.Количество = 0
	               |			ТОГДА 0
	               |		ИНАЧЕ УЧ_РеализацияРеализация.СуммаОтгрузки / УЧ_РеализацияРеализация.Количество
	               |	КОНЕЦ КАК Цена
	               |ПОМЕСТИТЬ ВТ_РеализацииВНХ
	               |ИЗ
	               |	Документ.УЧ_Реализация.Реализация КАК УЧ_РеализацияРеализация
	               |ГДЕ
	               |	НЕ УЧ_РеализацияРеализация.Ссылка.ПометкаУдаления
	               |	И УЧ_РеализацияРеализация.Ссылка.ДокОснование = &Заказ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(ВТ_Заказ.Номенклатура, ЕСТЬNULL(ВТ_ПеремещениеИз.Номенклатура, ВТ_РеализацииВНХ.Номенклатура)) КАК Номенклатура,
	               |	ВТ_Заказ.Количество КАК КоличествоЗаказ,
	               |	ЕСТЬNULL(ВТ_ПеремещениеИз.Количество, ВТ_РеализацииВНХ.Количество) КАК КоличествоОтправлено,
	               |	ЕСТЬNULL(ВТ_ПеремещениеИз.Количество, ВТ_РеализацииВНХ.Количество) КАК КоличествоКОформлению,
	               |	ЕСТЬNULL(ВТ_ПеремещениеИз.Цена, ВТ_РеализацииВНХ.Цена) КАК Цена,
	               |	ЕСТЬNULL(ВТ_ПеремещениеИз.Количество, ВТ_РеализацииВНХ.Количество) * ЕСТЬNULL(ВТ_ПеремещениеИз.Цена, ВТ_РеализацииВНХ.Цена) КАК Сумма,
	               |	ВТ_ПеремещениеИз.СчетУчетаБУ,
	               |	ВТ_ПеремещениеИз.НовыйСчетУчетаБУ
	               |ИЗ
	               |	ВТ_Заказ КАК ВТ_Заказ
	               |		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ПеремещениеИз КАК ВТ_ПеремещениеИз
	               |		ПО ВТ_Заказ.Номенклатура = ВТ_ПеремещениеИз.Номенклатура
	               |		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_РеализацииВНХ КАК ВТ_РеализацииВНХ
	               |		ПО ВТ_Заказ.Номенклатура = ВТ_РеализацииВНХ.Номенклатура";	
	Запрос.УстановитьПараметр("Заказ", ОбрабатываемыйДокумент);
	ТЗ_Перемещение.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоступление(Команда)
	
	Если ТЗ_ПланФакт.Итог("Сумма") = 0 Тогда
		Предупреждение("Таблица не заполнена");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СозданныйДокумент) Тогда
		Сообщить("Документ поступления уже был создан");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НомерТТН) Или Не ЗначениеЗаполнено(ДатаТТН) Или Не ЗначениеЗаполнено(СуммаИтогоДок) Тогда
		Предупреждение("Заполните номер/сумму входящего документа");
		Возврат;
	КонецЕсли;
	
	Если Не (Год(ТекущаяДата()) = Год(ДатаТТН) + 1 Или Год(ТекущаяДата()) = Год(ДатаТТН)) Тогда
		СообщениеПользователю = новый СообщениеПользователю;
		СообщениеПользователю.Поле = "ДатаТТН";
		СообщениеПользователю.Текст = "Некорректный год";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НомерСчФ) Тогда
		СообщениеПользователю = новый СообщениеПользователю;
		СообщениеПользователю.Поле = "НомерСчФ";
		СообщениеПользователю.Текст = "Заполните номер счета-фактуры";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Не (Год(ТекущаяДата()) = Год(ДатаСчФ) + 1 Или Год(ТекущаяДата()) = Год(ДатаСчФ)) Тогда
		СообщениеПользователю = новый СообщениеПользователю;
		СообщениеПользователю.Поле = "ДатаСчФ";
		СообщениеПользователю.Текст = "Некорректный год";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекСтрока Из ТЗ_ПланФакт Цикл
		Если ТекСтрока.КоличествоКОформлению = 0 Тогда
			Ответ = Вопрос("отсутствуют некоторые позиции из заказа, Продолжить?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			Иначе 
				Прервать;
			КонецЕсли;
		КонецЕсли;
		Если ?(ПринятьПоЦене = 2, ТекСтрока.ЦенаДок, ТекСтрока.ЦенаЗаказ) = 0 Или Не ТекСтрока.ЕстьВЗаказе Тогда
			Сообщить("В строке с номенклатурой " + ТекСтрока.Номенклатура + " не заполнена цена!");
			Возврат;
		КонецЕсли;
		Если ТекСтрока.КоличествоДок = 0 И ТекСтрока.КоличествоКОформлению <> 0 Тогда
			Сообщить("В строке с номенклатурой " + ТекСтрока.Номенклатура + " не заполнено количество по ТТН!");
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	//ЗаполнитьТаблицуПланФакт();
	
	СоздатьПоступлениеНаСервере();
	
	//ЭтапСозданиеПоступления = Ложь;
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьПоступлениеНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ОбрабатываемыйДокумент);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	*
	               |ИЗ
	               |	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	               |ГДЕ
	               |	ЗаказПоставщику.Ссылка = &Ссылка";
	РеквизитыЗаказа = Запрос.Выполнить().Выбрать();
	Если РеквизитыЗаказа.Следующий() = Ложь Тогда
		Сообщить("Не найден заказ");
		Возврат;
	КонецЕсли;
	
	ПоступлениеОбъект = Документы.УЧ_ПоступлениеТоваров.СоздатьДокумент();
	ПоступлениеОбъект.Дата = ТекущаяДата();
	ПоступлениеОбъект.Валюта = Справочники.Валюты.НациональнаяВалюта;
	ПоступлениеОбъект.ДокОснование = ОбрабатываемыйДокумент;
	ПоступлениеОбъект.ВидОперации = Перечисления.ВидыПоступлений.Поступление;
	ПоступлениеОбъект.НомерВходящегоДокумента = НомерТТН;
	ПоступлениеОбъект.НомерВходящегоСчФ 	  = НомерСчФ;
	ПоступлениеОбъект.ДатаВходящегоДокумента  = ДатаТТН;
	ПоступлениеОбъект.ДатаВходящегоСчФ		  = ДатаСчФ;
	ПоступлениеОбъект.СуммаВходящегоДокумента = СуммаИтогоДок;
	ПоступлениеОбъект.СуммаВключаетНДС = Истина;
	ПоступлениеОбъект.ЦенаВключаетНДС = Истина;
	ПоступлениеОбъект.УчитыватьНДС = Истина;
	ЗаполнитьЗначенияСвойств(ПоступлениеОбъект, РеквизитыЗаказа, "Предприятие, Организация, Подразделение, Контрагент, Договор, Склад");
	СчетКонтрагента = ПоступлениеОбъект.Договор.СчетВзаиморасчетов;
	Если ЗначениеЗаполнено(СчетКонтрагента) Тогда
		ПоступлениеОбъект.СчетКонтрагента = СчетКонтрагента;
	Иначе
		ПоступлениеОбъект.СчетКонтрагента = ПланыСчетов.Учетный.ПоставщикиОсн;
	КонецЕсли;
	
	Если ПоступлениеОбъект.СчетКонтрагента = ПланыСчетов.Учетный.ВНХТоварные Тогда
		ПоступлениеОбъект.ПредприятиеПоставщик = ПоступлениеОбъект.Контрагент.ПредприятиеХодлинга;
		ПоступлениеОбъект.ДоговорВн = ПоступлениеОбъект.Договор.ВнутрихолдинговыйДоговор;
	КонецЕсли;	
	
	Для Каждого ТекСтрока Из ТЗ_ПланФакт Цикл
		Если ТекСтрока.КоличествоДок <> 0 Или ТекСтрока.КоличествоКОформлению <> 0 Тогда
			НоваяСтрокаТЧ = ПоступлениеОбъект.ТабличнаяЧасть.Добавить();
			НоваяСтрокаТЧ.Склад 		= ТекСтрока.Склад;
			НоваяСтрокаТЧ.Номенклатура 	= ТекСтрока.Номенклатура;
			НоваяСтрокаТЧ.СрокГодности 	= ТекСтрока.СрокГодности;
			НоваяСтрокаТЧ.КоличествоПоПервичнымДокументам 	= ТекСтрока.КоличествоДок;
			НоваяСтрокаТЧ.Количество 						= ТекСтрока.КоличествоКОформлению;
			НоваяСтрокаТЧ.Цена 								= ?(ПринятьПоЦене = 3, ТекСтрока.ЦенаДок, ТекСтрока.ЦенаЗаказ);
			НоваяСтрокаТЧ.СтавкаНДС 						= НоваяСтрокаТЧ.Номенклатура.СтавкаНДС;
			НоваяСтрокаТЧ.Сумма 							= НоваяСтрокаТЧ.Количество * НоваяСтрокаТЧ.Цена;
			НоваяСтрокаТЧ.СуммаПоПервичнымДокументам		= ТекСтрока.СуммаДок;
			НоваяСтрокаТЧ.СуммаНДС							= ТекСтрока.Сумма / ((100+ТекСтрока.СтавкаНДССтавка)/100) * (ТекСтрока.СтавкаНДССтавка/100);
			НоваяСтрокаТЧ.СуммаНДСПоПервичнымДокументам		= ТекСтрока.СуммаНДСДок;
			НоваяСтрокаТЧ.ШтрихКод							= ТекСтрока.ШтрихКод;
		КонецЕсли;
		
		Если ТекСтрока.КоличествоБонуснойПродукции <> 0 Тогда
			НоваяСтрокаБонус = ПоступлениеОбъект.БонуснаяПродукция.Добавить();
			НоваяСтрокаБонус.Количество = ТекСтрока.КоличествоБонуснойПродукции;
			НоваяСтрокаБонус.Номенклатура = ТекСтрока.Номенклатура;
			ВидЦены = сабОперОбщегоНазначения.ПолучитьВидЦеныПоставщика(НоваяСтрокаБонус.Номенклатура, ПоступлениеОбъект.Предприятие, ПоступлениеОбъект.Подразделение, ПоступлениеОбъект.Контрагент, ПоступлениеОбъект.Дата);
			НоваяСтрокаБонус.Цена = сабОперОбщегоНазначения.РассчитатьЦенуПоВидуЦен(ВидЦены,НоваяСтрокаБонус.Номенклатура, ПоступлениеОбъект.Предприятие, ПоступлениеОбъект.Подразделение, ПоступлениеОбъект.Контрагент, ПоступлениеОбъект.Дата, ОбрабатываемыйДокумент.Дата); 
			НоваяСтрокаБонус.Сумма = НоваяСтрокаБонус.Количество * НоваяСтрокаБонус.Цена;  
		КонецЕсли;	
		
		//НоваяСтрокаТЧ.СуммаБезНДС   //потом
		//НоваяСтрокаТЧ.СуммаНДС	
	КонецЦикла;
	
	Попытка //на всякий случай
		ДопРеквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Создан обработкой заказов");
		Если ЗначениеЗаполнено(ДопРеквизит) Тогда
			НоваяСтрока = ПоступлениеОбъект.ДополнительныеРеквизиты.Добавить();
			НоваяСтрока.Свойство = ДопРеквизит;
			НоваяСтрока.Значение = Истина;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	//д1. подбираем характеристики по штрихкодам
	ЗаполнитьХарактеристики(ПоступлениеОбъект);
	
	Попытка
		ПоступлениеОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Сообщить("Не удалось провести поступление! " + ОписаниеОшибки());
		//ПоступлениеОбъект.Записать(РежимЗаписиДокумента.Запись);
		Возврат;
	КонецПопытки;
	//Сообщить("Создан документ " + ПоступлениеОбъект.Ссылка);
	СозданныйДокумент = ПоступлениеОбъект.Ссылка;
	
	//удаляем черновик
	УдалитьИзЧерновиковНаСервере();
		
	//меняем статус заказа
	Если Не ОбрабатываемыйДокумент.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт Тогда		
		ЗаказОб = ОбрабатываемыйДокумент.ПолучитьОбъект();
		ЗаказОб.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт;
		ЗаказОб.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.АвтосохранениеДанных.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	МенеджерЗаписи.Объект		= ОбрабатываемыйДокумент;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Удалить();
	
	Элементы.СоздатьПоступление.Доступность = Ложь;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьХарактеристики(ПоступлениеОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Характеристика,
	               |	ШтрихкодыНоменклатуры.Штрихкод
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |ГДЕ
	               |	ШтрихкодыНоменклатуры.Штрихкод В(&Штрихкоды)";
	
	Запрос.УстановитьПараметр("Штрихкоды", ПоступлениеОбъект.ТабличнаяЧасть.Выгрузить().ВыгрузитьКолонку("Штрихкод"));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Соотв = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Соотв.Вставить(Выборка.Штрихкод, Выборка.Характеристика);	
	КонецЦикла;
	
	Для каждого ТекСтрока Из ПоступлениеОбъект.ТабличнаяЧасть Цикл
		ТекСтрока.Характеристика = Соотв.Получить(ТекСтрока.Штрихкод);	
	КонецЦикла; 
	
	
КонецПроцедуры


&НаКлиенте
Процедура СоздатьВозврат(Команда)

	Если ТЗ_ТоварыВозврат.Итог("Сумма") = 0 Тогда
		Предупреждение("Таблица не заполнена");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СозданныйДокумент) Тогда
		Сообщить("Документ поступления уже был создан");
		Возврат;
	КонецЕсли;
	
	СоздатьВозвратНаСервере();
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьВозвратНаСервере()
	
	ВозвратОбъект = Документы.УЧ_ВозвратТоваровПоставщику.СоздатьДокумент();
	
	ЗаполнитьЗначенияСвойств(ВозвратОбъект, ОбрабатываемыйДокумент, , "Дата, Номер");
	ВозвратОбъект.Дата = ТекущаяДата();
	ВозвратОбъект.СчетКонтрагента = ПланыСчетов.Учетный.ПоставщикиОсн;
	ВозвратОбъект.ДокОснование = ОбрабатываемыйДокумент;
	Для Каждого ТекСтрока Из ТЗ_ТоварыВозврат Цикл
		Если Не ЗначениеЗаполнено(ТекСтрока.КоличествоКОформлению) Тогда
			Продолжить;
		КонецЕсли;	
		СтрокаТЧ = ВозвратОбъект.Товары.Добавить();
		СтрокаТЧ.Количество = ТекСтрока.КоличествоКОформлению;
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ТекСтрока);
	КонецЦикла;
	
	//склад возврата
	ВозвратОбъект.Склад = сабОперОбщегоНазначения.ПолучитьСкладПоДопСвойству(ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Склад возврата (Склады)", Истина), ВозвратОбъект.Предприятие);
	Если Не ЗначениеЗаполнено(ВозвратОбъект.Склад) Тогда
		ВозвратОбъект.Склад = ОбрабатываемыйДокумент.Склад;	
	КонецЕсли;
	
	Попытка
		ВозвратОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Сообщить("Не удалось провести возврат! " + ОписаниеОшибки());
		//ВозвратОбъект.Записать(РежимЗаписиДокумента.Запись);
		Возврат;
	КонецПопытки;
	Сообщить("Создан документ " + ВозвратОбъект.Ссылка);
	СозданныйДокумент = ВозвратОбъект.Ссылка;
		
	//меняем статус заказа
	Если Не ОбрабатываемыйДокумент.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт Тогда		
		ЗаказОб = ОбрабатываемыйДокумент.ПолучитьОбъект();
		ЗаказОб.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт;
		ЗаказОб.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПеремещение(Команда)
	
	Если ТЗ_Перемещение.Итог("КоличествоКОформлению") = 0 Тогда
		Предупреждение("Таблица не заполнена");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СозданныйДокумент) Тогда
		Сообщить("Документ поступления уже был создан");
		Возврат;
	КонецЕсли;
	
	СоздатьПеремещениеНаСервере();
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьПеремещениеНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ОбрабатываемыйДокумент.Предприятие = ОбрабатываемыйДокумент.ПредприятиеВн Тогда
		ПеремещениеИз = ПолучитьПеремещениеНаПодразделение(ОбрабатываемыйДокумент);
		Если ПеремещениеИз = Неопределено Тогда
			Сообщить("Не найдена отгрузка по этому заказу");
			Возврат;
		КонецЕсли;
		
		ПеремещениеОбъект = Документы.УЧ_ПеремещениеТоваров.СоздатьДокумент();
		ПеремещениеОбъект.Дата = ТекущаяДата();
		ПеремещениеОбъект.ВидОперации = Перечисления.ВидыПеремещений.ПоступлениеСОбособленногоПодразделения;
		ПеремещениеОбъект.ДокОснование = ПеремещениеИз;
		
		ЗаполнитьЗначенияСвойств(ПеремещениеОбъект, ПеремещениеИз, ,"Дата, Номер, Комментарий, ДокОснование, ВидОперации");
		
		Для Каждого ТекСтрока Из ТЗ_Перемещение Цикл
			Если Не ЗначениеЗаполнено(ТекСтрока.КоличествоКОформлению) Или Не ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;	
			НоваяСтрока = ПеремещениеОбъект.ТабличнаяЧасть.Добавить();
			НоваяСтрока.Количество 		 = ТекСтрока.КоличествоКОформлению;
			НоваяСтрока.Номенклатура	 = ТекСтрока.Номенклатура;
			НоваяСтрока.Сумма 			 = ТекСтрока.Сумма;
			НоваяСтрока.СчетУчетаБУ 	 = ТекСтрока.НовыйСчетУчетаБУ;
			НоваяСтрока.НовыйСчетУчетаБУ = ТекСтрока.СчетУчетаБУ;
			НоваяСтрока.ШтрихКод 		 = ТекСтрока.ШтрихКод;
		КонецЦикла;
	Иначе
		РеализацияВНХ = ПолучитьРеализациюВНХ(ОбрабатываемыйДокумент);
		Если Не ЗначениеЗаполнено(РеализацияВНХ) Тогда
			Сообщить("Не найдена отгрузка по этому заказу");
			Возврат;
		КонецЕсли;
		
		ПеремещениеОбъект = Документы.УЧ_ПоступлениеТоваров.СоздатьДокумент();
		ПеремещениеОбъект.Дата = ТекущаяДата();
		ПеремещениеОбъект.СчетКонтрагента = ПланыСчетов.Учетный.ВНХТоварные;
		ПеремещениеОбъект.ДокОснование = РеализацияВНХ;
		ПеремещениеОбъект.Предприятие = РеализацияВНХ.Контрагент;
		ПеремещениеОбъект.Подразделение = РеализацияВНХ.ПодразделениеВн;
		ПеремещениеОбъект.Организация = ПеремещениеОбъект.Подразделение.Организация;
		ПеремещениеОбъект.Контрагент = РеализацияВНХ.Предприятие;
		ПеремещениеОбъект.Договор = ПолучитьВнутреннийДоговор(ПеремещениеОбъект.Контрагент, ПеремещениеОбъект.Предприятие);
		ПеремещениеОбъект.ПодразделениеВн = РеализацияВНХ.ВидДеятельности;
		ПеремещениеОбъект.ОрганизацияВн = ПеремещениеОбъект.ПодразделениеВн.Организация;
		ПеремещениеОбъект.Склад = ПеремещениеОбъект.Подразделение.Склад;
		
		Для Каждого ТекСтрока Из ТЗ_Перемещение Цикл
			Если Не ЗначениеЗаполнено(ТекСтрока.КоличествоКОформлению) Или Не ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;	
			НоваяСтрока = ПеремещениеОбъект.ТабличнаяЧасть.Добавить();
			НоваяСтрока.Количество 		 = ТекСтрока.КоличествоКОформлению;
			НоваяСтрока.Цена             = ТекСтрока.Цена;
			НоваяСтрока.Номенклатура	 = ТекСтрока.Номенклатура;
			НоваяСтрока.СтавкаНДС 		 = ТекСтрока.Номенклатура.СтавкаНДС;
			НоваяСтрока.СуммаБезНДС 	 = 100 * ТекСтрока.Сумма / (100 + НоваяСтрока.СтавкаНДС.Ставка);
			НоваяСтрока.СуммаНДС 		 = НоваяСтрока.СуммаБезНДС * НоваяСтрока.СтавкаНДС.Ставка / 100;
			НоваяСтрока.Сумма 			 = ТекСтрока.Сумма;
			НоваяСтрока.ШтрихКод 		 = ТекСтрока.ШтрихКод;
		КонецЦикла;
	КонецЕсли;	
	
	Попытка
		ПеремещениеОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Сообщить("Не удалось провести перемещение! " + ОписаниеОшибки());
		//ПеремещениеОбъект.Записать(РежимЗаписиДокумента.Запись);
		Возврат;
	КонецПопытки;
	Сообщить("Создан документ " + ПеремещениеОбъект.Ссылка);
	СозданныйДокумент = ПеремещениеОбъект.Ссылка;
		
	//меняем статус заказа
	Если Не ОбрабатываемыйДокумент.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт Тогда		
		ЗаказОб = ОбрабатываемыйДокумент.ПолучитьОбъект();
		ЗаказОб.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт;
		ЗаказОб.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НасервереБезКонтекста
Функция ПолучитьВнутреннийДоговор(ТекПредприятиеВн, ТекПредприятие)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВнутренниеДоговоры.Ссылка
	|ИЗ
	|	Справочник.ВнутренниеДоговоры КАК ВнутренниеДоговоры
	|ГДЕ
	|	ВнутренниеДоговоры.Владелец = &Владелец
	|	И ВнутренниеДоговоры.Предприятие = &Предприятие
	|	И НЕ ВнутренниеДоговоры.ПометкаУдаления";
	Запрос.УстановитьПараметр("Владелец", ТекПредприятиеВн);
	Запрос.УстановитьПараметр("Предприятие", ТекПредприятие);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;	
	
	Возврат Справочники.ВнутренниеДоговоры.ПустаяСсылка();
	
КонецФункции

&НасервереБезКонтекста
Функция ПолучитьРеализациюВНХ(Заказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УЧ_Реализация.Ссылка
	|ИЗ
	|	Документ.УЧ_Реализация КАК УЧ_Реализация
	|ГДЕ
	|	УЧ_Реализация.ДокОснование = &Заказ
	|	И НЕ УЧ_Реализация.ПометкаУдаления";
	Запрос.УстановитьПараметр("Заказ", Заказ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
	Возврат Документы.УЧ_Реализация.ПустаяСсылка();
	
КонецФункции	

&НасервереБезКонтекста
Функция ПолучитьПеремещениеНаПодразделение(Заказ)
	
	Запрос = новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Заказ);
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ЗаказПоставщику.Ссылка КАК ЗаказСсылка,
	               |	УЧ_ПеремещениеТоваров.Ссылка КАК ПеремещениеСсылка
	               |ИЗ
	               |	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УЧ_ПеремещениеТоваров КАК УЧ_ПеремещениеТоваров
	               |		ПО ЗаказПоставщику.Ссылка = УЧ_ПеремещениеТоваров.ДокОснование
	               |ГДЕ
	               |	ЗаказПоставщику.Ссылка = &Ссылка
	               |	И НЕ УЧ_ПеремещениеТоваров.ПометкаУдаления
	               |	И УЧ_ПеремещениеТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыПеремещений.ПеремещениеНаОбособленноеПодразделение)";

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ПеремещениеСсылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийТЧ

&НаКлиенте
Процедура Скан_НоменклатураПриИзменении(Элемент)
	
	Если Скан_Количество = 0 Тогда
		Возврат;
	КонецЕсли;

	Если Скан_РежимРучногоВводаКоличества Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Количество;
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Штрихкод;
		Если ПроверкаНаличияСканНоменклатурыВЗаказе() Тогда
			ДобавитьСканНоменклатуруВТовары();
			РассчитатьСуммуПланФакта();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Скан_КоличествоПриИзменении(Элемент)
	
	Если Скан_Количество > 1000000 Тогда
		Скан_Количество = 0;
		Предупреждение("Слишком большое количество, введите еще раз");
		ПодключитьОбработчикОжидания("АктивироватьПолеСканКоличество", 0.1, Истина);		
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(Скан_Штрихкод) И ПроверкаНаличияСканНоменклатурыВЗаказе() Тогда
		ДобавитьСканНоменклатуруВТовары();
		РассчитатьСуммуПланФакта();
		
		СтрокиШтрихкод = ТЗ_ПланФакт.НайтиСтроки(новый Структура("Штрихкод", Скан_Штрихкод));
		Если СтрокиШтрихкод.Количество() > 0 Тогда
			Элементы.ТЗ_ПланФакт.ТекущаяСтрока = СтрокиШтрихкод[0].ПолучитьИдентификатор();
		КонецЕсли;
		ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Штрихкод;
		ЗаполнитьПодвалТЗ();
		
		Скан_Штрихкод = "";
		Скан_Номенклатура = Неопределено;
		Скан_Количество	= 0;
		Скан_КоличествоБонуснойПродукции = 0;
		Элементы.Скан_Номенклатура.Вид = ВидПоляФормы.ПолеНадписи;	
	КонецЕсли;
	
			
КонецПроцедуры

&НаКлиенте
Процедура Скан_ВводКоличестваПриИзменении(Элемент)
	
	//Элементы.Скан_Количество.Видимость = Скан_РежимРучногоВводаКоличества;
	Если Скан_РежимРучногоВводаКоличества Тогда
		Элементы.Скан_Количество.Доступность = Истина;
		Скан_Количество = 0;
	Иначе
		Элементы.Скан_Количество.Доступность = Ложь;
		Скан_Количество = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Скан_ШтрихкодПриИзменении(Элемент)
	
	СтруктураНоменклатура = сабОперОбщегоНазначения.ПодобратНоменклатуруПоШК(Скан_Штрихкод);
	Если ТипЗнч(СтруктураНоменклатура) = Тип("Структура") Тогда
		Скан_Номенклатура = СтруктураНоменклатура.Номенклатура;
		Скан_Количество   = СтруктураНоменклатура.Количество;
	Иначе
		Скан_Номенклатура = СтруктураНоменклатура;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Скан_Номенклатура) Тогда
		Элементы.Скан_Номенклатура.Вид = ВидПоляФормы.ПолеНадписи;	
		Если Скан_РежимРучногоВводаКоличества Тогда
			ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Количество;
		Иначе
			ПодключитьОбработчикОжидания("АктивироватьПолеСканШтрихкод", 0.1, Истина);		
			Если ПроверкаНаличияСканНоменклатурыВЗаказе() Тогда
				ДобавитьСканНоменклатуруВТовары();
				РассчитатьСуммуПланФакта();
				СтрокиШтрихкод = ТЗ_ПланФакт.НайтиСтроки(новый Структура("Штрихкод", Скан_Штрихкод));
				Если СтрокиШтрихкод.Количество() > 0 Тогда
					Элементы.ТЗ_ПланФакт.ТекущаяСтрока = СтрокиШтрихкод[0].ПолучитьИдентификатор();
				КонецЕсли;
				ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Штрихкод;
				ЗаполнитьПодвалТЗ();
				
				Скан_Штрихкод = "";
				Скан_Номенклатура = Неопределено;
				Скан_Количество	= 0;
			КонецЕсли;
						
		КонецЕсли;
		
		Элементы.Скан_Номенклатура.Вид = ВидПоляФормы.ПолеНадписи;	
		СтрокиНоменклатура = ТЗ_ПланФакт.НайтиСтроки(новый Структура("Номенклатура", Скан_Номенклатура));
		Если СтрокиНоменклатура.Количество() > 0 Тогда
			Элементы.ТЗ_ПланФакт.ТекущаяСтрока = СтрокиНоменклатура[0].ПолучитьИдентификатор();
		КонецЕсли;
	Иначе 
		Предупреждение("Не найдена номенклатура по штрихкоду " + Скан_Штрихкод);
		//Элементы.Скан_Номенклатура.ТолькоПросмотр = Ложь;
		Элементы.Скан_Номенклатура.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктивироватьПолеСканКоличество()
	ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Количество;		
КонецПроцедуры

&НаКлиенте
Процедура АктивироватьПолеСканШтрихкод()
	ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Штрихкод;		
КонецПроцедуры

&НаКлиенте
Процедура РежимСканирования(Команда)
	
	Элементы.ГруппаСкан.Видимость = Не Элементы.ГруппаСкан.Видимость;
	Если Элементы.ГруппаСкан.Видимость Тогда 
		ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Штрихкод;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВводКоличества(Команда)
	
	ЗаполнитьТаблицуПланФакт();
	РассчитатьСуммуПланФакта();
	Элементы.ГруппаСкан.Видимость = Ложь;
	
КонецПроцедуры	

&НаКлиенте
Процедура ДатаПоступленияЗаказаПриИзменении(Элемент)

	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаНачала", НачалоДня(ДатаНачала));
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ДатаОкончания));
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ПланФактКоличествоЦенаДокПриИзменении(Элемент)
	
	РассчитатьСуммуДокПланФакта();
	ЗаполнитьПодвалТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ПланФактЦенаДокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТЗ_ПланФакт.ТекущиеДанные;
	ТекущиеДанные.ЦенаДокБезНДС = ТекущиеДанные.ЦенаДок / (1 + ТекущиеДанные.СтавкаНДССтавка/100);
	
	РассчитатьСуммуДокПланФакта();
	ЗаполнитьПодвалТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ПланФактЦенаДокБезНДСПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ТЗ_ПланФакт.ТекущиеДанные;
	ТекущиеДанные.ЦенаДок = ТекущиеДанные.ЦенаДокБезНДС * (1 + ТекущиеДанные.СтавкаНДССтавка/100);
	
	РассчитатьСуммуДокПланФакта();
	ЗаполнитьПодвалТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ПланФактСуммаДокПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ТЗ_ПланФакт.ТекущиеДанные;
	ТекущиеДанные.ЦенаДок = ?(ТекущиеДанные.КоличествоДок = 0, 0, ТекущиеДанные.СуммаДок / ТекущиеДанные.КоличествоДок);
	ТекущиеДанные.ЦенаДокБезНДС = ТекущиеДанные.ЦенаДок / (1 + ТекущиеДанные.СтавкаНДССтавка/100);
	ТекущиеДанные.СуммаНДСДок 	= ТекущиеДанные.СуммаДок / ((100+ТекущиеДанные.СтавкаНДССтавка)/100) * (ТекущиеДанные.СтавкаНДССтавка/100);
	
	ЗаполнитьПодвалТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ТоварыВозвратКоличествоКОформлениюПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ТЗ_ТоварыВозврат.ТекущиеДанные;
	ТекущиеДанные.Сумма 	= ТекущиеДанные.КоличествоКОформлению * ТекущиеДанные.Цена;
	ТекущиеДанные.СуммаНДС 	= ТекущиеДанные.Сумма / ((100+ТекущиеДанные.СтавкаНДССтавка)/100) * (ТекущиеДанные.СтавкаНДССтавка/100);
	ТекущиеДанные.СуммаБезНДС = ТекущиеДанные.Сумма - ТекущиеДанные.СуммаНДС;
	
КонецПроцедуры


&НаКлиенте
Процедура РассчитатьСуммуПланФакта()
	
	КолвоПозиций = 0;
	Для Каждого ТекСтрока Из ТЗ_ПланФакт Цикл
		ТекСтрока.Сумма = ТекСтрока.КоличествоКОформлению * ?(ПринятьПоЦене = 3, ТекСтрока.ЦенаДок, ТекСтрока.ЦенаЗаказ);
		ТекСтрока.СуммаНДСДок 	= ТекСтрока.СуммаДок / ((100+ТекСтрока.СтавкаНДССтавка)/100) * (ТекСтрока.СтавкаНДССтавка/100);
		КолвоПозиций = КолвоПозиций + ?(ТекСтрока.КоличествоКОформлению = 0, 0, 1);
	КонецЦикла;
	
	ЗаполнитьПодвалТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокПланФакта(ТекущиеДанные = Неопределено)
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные = Элементы.ТЗ_ПланФакт.ТекущиеДанные;
	КонецЕсли;
	
	ТекущиеДанные.СуммаДок 		= ТекущиеДанные.КоличествоДок * ТекущиеДанные.ЦенаДок;
	ТекущиеДанные.СуммаНДСДок 	= ТекущиеДанные.СуммаДок / ((100+ТекущиеДанные.СтавкаНДССтавка)/100) * (ТекущиеДанные.СтавкаНДССтавка/100);
	
	ЗаполнитьПодвалТЗ();
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодвалТЗ()
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления Тогда
		КолвоПозиций = 0;
		Для Каждого ТекСтрока Из ТЗ_ПланФакт Цикл
			КолвоПозиций = КолвоПозиций + ?(ТекСтрока.КоличествоКОформлению = 0, 0, 1);
		КонецЦикла;

		ТЗ_ПланФактНоменклатураТекстПодвала = "Заказано: " + ТЗ_ПланФакт.Количество() + " Принято: " + КолвоПозиций;
														  
		ТЗ_ПланФактКоличествоКОформлениюТекстПодвала = Формат(ТЗ_ПланФакт.Итог("КоличествоКОформлению"), "ЧДЦ=2");
		ТЗ_ПланФактКоличествоЗаказТекстПодвала 		 = Формат(ТЗ_ПланФакт.Итог("КоличествоЗаказ"), "ЧДЦ=2");
		ТЗ_ПланФактКоличествоДокТекстПодвала 		 = Формат(ТЗ_ПланФакт.Итог("КоличествоДок"), "ЧДЦ=2");
														  
		ТЗ_ПланФактСуммаТекстПодвала		 = Формат(ТЗ_ПланФакт.Итог("Сумма"), "ЧДЦ=2");  
		ТЗ_ПланФактСуммаДокТекстПодвала 	 = Формат(ТЗ_ПланФакт.Итог("СуммаДок"), "ЧДЦ=2");
		ТЗ_ПланФактСуммаНДСДокТекстПодвала 	 = Формат(ТЗ_ПланФакт.Итог("СуммаНДСДок"), "ЧДЦ=2");
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеВозврата Тогда
		Элементы.ТЗ_ТоварыВозвратКоличествоЗаказ.ТекстПодвала 		= Формат(ТЗ_ТоварыВозврат.Итог("КоличествоЗаказ"));
		Элементы.ТЗ_ТоварыВозвратКоличествоКОформлению.ТекстПодвала = Формат(ТЗ_ТоварыВозврат.Итог("КоличествоКОформлению"));
		Элементы.ТЗ_ТоварыВозвратСумма.ТекстПодвала 				= Формат(ТЗ_ТоварыВозврат.Итог("Сумма"));
		Элементы.ТЗ_ТоварыВозвратСуммаНДС.ТекстПодвала 				= Формат(ТЗ_ТоварыВозврат.Итог("СуммаНДС"));
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПеремещения Тогда
		Элементы.ТЗ_ПеремещениеКоличествоЗаказ.ТекстПодвала 		= Формат(ТЗ_Перемещение.Итог("КоличествоЗаказ"));
		Элементы.ТЗ_ПеремещениеКоличествоОтправлено.ТекстПодвала 	= Формат(ТЗ_Перемещение.Итог("КоличествоОтправлено"));
		Элементы.ТЗ_ПеремещениеКоличествоКОформлению.ТекстПодвала 	= Формат(ТЗ_Перемещение.Итог("КоличествоКОформлению"));
		Элементы.ТЗ_ПеремещениеСумма.ТекстПодвала 					= Формат(ТЗ_Перемещение.Итог("Сумма"));		
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Скан_перемещ_ШтрихкодПриИзменении(Элемент)

	СтруктураНоменклатура = сабОперОбщегоНазначения.ПодобратНоменклатуруПоШК(Скан_Штрихкод);
	Если ТипЗнч(СтруктураНоменклатура) = Тип("Структура") Тогда
		Скан_Номенклатура = СтруктураНоменклатура.Номенклатура;
		Скан_Количество   = СтруктураНоменклатура.Количество;
	Иначе
		Скан_Номенклатура = СтруктураНоменклатура;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Скан_Номенклатура) Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.Скан_перемещ_Количество;
								
		СтрокиНоменклатура = ТЗ_Перемещение.НайтиСтроки(новый Структура("Номенклатура", Скан_Номенклатура));
		Если СтрокиНоменклатура.Количество() > 0 Тогда
			Элементы.ТЗ_Перемещение.ТекущаяСтрока = СтрокиНоменклатура[0].ПолучитьИдентификатор();
		КонецЕсли;
	Иначе 
		Предупреждение("Не найдена номенклатура по штрихкоду " + Скан_Штрихкод);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Скан_перемещ_КоличествоПриИзменении(Элемент)

	Если Скан_Количество > 1000000 Тогда
		Скан_Количество = 0;
		Предупреждение("Слишком большое количество, введите еще раз");
		ПодключитьОбработчикОжидания("АктивироватьПолеСканПеремещКоличество", 0.1, Истина);		
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(Скан_Штрихкод) И ЗначениеЗаполнено(Скан_Номенклатура) Тогда
		ДобавитьСканНоменклатуруВТовары();
		
		СтрокиШтрихкод = ТЗ_Перемещение.НайтиСтроки(новый Структура("Номенклатура", Скан_Номенклатура));
		Если СтрокиШтрихкод.Количество() > 0 Тогда
			Элементы.ТЗ_Перемещение.ТекущаяСтрока = СтрокиШтрихкод[0].ПолучитьИдентификатор();
		КонецЕсли;
		ЭтаФорма.ТекущийЭлемент = Элементы.Скан_перемещ_Штрихкод;
		ЗаполнитьПодвалТЗ();
		
		Скан_Штрихкод = "";
		Скан_Номенклатура = Неопределено;
		Скан_Количество	= 0;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура АктивироватьПолеСканПеремещКоличество()
	ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Перемещ_Количество;		
КонецПроцедуры

&НаКлиенте
Процедура АктивироватьПолеСканПеремещШтрихкод()
	ЭтаФорма.ТекущийЭлемент = Элементы.Скан_Перемещ_Штрихкод;		
КонецПроцедуры

#КонецОбласти

#Область Служебные
 
&НаКлиенте
Процедура УстановитьВидимость()
	
	Элементы.Назад.Видимость = Не (Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗаказы);
	Элементы.Далее.Видимость = Не Элементы.Назад.Видимость;	
	
	Элементы.Скан_Количество.Видимость = Скан_РежимРучногоВводаКоличества;
	
	СозданныйДокументЗаполнен = ЗначениеЗаполнено(СозданныйДокумент);
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления Тогда
		Если Элементы.Найти("ДокументУЧ_ПоступлениеТоваровПечатьТорг2") <> Неопределено Тогда
			Элементы.ДокументУЧ_ПоступлениеТоваровПечатьТорг2.Доступность = СозданныйДокументЗаполнен;
		КонецЕсли;
		Если Элементы.Найти("ДокументУЧ_ПоступлениеТоваровПечатьТорг4") <> Неопределено Тогда
			Элементы.ДокументУЧ_ПоступлениеТоваровПечатьТорг4.Доступность = СозданныйДокументЗаполнен;
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеВозврата Тогда
		Если Элементы.Найти("ДокументУЧ_ВозвратТоваровПоставщикуПечатьСчетФактура") <> Неопределено Тогда
			Элементы.ДокументУЧ_ВозвратТоваровПоставщикуПечатьСчетФактура.Доступность = СозданныйДокументЗаполнен;
		КонецЕсли;
		Если Элементы.Найти("ДокументУЧ_ВозвратТоваровПоставщикуПечатьТОРГ12") <> Неопределено Тогда
			Элементы.ДокументУЧ_ВозвратТоваровПоставщикуПечатьТОРГ12.Доступность = СозданныйДокументЗаполнен;
		КонецЕсли;
		Если Элементы.Найти("ДокументУЧ_ВозвратТоваровПоставщикуПечатьТТН") <> Неопределено Тогда
			Элементы.ДокументУЧ_ВозвратТоваровПоставщикуПечатьТТН.Доступность = СозданныйДокументЗаполнен;
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПеремещения Тогда
		Если Элементы.Найти("ДокументУЧ_ПеремещениеТоваровПечатьНакладнойНаПеремещение") <> Неопределено Тогда
			Элементы.ДокументУЧ_ПеремещениеТоваровПечатьНакладнойНаПеремещение.Доступность 	= СозданныйДокументЗаполнен;
		КонецЕсли;
		Если Элементы.Найти("ДокументУЧ_ПеремещениеТоваровПечатьТорг2") <> Неопределено Тогда
			Если ТипЗнч(СозданныйДокумент) = Тип("ДокументСсылка.УЧ_ПеремещениеТоваров") Тогда
				РеквизитыДокумента = БюджетныйНаСервере.ВернутьРеквизиты(СозданныйДокумент, "Организация, ОрганизацияПолучатель");
				Элементы.ДокументУЧ_ПеремещениеТоваровПечатьТорг2.Доступность = СозданныйДокументЗаполнен И РеквизитыДокумента.Организация <> РеквизитыДокумента.ОрганизацияПолучатель;
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьУсловноеОформление()
	
	//Оформление
	//
	//УсловноеОформление.Элементы.Добавить();
	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаНаличияСканНоменклатурыВЗаказе() 
	
	Если ТЗ_ПланФакт.НайтиСтроки(новый Структура("Номенклатура", Скан_Номенклатура)).Количество() > 0 Тогда
		Возврат Истина;
	Иначе
		Ответ = Вопрос("Номенклатура " + Скан_Номенклатура + " отсутствует в заказе. Принять ее?", РежимДиалогаВопрос.ДаНет);
		Возврат Ответ = КодВозвратаДиалога.Да;
	КонецЕсли;
			
	
КонецФункции

//&НаСервере
&НаКлиенте
Процедура ДобавитьСканНоменклатуруВТовары()
	
	//ШтрихкодНайден = ЗначениеЗаполнено(НайтиНоменклатуруПоШтрихкоду(Скан_Штрихкод));
	
	//ТЗ Товары
	//НайденныеСтроки = Товары.НайтиСтроки(новый Структура("Номенклатура", Скан_Номенклатура));
	//Если НайденныеСтроки.Количество() > 0 Тогда
	//	ТекСтрока = НайденныеСтроки[0];
	//	ТекСтрока.КоличествоФакт = ТекСтрока.КоличествоФакт + ?(Скан_РежимРучногоВводаКоличества, Скан_Количество, 1);
	//Иначе
	//	НоваяСтрока = Товары.Добавить();
	//	НоваяСтрока.Номенклатура 	= Скан_Номенклатура;
	//	НоваяСтрока.Штрихкод 		= ?(ШтрихкодНайден, Скан_Штрихкод, "");
	//	НоваяСтрока.КоличествоФакт	= ?(Скан_РежимРучногоВводаКоличества, Скан_Количество, 1);
	//КонецЕсли;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления Тогда

		НайденныеСтроки = ТЗ_ПланФакт.НайтиСтроки(новый Структура("Номенклатура", Скан_Номенклатура));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекСтрока = НайденныеСтроки[0];
			ТекСтрока.КоличествоФакт = ТекСтрока.КоличествоФакт + Скан_Количество;
			//Если Не ЗначениеЗаполнено(ТекСтрока.КоличествоДок) Тогда
				ТекСтрока.КоличествоДок	 = ТекСтрока.КоличествоДок + Скан_Количество;
			//КонецЕсли;	
			ТекСтрока.КоличествоБонуснойПродукции = ТекСтрока.КоличествоБонуснойПродукции + Скан_КоличествоБонуснойПродукции;
			Если ТекСтрока.ЕстьВЗаказе Тогда
				ТекСтрока.КоличествоКОформлению = ТекСтрока.КоличествоКОформлению + Скан_Количество;
			КонецЕсли;
			ТекСтрока.Штрихкод = Скан_Штрихкод;
			РассчитатьСуммуДокПланФакта(ТекСтрока);
		Иначе
			РеквизитыНоменклатуры = БюджетныйНаСервере.ВернутьРеквизиты(Скан_Номенклатура, "Код, Наименование, ЕдиницаИзмерения, СтавкаНДС, СтавкаНДС.Ставка");
			ТекСтрока = ТЗ_ПланФакт.Добавить();
			ТекСтрока.Номенклатура 		= Скан_Номенклатура;
			ТекСтрока.Штрихкод 			= Скан_Штрихкод;
			ТекСтрока.Наименование		= РеквизитыНоменклатуры.Наименование;
			ТекСтрока.СтавкаНДС			= РеквизитыНоменклатуры.СтавкаНДС;
			ТекСтрока.СтавкаНДССтавка	= РеквизитыНоменклатуры.СтавкаНДССтавка;
			ТекСтрока.КоличествоФакт	= ?(Скан_РежимРучногоВводаКоличества, Скан_Количество, 1);
			ТекСтрока.КоличествоКОформлению	= 0;
			ТекСтрока.КоличествоБонуснойПродукции = Скан_КоличествоБонуснойПродукции;
			ТекСтрока.Склад = ТЗ_ПланФакт[0].Склад;
		КонецЕсли;	
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПеремещения Тогда
		
		НайденныеСтроки = ТЗ_Перемещение.НайтиСтроки(новый Структура("Номенклатура", Скан_Номенклатура));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекСтрока = НайденныеСтроки[0];
			ТекСтрока.КоличествоКОформлению = ТекСтрока.КоличествоКОформлению + Скан_Количество;
		Иначе
			ТекСтрока = ТЗ_Перемещение.Добавить();
			ТекСтрока.Номенклатура 			= Скан_Номенклатура;
			ТекСтрока.КоличествоКОформлению	= Скан_Количество;
			ТекСтрока.СчетУчетаБУ 		= ТЗ_Перемещение[0].СчетУчетаБУ;
			ТекСтрока.НовыйСчетУчетаБУ  = ТЗ_Перемещение[0].НовыйСчетУчетаБУ;
			ТекСтрока.ДобавленоВручную	= Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Скан_КоличествоБонуснойПродукцииПриИзменении(Элемент)
	
	Если Скан_КоличествоБонуснойПродукции > 1000000 Тогда
		Скан_КоличествоБонуснойПродукции = 0;
		Возврат;
	КонецЕсли;
	
	Скан_КоличествоПриИзменении(Неопределено)
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиНоменклатуруПоШтрихкоду(Штрихкод)
	
	Запрос = новый Запрос;
	Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |ГДЕ
	               |	ШтрихкодыНоменклатуры.Штрихкод = &Штрихкод";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Номенклатура;
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПечатьТорг2(Команда)

	ТабДок = ПечатьТорг2НаСервере(СозданныйДокумент);
		
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Истина;
	ТабДок.АвтоМасштаб = Истина;
	ФормаПечати = ПолучитьФорму("ОбщаяФорма.ФормаПечатиДокумента");
	ФормаПечати.Результат = ТабДок;
	ФормаПечати.Открыть();
	
	БылВызовПечати = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПечатьТорг2НаСервере(Документ)

	Возврат Документы.УЧ_ПоступлениеТоваров.ПечатьТОРГ2(Документ);
	
КонецФункции

&НаКлиенте
Процедура ПечатьТорг4(Команда)

	ТабДок = ПечатьТорг4НаСервере(СозданныйДокумент);
		
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Истина;
	ТабДок.АвтоМасштаб = Истина;
	ФормаПечати = ПолучитьФорму("ОбщаяФорма.ФормаПечатиДокумента");
	ФормаПечати.Результат = ТабДок;
	ФормаПечати.Открыть();
	
	БылВызовПечати = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПечатьТорг4НаСервере(Документ)

	Возврат Документы.УЧ_ПоступлениеТоваров.ПечатьТОРГ4(Документ);
	
КонецФункции

&НаКлиенте
Процедура ТЗ_ПланФактКоличествоКОформлениюПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТЗ_ПланФакт.ТекущиеДанные;
	//Если ТекущиеДанные.КоличествоЗаказ = ТекущиеДанные.КоличествоКОформлению Тогда
		ТекущиеДанные.КоличествоДок = ТекущиеДанные.КоличествоКОформлению;
	//Иначе
	//	ТекущиеДанные.КоличествоДок = 0;
	//КонецЕсли;
	
	РассчитатьСуммуДокПланФакта();
	ЗаполнитьПодвалТЗ();
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ПеремещениеКоличествоКОформлениюПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТЗ_Перемещение.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.КоличествоКОформлению;
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньПредыдущий(Команда)
	
	ДатаНачала 		= ДатаНачала - 86400;
	ДатаОкончания 	= ДатаОкончания - 86400;
	
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаНачала", НачалоДня(ДатаНачала));
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ДатаОкончания));
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньСледующий(Команда)

	ДатаНачала 		= ДатаНачала + 86400;
	ДатаОкончания 	= ДатаОкончания + 86400;
	
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаНачала", НачалоДня(ДатаНачала));
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", КонецДня(ДатаОкончания));
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВЧерновикНаСервере()

	СтруктураЧерновик = новый Структура;
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления Тогда
		СтруктураЧерновик.Вставить("НомерТТН", НомерТТН);
		СтруктураЧерновик.Вставить("СуммаИтогоДок", СуммаИтогоДок);
		СтруктураЧерновик.Вставить("ДатаТТН", ДатаТТН);
		СтруктураЧерновик.Вставить("НомерСчФ", НомерСчФ);
		СтруктураЧерновик.Вставить("ДатаСчФ", ДатаСчФ);
		СтруктураЧерновик.Вставить("ИмяТаблицы", "ТЗ_ПланФакт");
		СтруктураЧерновик.Вставить("Таблица", ТЗ_ПланФакт.Выгрузить());
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПеремещения Тогда
		СтруктураЧерновик.Вставить("ИмяТаблицы", "ТЗ_Перемещение");
		СтруктураЧерновик.Вставить("Таблица", ТЗ_Перемещение.Выгрузить());
	КонецЕсли;
	
	Хранилище = новый ХранилищеЗначения(СтруктураЧерновик);
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	АвтосохранениеДанных.Пользователь,
	               |	АвтосохранениеДанных.Объект
	               |ИЗ
	               |	РегистрСведений.АвтосохранениеДанных КАК АвтосохранениеДанных
	               |ГДЕ
	               |	АвтосохранениеДанных.Объект = &Объект";
	Запрос.УстановитьПараметр("Объект", ОбрабатываемыйДокумент);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	Если Выгрузка.Количество() = 1 Тогда
		АвторЧерновика = Выгрузка[0].Пользователь;
	Иначе 
		АвторЧерновика = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.АвтосохранениеДанных.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = АвторЧерновика;
	МенеджерЗаписи.Объект 	= ОбрабатываемыйДокумент;
	МенеджерЗаписи.Данные 	= Хранилище;
	МенеджерЗаписи.Дата		= ТекущаяДата();
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьИзЧерновиковНаСервере()
	
	НаборЗаписей = РегистрыСведений.АвтосохранениеДанных.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(ОбрабатываемыйДокумент);
	//НаборЗаписей.Отбор.Пользователь.Установить(ПараметрыСеанса.ТекущийПользователь);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() Тогда
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();	
	КонецЕсли; 
	
КонецПроцедуры


&НаКлиенте
Процедура СохранитьВЧерновикНаКлиенте()
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления Тогда
		СохранитьВЧерновикНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВЧерновик(Команда)
	
	СохранитьВЧерновикНаСервере();
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСозданиеПоступления Тогда
		Элементы.ДекорацияСохранено.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьИзЧерновикаНаСервере()
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	АвтосохранениеДанных.Данные,
	               |	ВЫБОР
	               |		КОГДА АвтосохранениеДанных.Пользователь = &Пользователь
	               |			ТОГДА 1
	               |		КОГДА АвтосохранениеДанных.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	               |			ТОГДА 2
	               |		ИНАЧЕ 3
	               |	КОНЕЦ КАК Порядок
	               |ИЗ
	               |	РегистрСведений.АвтосохранениеДанных КАК АвтосохранениеДанных
	               |ГДЕ
	               |	АвтосохранениеДанных.Объект = &Объект
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Порядок";
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", ОбрабатываемыйДокумент);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Хранилище = Выборка.Данные;
		СтруктураЧерновик = Хранилище.Получить();
		Если СтруктураЧерновик.ИмяТаблицы = "ТЗ_ПланФакт" Тогда
			НомерТТН = СтруктураЧерновик.НомерТТН;
			Если СтруктураЧерновик.Свойство("СуммаИтогоДок") Тогда
				СуммаИтогоДок = СтруктураЧерновик.СуммаИтогоДок;
			КонецЕсли;
			ДатаТТН  = СтруктураЧерновик.ДатаТТН;
			НомерСчФ = СтруктураЧерновик.НомерСчФ;
			ДатаСчФ  = СтруктураЧерновик.ДатаСчФ;
			ТЗ_ПланФакт.Загрузить(СтруктураЧерновик.Таблица);
		ИначеЕсли СтруктураЧерновик.ИмяТаблицы = "ТЗ_Перемещение" Тогда
			ТЗ_Перемещение.Загрузить(СтруктураЧерновик.Таблица);
		КонецЕсли;
	Иначе
		Сообщить("Нет данных в черновике");
	КонецЕсли;
	
	//обновим статусы
	Если ОбрабатываемыйДокумент.ВидОперации = Перечисления.ВидыЗаказов.ЗакупкаТоваров Тогда
		ЗаказаннаяНоменклатура = ОбрабатываемыйДокумент.ТабличнаяЧасть.ВыгрузитьКолонку("Номенклатура");
		Для Каждого ТекСтрока Из ТЗ_ПланФакт Цикл
			Если ЗаказаннаяНоменклатура.Найти(ТекСтрока.Номенклатура) <> Неопределено Тогда
				ТекСтрока.ЕстьВЗаказе = Истина;
			Иначе
				ТекСтрока.ЕстьВЗаказе = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьЗаписьВЧерновике(Документ)
	
	Запрос = новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	АвтосохранениеДанных.Данные
	               |ИЗ
	               |	РегистрСведений.АвтосохранениеДанных КАК АвтосохранениеДанных
	               |ГДЕ
				   //|	АвтосохранениеДанных.Пользователь = &Пользователь
	               |	 АвтосохранениеДанных.Объект = &Объект";
	//Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Объект", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ВсеКонтрагенты", Не ЗначениеЗаполнено(Контрагент));
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоказыватьЗакрытые(Команда)
	Элементы.ПоказыватьЗакрытые.Пометка = НЕ Элементы.ПоказыватьЗакрытые.Пометка;
	СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ВсеЗаказы", НЕ Элементы.ПоказыватьЗакрытые.Пометка);
КонецПроцедуры

#Область НайтиПоШК
&НаКлиенте
Процедура НайтиЗаказПоШК(Команда)
	ОбработкаТабличнойЧастиТоварыКлиент.ВвестиШтрихкод(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПоискаПоШтрихкоду(Штрихкод, ДополнительныеПараметры) Экспорт
	
	ТекЗаказ = НайтиЗаказПоКоду(Штрихкод);
	Если Не ЗначениеЗаполнено(ТекЗаказ) Тогда
		Сообщить("Заказ с №" + Строка(Штрихкод) + " не найден!");
		Возврат;
	ИначеЕсли ТекЗаказ.Закрыт Тогда  
		Сообщить("Заказ с №" + Строка(Штрихкод) + " уже обработан!");
		Возврат;
	КонецЕсли; 
	
	Если ДатаНачала > ТекЗаказ.ДатаПоступления Тогда
		ДатаНачала = ТекЗаказ.ДатаПоступления;	
		СписокЗаказы.Параметры.УстановитьЗначениеПараметра("ДатаНачала", НачалоДня(ДатаНачала));
	КонецЕсли; 
	Элементы.СписокЗаказы.ТекущаяСтрока = ТекЗаказ.Заказ;
	Далее(Неопределено);
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиЗаказПоКоду(Штрихкод)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПоставщику.Ссылка,
	|	ЗаказПоставщику.Статус,
	|	ЗаказПоставщику.ДатаПоступления
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Номер = &Номер";
	Запрос.УстановитьПараметр("Номер", Штрихкод);
	Запрос.УстановитьПараметр("ДоступныеПодразделения", ПараметрыСеанса.ДоступныеПодразделения);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	
	Для каждого ТекЗаказ Из РезультатЗапроса Цикл
		Возврат Новый Структура("Заказ, ТекСтатус, Закрыт, ДатаПоступления", ТекЗаказ.Ссылка, ТекЗаказ.Статус, ТекЗаказ.Статус = Перечисления.СтатусыЗаказовПоставщикам.Закрыт, ТекЗаказ.ДатаПоступления);
	КонецЦикла; 
	
	
	Возврат Неопределено;		
КонецФункции

&НаКлиенте
Процедура ПечатьСпискаЗаказов(Команда)
	СписокМеню = Новый СписокЗначений;
	СписокМеню.Добавить("Вывести список выделенных заказов");
	СписокМеню.Добавить("Вывести заказы с " + Формат(ДатаНачала, "ДФ=dd.MM.yyyy") + " по " + Формат(ДатаОкончания, "ДФ=dd.MM.yyyy"));
	ВыбранныйПункт = ВыбратьИзМеню(СписокМеню,  Элементы.Группа4);
		
	Если ВыбранныйПункт = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	ТабДок = Новый ТабличныйДокумент;
	
	ПечатьСпискаЗаказовСервер(ТабДок, Новый Структура("ВыбранныйПункт, ВыделенныеЗаказы, ДатаНачала, ДатаОкончания, ВыводитьЗакрытые", ВыбранныйПункт.Значение, Элементы.СписокЗаказы.ВыделенныеСтроки, ДатаНачала, ДатаОкончания, НЕ Элементы.ПоказыватьЗакрытые.Пометка));
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ФормаПечати = ПолучитьФорму("ОбщаяФорма.ФормаПечатиДокумента");
	ФормаПечати.Результат = ТабДок;
	ФормаПечати.Заголовок = "Список заказов: Печать";
	ФормаПечати.Автозаголовок = Ложь;
	ФормаПечати.Открыть();
	//}}
	
КонецПроцедуры
&НаСервере
Процедура ПечатьСпискаЗаказовСервер(ТабДок, ПараметрКоманды)
	Если ПараметрКоманды.ВыбранныйПункт = "Вывести список выделенных заказов" Тогда
		МассивЗаказов = ПараметрКоманды.ВыделенныеЗаказы;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказПоставщику.Ссылка
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|ГДЕ
		|	ЗаказПоставщику.ДатаПоступления МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ВЫБОР
		|			КОГДА &ВыводитьЗакрытые
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ НЕ ЗаказПоставщику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)
		|		КОНЕЦ";
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрКоманды.ДатаНачала);
		Запрос.УстановитьПараметр("ВыводитьЗакрытые", ПараметрКоманды.ВыводитьЗакрытые);
		Запрос.УстановитьПараметр("ДатаОкончания", ПараметрКоманды.ДатаОкончания);
		МассивЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли; 
	Документы.ЗаказПоставщику.ПечатьСпискаЗаказов(ТабДок, МассивЗаказов);
КонецПроцедуры

&НаКлиенте
Процедура ДанныеТТНПриИзменении(Элемент)
			
	Элементы.ДекорацияСохранено.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗ_ПланФактПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	а = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКолвоТТН(Команда)
	
	Ответ = Вопрос("Значения в колонке ""Кол-во по ТТН"" будут заменены значениями колонки ""Кол-во к оформлению"". Продолжить?", РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекСтрока Из ТЗ_ПланФакт Цикл
		Если ТекСтрока.ЕстьВЗаказе Тогда
			ТекСтрока.КоличествоДок = ТекСтрока.КоличествоКОформлению;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуПеремещение(Команда)
	
	НоваяСтрока = ТЗ_Перемещение.Добавить();
	НоваяСтрока.ДобавленоВручную = Истина;
	
КонецПроцедуры


#КонецОбласти 
