#Область ПрограммныйИнтерфейс

Функция ПолучитьПоследнююВерсию() Экспорт
	
	Если ПравоДоступа("Administration", Метаданные) = Ложь
		И БезопасныйРежим() = Ложь
		И ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	ПоследнийРелиз = сабКоннекторHTTP.GetJson(
			"https://api.github.com/repos/whitedi2/modmcount/releases/latest");
	
	ОписаниеРелиза = Новый Структура;
	ОписаниеРелиза.Вставить("Версия", ПоследнийРелиз.Получить("tag_name"));
	
	Возврат ОписаниеРелиза;
	
КонецФункции

Функция ПолучитьДанные(Версия) Экспорт
	
	Если ПравоДоступа("Administration", Метаданные) = Ложь
		И БезопасныйРежим() = Ложь
		И ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Uri = СтрШаблон("https://github.com/whitedi2/modmcount/releases/download/%1/modmcount.cfe", Версия);
	Ответ = сабКоннекторHTTP.Get(Uri);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение сабКоннекторHTTP.КакИсключение(Ответ);
	КонецЕсли;
	
	Данные = сабКоннекторHTTP.КакДвоичныеДанные(Ответ);
	
	_ПроверитьДанныеРасширенияКонфигурации(Новый Структура("Имя, Версия", "модульУправленка", Версия), Данные);
	
	Возврат Данные;
	
КонецФункции

Функция ПолучитьУстановленнуюВерсию() Экспорт
	
	Если ПравоДоступа("Administration", Метаданные) = Ложь
		И БезопасныйРежим() = Ложь
		И ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Массив = РасширенияКонфигурации.Получить(Новый Структура("Имя", "модульУправленка"));
	
	Результат = Новый Структура;
	Результат.Вставить("Версия", Массив[0].Версия);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьАктивнуюВерсию() Экспорт
	
	Расширение = Метаданные.Обработки.сабОбновлениеМодуля.РасширениеКонфигурации();
	Результат = Новый Структура;
	Результат.Вставить("Версия", Расширение.Версия);
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьВерсию(Версия) Экспорт
	
	Если ПравоДоступа("Administration", Метаданные) = Ложь
		И БезопасныйРежим() = Ложь
		И ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Данные = ПолучитьДанные(Версия);
	Возврат _УстановитьРасширение("модульУправленка", Данные);
	
КонецФункции

Функция Обновление() Экспорт
	
	Если ПравоДоступа("Administration", Метаданные) = Ложь
		И БезопасныйРежим() = Ложь
		И ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	ПоследняяВерсия = ПолучитьПоследнююВерсию();
	УстановленнаяВерсия = ПолучитьУстановленнуюВерсию();
	
	Если ПоследняяВерсия.Версия <> УстановленнаяВерсия.Версия Тогда
		Возврат ПоследняяВерсия;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура _ПроверитьДанныеРасширенияКонфигурации(Расширение, Данные)
	
	ПараметрыКонструктора = Новый Массив;
	ПараметрыКонструктора.Добавить(Данные);
	
	ОписаниеКонфигурации = Новый(Тип("ОписаниеКонфигурации"), ПараметрыКонструктора);
	Если ОписаниеКонфигурации.Версия <> Расширение.Версия
		ИЛИ ОписаниеКонфигурации.Имя <> Расширение.Имя Тогда
		ВызватьИсключение "Данные расширения конфигурации не совпадают с манифестом";
	КонецЕсли;
	
КонецПроцедуры

Функция _УстановитьРасширение(Имя, ДанныеРасширения)
	
	Если ПравоДоступа("Administration", Метаданные) = Ложь
		И БезопасныйРежим() = Ложь
		И ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Расширение = _НайтиРасширениеПоИмени(Имя);
	Если Расширение = Неопределено Тогда
		Расширение = Вычислить("РасширенияКонфигурации").Создать();
	КонецЕсли;
	
	ЗащитаОтОпасныхДействий = Вычислить("Новый ОписаниеЗащитыОтОпасныхДействий");
	ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях = Ложь;
	
	Расширение.Активно = Истина;
	Расширение.БезопасныйРежим = Ложь;
	Расширение.ЗащитаОтОпасныхДействий = ЗащитаОтОпасныхДействий;
	
	Структура = Новый Структура("ИспользоватьОсновныеРолиДляВсехПользователей");
	ЗаполнитьЗначенияСвойств(Структура, Расширение);
	Если Структура.ИспользоватьОсновныеРолиДляВсехПользователей <> Неопределено Тогда
		Расширение.ИспользоватьОсновныеРолиДляВсехПользователей = Истина;
	КонецЕсли;
	
	Проблемы = Расширение.ПроверитьВозможностьПрименения(ДанныеРасширения);
	Отказ = Ложь;
	
	Для Каждого Элемент Из Проблемы Цикл
		
		Ошибка = "(" + Элемент.Важность + ") " +
			Элемент.Расширение.Синоним + ": " +
			Элемент.Описание;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Ошибка;
		Сообщение.Сообщить();
		
		НизкаяВажность = ПредопределенноеЗначение("ВажностьПроблемыПримененияРасширенияКонфигурации.Низкая");
		Если Элемент.Важность <> НизкаяВажность Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Расширение.Записать(ДанныеРасширения);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции // УстановитьРасширениеПоИмени

Функция _НайтиРасширениеПоИмени(Имя)
	
	Если ПравоДоступа("Administration", Метаданные) = Ложь
		И БезопасныйРежим() = Ложь
		И ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Имя", СокрЛП(Имя));
	
	Расширение = Неопределено;
	Расширения = Вычислить("РасширенияКонфигурации").Получить(Отбор);
	Если Расширения.Количество() Тогда
		Расширение = Расширения[0];
	КонецЕсли;
	
	Возврат Расширение;
	
КонецФункции // НайтиРасширениеПоИмени

#КонецОбласти