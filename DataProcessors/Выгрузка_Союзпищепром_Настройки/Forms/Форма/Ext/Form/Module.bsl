
&НаКлиенте
Асинх Процедура ВыполнитьВыгрузку(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ПапкаДляВыгрузки) Тогда
		Сообщить("Нужно выбрать папку для выгрузки");
		Возврат;
	КонецЕсли;
	АдресВХ = Истина;
	ВыполнитьВыгрузкуНаСервере(Ложь,АдресВХ);
	Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
		Архив = ПолучитьИзВременногоХранилища(АдресВХ);
		Обещание = Архив.ЗаписатьАсинх(Объект.ПапкаДляВыгрузки+"\batch.zip"); 
		Результат = Ждать Обещание;
		Сообщить("Выгружен архив с файлами " + Объект.ПапкаДляВыгрузки + "\batch.zip");
	КонецЕсли;
	// ПоказатьОповещениеПользователя("Выгрузка завершена...");
	
КонецПроцедуры 

&НаКлиенте
Асинх Процедура ВыполнитьОтправку(Команда) 
	
	Если Не ЗначениеЗаполнено(Объект.ПапкаДляВыгрузки) Тогда
		Сообщить("Нужно выбрать папку для выгрузки");
		Возврат;
	КонецЕсли;
	обещание = ВопросАсинх(НСтр("ru='Подтвердите отправку выгрузки данных на сервер поставщика?'"), РежимДиалогаВопрос.ОКОтмена, 15, КодВозвратаДиалога.ОК);
	Результат = Ждать обещание; // тут выполнение остановится пока пользователь не ответит на вопрос
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		АдресВХ = Истина;
		ВыполнитьВыгрузкуНаСервере(Истина,АдресВХ); 
		Если ЭтоАдресВременногоХранилища(АдресВХ) Тогда
			Архив = ПолучитьИзВременногоХранилища(АдресВХ);
			ОбещаниеЗапись = Архив.ЗаписатьАсинх(Объект.ПапкаДляВыгрузки+"\batch.zip"); 
			Результат = Ждать ОбещаниеЗапись;
			Сообщить("Выгружен архив с файлами " + Объект.ПапкаДляВыгрузки + "\batch.zip");
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда 
		текст = НСтр("ru='Отказ пользователя от отправки данных!'");		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		текст);
	Иначе
		текст = НСтр("ru='Ответ не получен. Отправка не выполнена!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		текст);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере                 
// РежимВыгрузки:
//  Истина  - выгрузка и отправка на сервер
//  Ложь	- только выгрузка в файл
Процедура ВыполнитьВыгрузкуНаСервере(РежимВыгрузки,АдресВХ = Ложь) 
	
	струПараметры = Новый Структура("КодДистрибьютора, Логин, Пароль, АдресСервера, ПутьКСервису,
									 |КаталогВыгрузки,	
	                                 |Производитель,
									 |Организация");
		
	струПараметры.КодДистрибьютора 	= Объект.КодДистрибьютора;  
	струПараметры.Логин			   	= Объект.Логин;
	струПараметры.Пароль		   	= Объект.Пароль;    
	струПараметры.АдресСервера		= Объект.АдресСервера;
	струПараметры.ПутьКСервису		= Объект.ПутьКСервису;	
	струПараметры.Производитель		= Объект.Производитель;  
	струПараметры.Организация		= Объект.Организация;
	СохранитьПараметрыНаСервере(струПараметры);
    Модифицированность = Ложь;
	// Выгрузка происходит интерактивно, период выгрузки взять из реквизитов формы
    ПериодВыгрузки = Новый Структура("НачалоПериода, КонецПериода");
	ПериодВыгрузки.НачалоПериода 	= Объект.НачалоПериода;
	ПериодВыгрузки.КонецПериода 	= Объект.КонецПериода;
	РеквизитФормыВЗначение("Объект").ВыполнитьВыгрузкуНаСервере(РежимВыгрузки, Истина, ПериодВыгрузки, АдресВХ);  
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)   
	
	/// Прочитать настройки из безопасного хранилища
	ПространствоИмен = "BSC_SellOut_Upload_Союзпищепром";
	КлючНастроек	 = "Parameters_Союзпищепром";  
	Настройки = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ПространствоИмен, КлючНастроек);
	Если Неопределено = Настройки Тогда
		// Либо ещё настройки не записывались, либо невозможно их прочитать, восстановим значения по умолчанию
		струПараметры = Новый Структура("КодДистрибьютора, Логин, Пароль, АдресСервера, ПутьКСервису,
										 |КаталогВыгрузки,	
		                                 |Производитель");
		
		струПараметры.КодДистрибьютора  	= "D___________";   // Укажите Код дистрибьютора, предоставленный производителем
		//струПараметры.Логин				= ********;
		//струПараметры.Пароль				= ********;
		струПараметры.АдресСервера			= "domain_name";  // Укажите Название домена, предоставленное производителем
        струПараметры.ПутьКСервису			= "/bsc-sell-out/dc/cFileUpload.jsp";
		струПараметры.Производитель	= Справочники.Контрагенты.НайтиПоКоду("000000___");	   // Укажите Код производителя, заполненный в номенклатуре
    Иначе
        струПараметры = Настройки; 
	КонецЕсли;
	
	// Отобразить на форме
	Объект.КодДистрибьютора	= ?(струПараметры.Свойство("КодДистрибьютора"), струПараметры.КодДистрибьютора, 0);  
	Объект.Логин			= ?(струПараметры.Свойство("Логин"), струПараметры.Логин, "user@domain.ru");
	Объект.Пароль			= ?(струПараметры.Свойство("Пароль"), струПараметры.Пароль, "");
	Объект.АдресСервера		= ?(струПараметры.Свойство("АдресСервера"), струПараметры.АдресСервера, "distr.new-terra.ru");
	Объект.ПутьКСервису		= ?(струПараметры.Свойство("ПутьКСервису"), струПараметры.ПутьКСервису, "/bsc-sell-out/dc/cFileUpload.jsp");
	
 	Объект.КонецПериода		= КонецДня(КонецДня(ТекущаяДата() 		- 60*60*24));
	Объект.НачалоПериода	= ДобавитьМесяц(НачалоДня(ТекущаяДата() - 60*60*24), -1); // НачалоДня(ТекущаяДата()) - 31*24*60*60; 
	Объект.Производитель	= ?(струПараметры.Свойство("Производитель"), струПараметры.Производитель, Справочники.Контрагенты.ПустаяСсылка());
	Объект.Организация		= ?(струПараметры.Свойство("Организация"), струПараметры.Организация, Справочники.Организации.ПустаяСсылка());

КонецПроцедуры

&НаКлиенте
Процедура СохранитьПараметры(Команда) 
	
	струПараметры = Новый Структура("КодДистрибьютора, Логин, Пароль, АдресСервера, ПутьКСервису,
									 |КаталогВыгрузки,	
	                                 |Производитель,
									 |Организация");
		
	струПараметры.КодДистрибьютора 	= Объект.КодДистрибьютора;  
	струПараметры.Логин			   	= Объект.Логин;
	струПараметры.Пароль		   	= Объект.Пароль;    
	струПараметры.АдресСервера		= Объект.АдресСервера;
	струПараметры.ПутьКСервису		= Объект.ПутьКСервису;	
	струПараметры.Производитель		= Объект.Производитель;  
	струПараметры.Организация		= Объект.Организация;
	СохранитьПараметрыНаСервере(струПараметры);
	Модифицированность = Ложь;
    
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьПараметрыНаСервере(струПараметры)

	ПространствоИмен = "BSC_SellOut_Upload_Союзпищепром";
	КлючНастроек	 = "Parameters_Союзпищепром";  
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ПространствоИмен, струПараметры, КлючНастроек); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаДляВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда		
		ТекстСообщения = НСтр(
		"ru='Для данной операции необходимо
		|установить расширение работы с файлами!
		|Установка выполняется в разделе
		|""Сервис и администрирование""/
		|""Настройка сервисных функций"".'"
		);
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(Режим);
	ДиалогВыбораКаталога.Каталог = Объект.ПапкаДляВыгрузки;
	ДиалогВыбораКаталога.МножественныйВыбор = Ложь;
	ДиалогВыбораКаталога.Заголовок = НСтр("ru = 'Выберите каталог'");
	Если ДиалогВыбораКаталога.Выбрать() Тогда
		Объект.ПапкаДляВыгрузки = ДиалогВыбораКаталога.Каталог;
		Модифицированность = Истина;
	КонецЕсли; 

КонецПроцедуры




