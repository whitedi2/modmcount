
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;   
	
	Если НЕ ЗначениеЗаполнено(Параметры.Объекты)
		ИЛИ НЕ ЗначениеЗаполнено(Параметры.МенеджерПечати)
		ИЛИ НЕ ЗначениеЗаполнено(Параметры.ИмяФормы) Тогда
		
		ВызватьИсключение НСтр("ru='Непосредственное открытие этой формы не предусмотрено.'");
		
	КонецЕсли;
	
	Объекты.ЗагрузитьЗначения(Параметры.Объекты);
	ТипОбъекта = Параметры.МенеджерПечати;
	
	// Формирования состава печатных форм
	КомандыПечати = УправлениеПечатью.КомандыПечатиФормы(Параметры.ИмяФормы);
	
	Идентификатор = ""; 

	//++ЖВИ
	ДокументыИсточника = Параметры.ДокументыИсточника;
	Если ДокументыИсточника.Количество()> 0 тогда
		Если ТипЗнч(ДокументыИсточника[0]) = Тип("ДокументСсылка.сабМаршрутныйЛист") тогда
			КомандаПечати = КомандыПечати.Добавить();
			КомандаПечати.Идентификатор  = "Накладная";
			КомандаПечати.МенеджерПечати = "Документ.ПеремещениеТоваров";
			КомандаПечати.Представление  = НСтр("ru = 'Перемещение'");
			КомандаПечати.СписокФорм     = "ФормаСписка";
			КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
			КомандаПечати.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте", "Накладная");
			КомандаПечати.ДополнительныеПараметры.Вставить("СокращенноеСообщениеОбОшибке", Ложь);
			КомандаПечати.Порядок        = 170;
		КонецЕсли;
		//Если ТипЗнч(ДокументыИсточника[0]) = Тип("ДокументСсылка.сабМаршрутныйЛист") тогда
		//	КомандаПечати = КомандыПечати.Добавить();
		//	КомандаПечати.Идентификатор  = "ТоварныйЧек";
		//	КомандаПечати.МенеджерПечати = "Документ.РозничнаяПродажа";
		//	КомандаПечати.Представление  = НСтр("ru = 'Товарный чек'");
		//	КомандаПечати.СписокФорм     = "ФормаСписка";
		//	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
		//	КомандаПечати.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте", "ТоварныйЧек");
		//	КомандаПечати.ДополнительныеПараметры.Вставить("СокращенноеСообщениеОбОшибке", Ложь);
		//	КомандаПечати.Порядок        = 170;
		//КонецЕсли;
	КонецЕсли;
	//--ЖВИ  
	
	Для Каждого КомандаПечати Из КомандыПечати Цикл
	
		Если КомандаПечати.ДополнительныеПараметры.Свойство("НеВыводитьВКомплекте") Тогда
			Продолжить;
		КонецЕсли;
		
		Если КомандаПечати.СкрытаФункциональнымиОпциями Тогда
			Продолжить;
		КонецЕсли;
		
		НеДобавлятьВКомплект = Ложь;
		
		Если КомандаПечати.УсловияВидимости.Количество() > 0
		   И Объекты.Количество() = 1 Тогда
			Для Каждого ЭлементУсловия Из КомандаПечати.УсловияВидимости Цикл
				Если ТипЗнч(ЭлементУсловия) = Тип("Структура")
				   И ЭлементУсловия.Свойство("Реквизит")
				   И ЭлементУсловия.Реквизит = "ЭтоУниверсальныйДокумент" Тогда
					ОбъектПечати = Объекты[0].Значение;
					Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ЭтоУниверсальныйДокумент", ОбъектПечати.Метаданные())
					   И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектПечати, "ЭтоУниверсальныйДокумент") <> ЭлементУсловия.Значение Тогда
					
						НеДобавлятьВКомплект = Истина;
						Прервать;
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НеДобавлятьВКомплект Тогда
			Продолжить;
		КонецЕсли;		
		
		НоваяСтрока = КомплектПечатныхФорм.Добавить();
		Если КомандаПечати.ДополнительныеПараметры.Свойство("ИдентификаторВКомплекте") Тогда
			НоваяСтрока.Имя = КомандаПечати.ДополнительныеПараметры.ИдентификаторВКомплекте;
		Иначе
			НоваяСтрока.Имя = КомандаПечати.Идентификатор;
		КонецЕсли;
		
		Если КомандаПечати.ДополнительныеПараметры.Свойство("ПроверятьНаличиеПодписиПередПечатью") Тогда
			НоваяСтрока.ПроверятьНаличиеПодписиПередПечатью = КомандаПечати.ДополнительныеПараметры.ПроверятьНаличиеПодписиПередПечатью;
		КонецЕсли;

		НоваяСтрока.МенеджерПечати = КомандаПечати.МенеджерПечати;
		НоваяСтрока.Представление = КомандаПечати.Представление;
		НоваяСтрока.Порядок = КомандаПечати.Порядок;
		
		Если КомандаПечати.МенеджерПечати <> "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда
			Идентификатор = Идентификатор + НоваяСтрока.Имя + ",";
		КонецЕсли;
		
	КонецЦикла;
	
	// Загрузка сохраненных настроек
	Если Идентификатор <> "" Тогда
		Идентификатор = Лев(Идентификатор, СтрДлина(Идентификатор) - 1);
	КонецЕсли;
	
	КлючНастроек = Параметры.МенеджерПечати + "-" + Идентификатор;
	СохраненныеНастройкиПечатныхФорм = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПечатныхФормКомплект", КлючНастроек, Новый Массив);
	
	ВосстановитьНастройкиПечатныхФорм(СохраненныеНастройкиПечатныхФорм, Параметры.ИмяФормы);
	
	СразуНаПринтер = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОбщиеНастройкиПользователя", "ПечатьКомплектаСразуНаПринтер",Ложь);
	Если СразуНаПринтер <> Неопределено Тогда
		ПечатьСразуНаПринтер = СразуНаПринтер;
	Иначе
		ПечатьСразуНаПринтер = Ложь;
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ПечатьКомплектаСразуНаПринтер", Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЭтаФорма.ТекущийЭлемент = Элементы.Печать;     

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)

	Если КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Печатать", Истина)).Количество() > 0 Тогда
		
		ИменаМакетов = ПодготовитьНастройкиДляПечати();
		
		//++ЖВИ	            
		Если ИменаМакетов = "Документ.ПеремещениеТоваров.Накладная" И Источник = "Документ.сабМаршрутныйЛист" тогда  
			ТипОбъекта = "Документ.ПеремещениеТоваров";	
		КонецЕсли; 
		
		Если ИменаМакетов = "Документ.РозничнаяПродажа.ТоварныйЧек" И Источник = "Документ.сабМаршрутныйЛист" тогда
			ТипОбъекта = "Документ.РозничнаяПродажа";	
		КонецЕсли;
		 		
		ПараметрыОткрытия = Новый Структура("ФиксированныйКомплект", Истина);
		ПараметрыОткрытия.Вставить("ДокументыИсточника",ДокументыИсточника);
		
		СтрокаПП = "Обработка.ПечатьСчетаНаОплату.СчетЗаказКомплект";
		Инд1 = СтрНайти(ИменаМакетов,СтрокаПП);
		Если Инд1 > 0 тогда
			ИсключитьМакет = Истина;
			Для каждого СтрОбъект из Объекты цикл
				Если ТипЗнч(СтрОбъект.Значение) = Тип("ДокументСсылка.РеализацияТоваровУслуг") тогда
					ИсключитьМакет = Ложь;		
				КонецЕсли;
			КонецЦикла;
			Если ИсключитьМакет тогда
				Если Лев(Лев(ИменаМакетов,Инд1-1),1) = "," тогда 
					СтрокаПП = "," + СтрокаПП;
				КонецЕсли;
				Если Прав(Лев(ИменаМакетов,Инд1+СтрДлина(СтрокаПП)),1) = "," тогда 
					СтрокаПП = СтрокаПП + ",";
				КонецЕсли; 
				ИменаМакетов = СтрЗаменить(ИменаМакетов,СтрокаПП,"");	
			КонецЕсли;
		КонецЕсли;
		Если ИменаМакетов = "" тогда
			Сообщить("Нет документов для печати.");
			Возврат;
		КонецЕсли;
			
		//--ЖВИ 
		
		Если ПечатьСразуНаПринтер Тогда  
			
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(
				ТипОбъекта,
				ИменаМакетов,
				Объекты.ВыгрузитьЗначения(),
				ПараметрыОткрытия);
				
		Иначе
			
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
				ТипОбъекта,
				ИменаМакетов,
				Объекты.ВыгрузитьЗначения(),
				Неопределено,
				ПараметрыОткрытия);
				
		КонецЕсли;
		
		Если Не ПустаяСтрока(КлючНастроек) Тогда
			СохраняемыеНастройкиПечатныхФорм = Новый Массив;
			Для Каждого НастройкаПечати Из КомплектПечатныхФорм Цикл
				СохраняемаяНастройка = Новый Структура("Имя, Копий, Печатать");
				ЗаполнитьЗначенияСвойств(СохраняемаяНастройка, НастройкаПечати);
				СохраняемыеНастройкиПечатныхФорм.Добавить(СохраняемаяНастройка);
			КонецЦикла;
			СохранитьНастройкиПечатныхФорм("НастройкиПечатныхФормКомплект", КлючНастроек, СохраняемыеНастройкиПечатныхФорм);
		КонецЕсли;
		
		Закрыть();
		
	Иначе
		
		ОчиститьСообщения();
		ТекстСообщения = НСтр("ru = 'Выберите хотя бы одну печатную форму для печати.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КомплектПечатныхФорм");
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПечатьСразуНаПринтерПриИзменении(Элемент)

	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("ОбщиеНастройкиПользователя", "ПечатьКомплектаСразуНаПринтер", ПечатьСразуНаПринтер);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКомплектПечатныхФорм

&НаКлиенте
Процедура НастройкиПечатныхФормКоличествоРегулирование(Элемент, Направление, СтандартнаяОбработка)

	НастройкаПечатнойФормы = ТекущаяНастройкаПечатнойФормы();
	НастройкаПечатнойФормы.Печатать = НастройкаПечатнойФормы.Копий + Направление > 0;

КонецПроцедуры

&НаКлиенте
Процедура КомплектПечатныхФормКопийПриИзменении(Элемент)

	НастройкаПечатнойФормы = ТекущаяНастройкаПечатнойФормы();
	Если НастройкаПечатнойФормы.Копий = 0 Тогда
		НастройкаПечатнойФормы.Печатать = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиПечатныхФормПечататьПриИзменении(Элемент)

	НастройкаПечатнойФормы = ТекущаяНастройкаПечатнойФормы();
	Если НастройкаПечатнойФормы.Печатать И НастройкаПечатнойФормы.Копий = 0 Тогда
		НастройкаПечатнойФормы.Копий = 1;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПодготовитьНастройкиДляПечати()

	ИменаМакетов = "";
	СохраняемыеНастройкиПечатныхФорм = Новый Массив;
	Индекс = 0;
	Для Каждого ПечатнаяФорма Из КомплектПечатныхФорм Цикл
		Если ПечатнаяФорма.Печатать Тогда
			Если ПечатнаяФорма.Копий = 0 Тогда
				ПечатнаяФорма.Копий = 1;
			КонецЕсли;
			Если ПечатнаяФорма.МенеджерПечати <> ТипОбъекта Тогда
				Если ПечатнаяФорма.МенеджерПечати = "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда
					ИмяМакета = "ВнешняяПечатнаяФорма." + ПечатнаяФорма.Имя;
				Иначе
					ИмяМакета = ПечатнаяФорма.МенеджерПечати + "." + ПечатнаяФорма.Имя;
				КонецЕсли;
			Иначе
				ИмяМакета = ПечатнаяФорма.Имя;
			КонецЕсли;
			Если ПечатнаяФорма.Имя = "СчетФактураКомплект" Тогда
				КоличествоМакетовСчетовФактур = КоличествоМакетовСчетовФактур();
				Если КоличествоМакетовСчетовФактур > 1 Тогда
					Для Счетчик = 1 По КоличествоМакетовСчетовФактур - 1 Цикл
						СохраняемаяНастройка = Новый Структура;
						СохраняемаяНастройка.Вставить("ИмяМакета", ПечатнаяФорма.Имя);
						СохраняемаяНастройка.Вставить("Количество", ПечатнаяФорма.Копий);
						СохраняемаяНастройка.Вставить("ПозицияПоУмолчанию", Индекс);
						СохраняемыеНастройкиПечатныхФорм.Добавить(СохраняемаяНастройка);
						Индекс = Индекс + 1;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			СохраняемаяНастройка = Новый Структура;
			СохраняемаяНастройка.Вставить("ИмяМакета", ПечатнаяФорма.Имя);
			СохраняемаяНастройка.Вставить("Количество", ПечатнаяФорма.Копий);
			СохраняемаяНастройка.Вставить("ПозицияПоУмолчанию", Индекс);
			СохраняемыеНастройкиПечатныхФорм.Добавить(СохраняемаяНастройка);
			Индекс = Индекс + 1;

			// Для печати сразу на принтер строка с именами макетов
			// должна содержать столько имен макетов, сколько копий требуется напечатать.
			КоличествоМакетовДляПечати = ?(ПечатьСразуНаПринтер, ПечатнаяФорма.Копий, 1);
			Для Сч =1 по КоличествоМакетовДляПечати Цикл
				ИменаМакетов = ИменаМакетов +"," + ИмяМакета;
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	ИменаМакетов = Сред(ИменаМакетов, 2);
	
	// Сохранение настроек
	КлючНастроекПечати = ТипОбъекта + "-" + ИменаМакетов;
	Если Не ПустаяСтрока(ИменаМакетов) Тогда
		СохранитьНастройкиПечатныхФорм("НастройкиПечатныхФорм",КлючНастроекПечати, СохраняемыеНастройкиПечатныхФорм);
	КонецЕсли;
	
	Возврат ИменаМакетов;

КонецФункции

&НаСервере
Процедура ВосстановитьНастройкиПечатныхФорм(СохраненныеНастройкиПечатныхФорм, ИмяФормы)

	Если СохраненныеНастройкиПечатныхФорм = Неопределено
		ИЛИ (ТипЗнч(СохраненныеНастройкиПечатныхФорм) = Тип("Массив")
				И СохраненныеНастройкиПечатныхФорм.Количество() = 0) Тогда
		ВосстановитьНастройкиПечатныхФормПоУмолчанию(ИмяФормы);
	КонецЕсли;
	
	Для Каждого СохраненнаяНастройка Из СохраненныеНастройкиПечатныхФорм Цикл
		НайденныеНастройки = КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Имя", СохраненнаяНастройка.Имя));
		Для Каждого НастройкаПечатнойФормы Из НайденныеНастройки Цикл
			НастройкаПечатнойФормы.Копий = СохраненнаяНастройка.Копий;
			НастройкаПечатнойФормы.Печатать = СохраненнаяНастройка.Печатать;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиПечатныхФормПоУмолчанию(ИмяФормы)

	Если ТипОбъекта = "Документ.РеализацияТоваровУслуг" Тогда
		
		Если ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаУслуги" Тогда
			НайденныеНастройки =
				КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Имя", "Акт"));
			Для Каждого НастройкаПечатнойФормы Из НайденныеНастройки Цикл
				НастройкаПечатнойФормы.Копий = 2;
				НастройкаПечатнойФормы.Печатать = Истина;
			КонецЦикла;
			НайденныеНастройки =
				КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Имя", "СчетФактураКомплект"));
			Для Каждого НастройкаПечатнойФормы Из НайденныеНастройки Цикл
				НастройкаПечатнойФормы.Копий = 2;
				НастройкаПечатнойФормы.Печатать = Истина;
			КонецЦикла;
			ДобавитьВКомплектУПД(НайденныеНастройки);

			
		ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокументаТовары" Тогда
			НайденныеНастройки =
				КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Имя", "ТОРГ12_БезУслуг"));
			Для Каждого НастройкаПечатнойФормы Из НайденныеНастройки Цикл
				НастройкаПечатнойФормы.Копий = 2;
				НастройкаПечатнойФормы.Печатать = Истина;
			КонецЦикла;
			НайденныеНастройки =
				КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Имя", "СчетФактураКомплект"));
			Для Каждого НастройкаПечатнойФормы Из НайденныеНастройки Цикл
				НастройкаПечатнойФормы.Копий = 2;
				НастройкаПечатнойФормы.Печатать = Истина;
			КонецЦикла;
			ДобавитьВКомплектУПД(НайденныеНастройки);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ДобавитьВКомплектУПД(НайденныеНастройки)
	
	Перем НастройкаПечатнойФормы;
	
	Если НайденныеНастройки.Количество() = 0 Тогда
		НайденныеНастройки =
		КомплектПечатныхФорм.НайтиСтроки(Новый Структура("Имя", "УниверсальныйПередаточныйДокументКомплект"));
		Для Каждого НастройкаПечатнойФормы Из НайденныеНастройки Цикл
			НастройкаПечатнойФормы.Копий = 2;
			НастройкаПечатнойФормы.Печатать = Истина;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция КоличествоМакетовСчетовФактур()

	НастройкиПечати = УчетНДСВызовСервера.ПолучитьНастройкиПечатиСчетовФактур(Объекты.ВыгрузитьЗначения());
	СписокМакетов = НастройкиПечати.СписокМакетов;
	МассивМакетов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(НастройкиПечати.СписокМакетов);
	Возврат МассивМакетов.Количество();

КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьНастройкиПечатныхФорм(КлючОбъекта, КлючНастроек, СохраняемыеНастройкиПечатныхФорм)

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНастроек, СохраняемыеНастройкиПечатныхФорм);

КонецПроцедуры

&НаКлиенте
Функция ТекущаяНастройкаПечатнойФормы()

	Результат = Элементы.КомплектПечатныхФорм.ТекущиеДанные;
	Если Результат = Неопределено И КомплектПечатныхФорм.Количество() > 0 Тогда
		Результат = КомплектПечатныхФорм[0];
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти
