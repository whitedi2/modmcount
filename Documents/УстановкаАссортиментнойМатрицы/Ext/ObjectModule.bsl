
Процедура ОбработкаПроведения(Отказ, Режим)

	ЗапросПоПодразделениям = Новый Запрос;
	ЗапросПоПодразделениям.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтруктураПредприятия.Родитель
	|ПОМЕСТИТЬ ВТ_ЭлементыЯвляющиесяГруппами
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ(&Подразделения)
	|	И НЕ СтруктураПредприятия.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_ЭлементыЯвляющиесяГруппами.Родитель
	|				ИЗ
	|					ВТ_ЭлементыЯвляющиесяГруппами КАК ВТ_ЭлементыЯвляющиесяГруппами)";
	ЗапросПоПодразделениям.УстановитьПараметр("Подразделения", Подразделения.ВыгрузитьКолонку("Подразделение"));
	ВыборкаПоПодразделениям = ЗапросПоПодразделениям.Выполнить().Выбрать();
	
	Движения.АссортиментнаяМатрица.Записывать = Истина;
	Пока ВыборкаПоПодразделениям.Следующий() Цикл
		Для Каждого ТекСтрокаТовары Из Товары Цикл
			Движение = Движения.АссортиментнаяМатрица.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
			Движение.Подразделение = ВыборкаПоПодразделениям.Ссылка;
			Движение.Предприятие = Предприятие;
			Если ВидОперации = Перечисления.ВидыОперацииУстановкиТоварнойМатрицы.УдалениеИзАссортимента Тогда
				Движение.ВыведенИзАссортимента = Истина;
			КонецЕсли;	
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	//из подписки на событие
	БюджетныйНаСервере.ДокументыПередЗаписьюПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	БюджетныйНаСервере.ПриУстановкеНовогоНомераПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры
