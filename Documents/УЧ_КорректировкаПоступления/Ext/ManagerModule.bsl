Процедура ЗаполнитьПоДокументуОснованию(Объект) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.ДокОснование) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Товары.Очистить();
	
	Если ТипЗнч(Объект.ДокОснование) = Тип("ДокументСсылка.УЧ_ПоступлениеТоваров") Тогда
		// Заполнение шапки
		
		Запрос = новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	УЧ_ПоступлениеТоваров.Ссылка КАК Ссылка,
		               |	УЧ_ПоступлениеТоваров.Предприятие КАК Предприятие,
		               |	УЧ_ПоступлениеТоваров.СчетКонтрагента КАК СчетКонтрагента,
		               |	УЧ_ПоступлениеТоваров.Контрагент КАК Контрагент,
		               |	УЧ_ПоступлениеТоваров.Договор КАК Договор,
		               |	УЧ_ПоступлениеТоваров.Подразделение КАК Подразделение,
		               |	УЧ_ПоступлениеТоваров.ФлагВалюты КАК ФлагВалюты,
		               |	УЧ_ПоступлениеТоваров.Валюта КАК Валюта,
		               |	УЧ_ПоступлениеТоваров.Курс КАК Курс,
		               |	УЧ_ПоступлениеТоваров.УчитыватьНДС КАК УчитыватьНДС,
		               |	УЧ_ПоступлениеТоваров.Организация КАК Организация,
		               |	УЧ_ПоступлениеТоваров.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
		               |	УЧ_ПоступлениеТоваров.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
		               |	УЧ_ПоступлениеТоваров.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		               |	УЧ_ПоступлениеТоваров.Склад КАК Склад,
		               |	УЧ_ПоступлениеТоваров.ТабличнаяЧасть.(
		               |		Ссылка КАК Ссылка,
		               |		Номенклатура КАК Номенклатура,
		               |		Склад КАК Склад,
		               |		Количество КАК Количество,
		               |		Цена КАК Цена,
		               |		Сумма КАК Сумма,
		               |		СуммаБезНДС КАК СуммаБезНДС,
		               |		СтавкаНДС КАК СтавкаНДС,
		               |		СуммаНДС КАК СуммаНДС,
		               |		СчетУчетаНДС КАК СчетУчетаНДС,
		               |		ИдентификаторСтроки КАК ИдентификаторСтроки,
		               |		АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		               |		Характеристика КАК Характеристика,
		               |		Штрихкод КАК Штрихкод
		               |	) КАК ТабличнаяЧасть,
		               |	УЧ_ПоступлениеТоваров.УчетПартий.(
		               |		Ссылка КАК Ссылка,
		               |		НомерСтроки КАК НомерСтроки,
		               |		Номенклатура КАК Номенклатура,
		               |		Количество КАК Количество,
		               |		СуммаБезНДС КАК СуммаБезНДС,
		               |		СуммаНДС КАК СуммаНДС,
		               |		ВидДвижения КАК ВидДвижения
		               |	) КАК УчетПартий
		               |ИЗ
		               |	Документ.УЧ_ПоступлениеТоваров КАК УЧ_ПоступлениеТоваров
		               |ГДЕ
		               |	УЧ_ПоступлениеТоваров.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Объект.ДокОснование);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЗаполнитьЗначенияСвойств(Объект, Выборка,, "УчетПартий, ТабличнаяЧасть");
			//Объект.Дата = ТекущаяДата();
			Объект.ЦенаВключаетНДС = Истина;  //в поступлениях подразумеваем, что всегда включает
			
			ВыборкаТЧ = Выборка.ТабличнаяЧасть.Выбрать();
			Пока ВыборкаТЧ.Следующий() Цикл
				НоваяСтрока = Объект.Товары.Добавить();
				НоваяСтрока.АлкогольнаяПродукция 	= ВыборкаТЧ.АлкогольнаяПродукция;
				НоваяСтрока.КоличествоДоИзменения 	= ВыборкаТЧ.Количество;
				НоваяСтрока.Количество 				= ВыборкаТЧ.Количество;
				НоваяСтрока.Номенклатура 	= ВыборкаТЧ.Номенклатура;
				НоваяСтрока.Склад 			= ВыборкаТЧ.Склад;
				НоваяСтрока.СтавкаНДСДоИзменения 	= ВыборкаТЧ.СтавкаНДС;
				НоваяСтрока.СтавкаНДС 				= ВыборкаТЧ.СтавкаНДС;
				НоваяСтрока.СуммаДоИзменения 	= ВыборкаТЧ.Сумма;
				НоваяСтрока.Сумма 				= ВыборкаТЧ.Сумма;
				НоваяСтрока.СуммаНДСДоИзменения = ВыборкаТЧ.СуммаНДС;
				НоваяСтрока.СуммаНДС 			= ВыборкаТЧ.СуммаНДС;
				НоваяСтрока.ЦенаДоИзменения 	= ВыборкаТЧ.Цена;
				НоваяСтрока.Цена 				= ВыборкаТЧ.Цена;
				НоваяСтрока.ЕстьВДокументеПоступления = Истина;
			КонецЦикла;
			
			ВыборкаУчетПартий = Выборка.УчетПартий.Выбрать();
			Пока ВыборкаУчетПартий.Следующий() Цикл
				
				НоваяСтрока = Объект.УчетПартий.Добавить();
				НоваяСтрока.Номенклатура = ВыборкаУчетПартий.Номенклатура;
				//НоваяСтрока.КоличествоДоИзменения 	= ВыборкаУчетПартий.Количество;
				НоваяСтрока.Количество 				= ВыборкаУчетПартий.Количество;
				//НоваяСтрока.СуммаБезНДСДоИзменения 	= ВыборкаУчетПартий.СуммаБезНДС;
				НоваяСтрока.СуммаБезНДС 			= ВыборкаУчетПартий.СуммаБезНДС;
				//НоваяСтрока.СуммаНДСДоИзменения 	= ВыборкаУчетПартий.СуммаНДС;
				НоваяСтрока.СуммаНДС 				= ВыборкаУчетПартий.СуммаНДС;
				НоваяСтрока.ВидДвижения 			= ВыборкаУчетПартий.ВидДвижения;
				//НоваяСтрока.ЕстьВДокументеПоступления = Истина;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры