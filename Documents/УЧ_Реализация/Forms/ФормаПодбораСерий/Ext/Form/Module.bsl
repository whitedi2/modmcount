
&НаКлиенте
Процедура СерииНоменклатурыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элементы.СерииНоменклатуры.ТекущиеДанные.Номенклатура = Номенклатура;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьИЗакрыть(Команда)
	Если ДокументПодбора = "УЧ_ВыпускПродукции" Тогда
		Если СерииНоменклатуры.Итог("Количество") > Количество Тогда
			Сообщить("Превышено выбираемое количество");
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	СтруктураПараметров = ПолучитьМассивСтруктур();
	Закрыть(Новый Структура("НомерСтрокиРеализации, СерииНоменклатуры", НомерСтрокиРеализации, СтруктураПараметров));
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивСтруктур()
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(СерииНоменклатуры.Выгрузить());	
КонецФункции // ()


&НаКлиенте
Процедура СерииНоменклатурыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	КоличествоНовое = СерииНоменклатуры.Итог("Количество");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	КоличествоНовое = СерииНоменклатуры.Итог("Количество");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПроцентОСГ") Тогда
		Если Параметры.ПроцентОСГ Тогда
			ПроцентОСГ = Параметры.ПроцентОСГ;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("ОСГ") Тогда
		Если Параметры.ОСГ Тогда
			Для каждого ЭлементУсловногоОформления Из УсловноеОформление.Элементы Цикл
				Если ЭлементУсловногоОформления.Представление = "ОформлениеОСГ" Тогда
					ЭлементУсловногоОформления.Использование = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

  
&НаКлиенте
Процедура СерииНоменклатурыСерияНоменклатурыНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекДанные = Элементы.СерииНоменклатуры.ТекущиеДанные;
	НовыйПараметрКоличество = Новый ПараметрВыбора("КоличествоПодбора",ТекДанные.Количество); 
	МассивПараметры = Новый Массив(Элемент.ПараметрыВыбора);
	Для Каждого ПараметрВыбораЭлементМассива Из МассивПараметры Цикл
		Если ПараметрВыбораЭлементМассива.Имя = "КоличествоПодбора" Тогда 
			ПраметрКоличествоИндекс = МассивПараметры.Найти(ПараметрВыбораЭлементМассива);
			Если ПраметрКоличествоИндекс <> Неопределено Тогда
				МассивПараметры.Установить(ПраметрКоличествоИндекс,НовыйПараметрКоличество);
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ФиксМассивПараметры = Новый ФиксированныйМассив(МассивПараметры);
	Элемент.ПараметрыВыбора = ФиксМассивПараметры;

КонецПроцедуры

&НаКлиенте
Процедура СерииНоменклатурыСерияНоменклатурыПриИзменении(Элемент)
	
	ТекДанные = Элементы.СерииНоменклатуры.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекДанные.СерияНоменклатуры) Тогда
		ТекДанные.ОСГ = РассчитатьОСГНаСервере(ТекДанные.СерияНоменклатуры);
	Иначе
		ТекДанные.ОСГ = 0;
	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция РассчитатьОСГНаСервере(СерияНоменклатуры)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РАЗНОСТЬДАТ(&ТекущаяДата, СерииНоменклатуры.ГоденДо, ДЕНЬ) КАК ОсталосьДнейСрокаГодности,
		|	ВЫБОР
		|		КОГДА СерииНоменклатуры.СрокГодности = 0
		|			ТОГДА РАЗНОСТЬДАТ(СерииНоменклатуры.ДатаПроизводства, СерииНоменклатуры.ГоденДо, ДЕНЬ)
		|		ИНАЧЕ СерииНоменклатуры.СрокГодности
		|	КОНЕЦ КАК СрокГодности
		|ИЗ
		|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|ГДЕ
		|	СерииНоменклатуры.Ссылка = &Ссылка";  
	Запрос.УстановитьПараметр("Ссылка", СерияНоменклатуры);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
    Возврат Окр(Выборка.ОсталосьДнейСрокаГодности / Выборка.СрокГодности * 100,0,РежимОкругления.Окр15как20);
	
КонецФункции

