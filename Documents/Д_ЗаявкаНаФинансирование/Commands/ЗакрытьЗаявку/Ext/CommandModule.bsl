
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Задача") Тогда
		СтруктураПараметров = БюджетныйНаСервере.ВернутьРеквизиты(ПараметрКоманды, "БизнесПроцесс, Заявка");
		ТекБП = СтруктураПараметров.БизнесПроцесс;
		ПараметрКоманды = СтруктураПараметров.Заявка;
	Иначе
		ТекБП = БПСервер.НайтиТекущийБПСервер(ПараметрКоманды);
	КонецЕсли;
	
	Если НЕ ТекБП = Неопределено Тогда
		
		Если Не БПСервер.СтадияИсполненияОбщая(ТекБП) Тогда
			ПоказатьПредупреждение(Неопределено, "Бизнес-процесс по данному документу находится на стадии согласования. Для закрытия или изменения заявки воспользуйтесь функцией Редактирования или установите Пометку на удаление заявки.");
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, Новый Структура("ПараметрКоманды", ПараметрКоманды)), "При закрытии заявки произойдет очистка строк графика в Платежном календаре, а также статус заявки станет Оплачено. Продолжить?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ПараметрКоманды = ДополнительныеПараметры.ПараметрКоманды;
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗакрытьЗаявкуНаСервере(ПараметрКоманды);
		ОповеститьОбИзменении(ПараметрКоманды);
		Оповестить("сабОбноватьПлатежныйКалендарь");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗакрытьЗаявкуНаСервере(ЗаявкаСсылка)
	
	//очищаем Гтрафик
	НаборЗаписей = РегистрыСведений.сабГрафикПлатежей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ЗаявкаСсылка);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
	
	//выполняем заявки на оплату
	БПСервер.ВыполнитьЗадачиИсполненияЗаявокаОплату(ЗаявкаСсылка, "Автовыполнение при старте оплаты Реестра");
	
	//акцептуем заявку
	ЗаявкаНаОплатуОбъект = ЗаявкаСсылка.ПолучитьОбъект();
	ЗаявкаНаОплатуОбъект.Акцептован = Истина;
	ЗаявкаНаОплатуОбъект.Записать();

КонецПроцедуры

