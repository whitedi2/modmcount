
&После("ДобавитьКомандыПечати")
Процедура УУ_ДобавитьКомандыПечати(КомандыПечати)
	
	// Товарная накладная (ТОРГ-12) за поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ12_БезУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12) за поставщика'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	//КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая";
	//КомандаПечати.Порядок       = 60;
	//УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);

КонецПроцедуры

&Вместо("Печать")
Процедура УУ_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
																"Накладная",
																"Возврат от покупателя",
																ПечатьВозвратаОтПокупателя(МассивОбъектов, ОбъектыПечати),
																,
																"Документ.ВозвратТоваровОтПокупателя.ПФ_MXL_Накладная");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УниверсальныйПередаточныйДокументКомплект") Тогда
		ИменаФайлов = Неопределено;
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, "УниверсальныйПередаточныйДокумент", "УПД",
			УчетНДС.ПечатьКомплектаУПД(
				КоллекцияПечатныхФорм, МассивОбъектов, ОбъектыПечати, ИменаФайлов, ПараметрыПечати),,, ИменаФайлов);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12_БезУслуг") Тогда
		ВключатьУслуги = Ложь;
		ТаблицаСведенийТОРГ12 = ПолучитьТаблицуСведенийТОРГ12Покупатель(МассивОбъектов, ВключатьУслуги);
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаПоступления_ТОРГ12");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ12_БезУслуг", "ТОРГ-12 (Товарная накладная за поставщика)",
		ПечатьТорговыхДокументов.ПечатьТОРГ12(ТаблицаСведенийТОРГ12, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_ТОРГ12");
	КонецЕсли;
		
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

Функция ПолучитьТаблицуСведенийТОРГ12Покупатель(Знач МассивОбъектов, Знач ВключатьУслуги) Экспорт
	
	ТаблицаСведений = ПечатьТорговыхДокументов.ПолучитьОписаниеТОРГ12();
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	Запрос.Параметры.Вставить("ВключатьУслуги", ВключатьУслуги);
	
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияТаблицыСведенийТОРГ12(); 
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	РеквизитыВалютыРегУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаРегУчета, "Код, Наименование");
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	СтрокиДокументов = РезультатыЗапроса[3].Выгрузить();
	СтрокиДокументов.Индексы.Добавить("Документ");
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОДокументе = ТаблицаСведений.Добавить();
		ЗаполнитьЗначенияСвойств(СведенияОДокументе, Выборка);
		СведенияОДокументе.Валюта             = ВалютаРегУчета;
		СведенияОДокументе.ВалютаКод          = РеквизитыВалютыРегУчета.Код;
		СведенияОДокументе.ВалютаНаименование = РеквизитыВалютыРегУчета.Наименование;
		
		ТаблицаДокумента = ПечатьТорговыхДокументов.ПолучитьОписаниеТаблицыТОРГ12();
		
		Отбор = Новый Структура("Документ", Выборка.Документ);
		СтрокиДокумента = СтрокиДокументов.НайтиСтроки(Отбор);
		
		НуженПересчетВРубли = Ложь;
		Если Выборка.ВалютаДокумента <> ВалютаРегУчета Тогда
			НуженПересчетВРубли	= Истина;
		КонецЕсли;
		
		Для Каждого Строка Из СтрокиДокумента Цикл
			
			СтрокаТаблицыДокумента = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокумента, Строка);
			
			Если НуженПересчетВРубли Тогда
				Если Строка.СуществуютЗаписиРублевыхСумм Тогда
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.НДСВРублях;
					СтрокаТаблицыДокумента.СуммаБезНДС 	= Строка.ВсегоВРублях - Строка.НДСВРублях;
				Иначе
					Кратность = ?(Выборка.Кратность 	= 0, 1, Выборка.Кратность);
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.СуммаНДС * Выборка.Курс / Кратность;;
					СтрокаТаблицыДокумента.СуммаБезНДС 	= (Строка.СуммаБезНДС + Строка.СуммаНДС) * Выборка.Курс / Кратность - СтрокаТаблицыДокумента.СуммаНДС;
				КонецЕсли;
			КонецЕсли;
			СтрокаТаблицыДокумента.СуммаСНДС = СтрокаТаблицыДокумента.СуммаБезНДС + СтрокаТаблицыДокумента.СуммаНДС;
			СтрокаТаблицыДокумента.Цена = ?(СтрокаТаблицыДокумента.Количество = 0, СтрокаТаблицыДокумента.СуммаБезНДС, СтрокаТаблицыДокумента.СуммаБезНДС / СтрокаТаблицыДокумента.Количество);
			
		КонецЦикла;
		
		СведенияОДокументе.ТаблицаДокумента = ТаблицаДокумента;
		
	КонецЦикла;
	
	Возврат ТаблицаСведений;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляФормированияТаблицыСведенийТОРГ12()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК Всего,
|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДС,
|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
|ИЗ
|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
|ГДЕ
|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
|
|ИНДЕКСИРОВАТЬ ПО
|	Регистратор,
|	НомерСтрокиДокумента,
|	ТабличнаяЧастьДокумента
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВозвратТоваровОтПокупателя.Ссылка КАК Ссылка,
|	ВозвратТоваровОтПокупателя.СуммаВключаетНДС КАК СуммаВключаетНДС,
|	ВозвратТоваровОтПокупателя.Дата КАК ДатаДокумента
|ПОМЕСТИТЬ ДокументыДляПечати
|ИЗ
|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
|ГДЕ
|	ВозвратТоваровОтПокупателя.Ссылка В(&МассивОбъектов)
|
|ИНДЕКСИРОВАТЬ ПО
|	Ссылка
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВозвратТоваровОтПокупателя.Ссылка КАК Документ,
|	ВозвратТоваровОтПокупателя.Дата КАК ДатаДляПолученияСведений,
|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
|	ВозвратТоваровОтПокупателя.Контрагент КАК Организация,
|	"""" КАК Подразделение,
|	ВозвратТоваровОтПокупателя.Контрагент КАК Поставщик,
|	ВозвратТоваровОтПокупателя.Организация КАК Покупатель,
|	ВЫБОР
|		КОГДА ВозвратТоваровОтПокупателя.Сделка.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
|			ТОГДА ВозвратТоваровОтПокупателя.Организация
|		ИНАЧЕ ВозвратТоваровОтПокупателя.Сделка.Грузоотправитель
|	КОНЕЦ КАК Грузополучатель,
|	ВЫБОР
|		КОГДА ВозвратТоваровОтПокупателя.Сделка.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
|			ТОГДА ВозвратТоваровОтПокупателя.Контрагент
|		ИНАЧЕ ВозвратТоваровОтПокупателя.Сделка.Грузополучатель
|	КОНЕЦ КАК Грузоотправитель,
|	ВозвратТоваровОтПокупателя.ДоговорКонтрагента КАК Договор,
|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.ДоговорКонтрагента.Представление, """") КАК Основание,
|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.ДоговорКонтрагента.Номер, """") КАК ОснованиеНомер,
|	ЕСТЬNULL(ВозвратТоваровОтПокупателя.ДоговорКонтрагента.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ОснованиеДата,
|	ВозвратТоваровОтПокупателя.ВалютаДокумента КАК ВалютаДокумента,
|	ВозвратТоваровОтПокупателя.КурсВзаиморасчетов КАК Курс,
|	ВозвратТоваровОтПокупателя.КратностьВзаиморасчетов КАК Кратность
|ИЗ
|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
|		ПО ВозвратТоваровОтПокупателя.Организация = ДанныеПервичныхДокументов.Организация
|			И ВозвратТоваровОтПокупателя.Ссылка = ДанныеПервичныхДокументов.Документ
|ГДЕ
|	ВозвратТоваровОтПокупателя.Ссылка В
|			(ВЫБРАТЬ
|				ДокументыДляПечати.Ссылка
|			ИЗ
|				ДокументыДляПечати КАК ДокументыДляПечати)
|
|УПОРЯДОЧИТЬ ПО
|	ДатаДокумента,
|	Документ
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Документ,
|	ДокументыДляПечати.ДатаДокумента КАК ДатаДокумента,
|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары) КАК ТабличнаяЧасть,
|	2 КАК ПорядокТабличнойЧасти,
|	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтроки,
|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Товар,
|	ВозвратТоваровОтПокупателяТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
|	ВЫБОР
|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
|			ТОГДА ВозвратТоваровОтПокупателяТовары.Номенклатура.Артикул
|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
|			ТОГДА ВозвратТоваровОтПокупателяТовары.Номенклатура.Код
|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
|			ТОГДА """"
|	КОНЕЦ КАК ТоварКод,
|	ВозвратТоваровОтПокупателяТовары.Количество КАК Количество,
|	0 КАК КоличествоМест,
|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.Номенклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
|	ВозвратТоваровОтПокупателяТовары.Номенклатура.ЕдиницаИзмерения КАК ВидУпаковки,
|	0 КАК Коэффициент,
|	0 КАК КоличествоВОдномМесте,
|	ВозвратТоваровОтПокупателяТовары.Цена КАК Цена,
|	ВЫБОР
|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
|			ТОГДА ВозвратТоваровОтПокупателяТовары.Сумма - ВозвратТоваровОтПокупателяТовары.СуммаНДС
|		ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Сумма
|	КОНЕЦ КАК СуммаБезНДС,
|	ВозвратТоваровОтПокупателяТовары.СуммаНДС КАК СуммаНДС,
|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС КАК СтавкаНДС,
|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоВРублях,
|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСВРублях,
|	ВЫБОР
|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ КАК СуществуютЗаписиРублевыхСумм
|ИЗ
|	ДокументыДляПечати КАК ДокументыДляПечати
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
|		ПО ДокументыДляПечати.Ссылка = ВозвратТоваровОтПокупателяТовары.Ссылка
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
|		ПО (ВозвратТоваровОтПокупателяТовары.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
|			И (ВозвратТоваровОтПокупателяТовары.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	ВозвратТоваровОтПокупателяВозвратнаяТара.Ссылка,
|	ДокументыДляПечати.ДатаДокумента,
|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ВозвратнаяТара),
|	5,
|	ВозвратТоваровОтПокупателяВозвратнаяТара.НомерСтроки,
|	ВозвратТоваровОтПокупателяВозвратнаяТара.Номенклатура,
|	ВозвратТоваровОтПокупателяВозвратнаяТара.Номенклатура.НаименованиеПолное,
|	ВЫБОР
|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
|			ТОГДА ВозвратТоваровОтПокупателяВозвратнаяТара.Номенклатура.Артикул
|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
|			ТОГДА ВозвратТоваровОтПокупателяВозвратнаяТара.Номенклатура.Код
|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
|			ТОГДА """"
|	КОНЕЦ,
|	ВозвратТоваровОтПокупателяВозвратнаяТара.Количество,
|	0,
|	ЕСТЬNULL(ВозвратТоваровОтПокупателяВозвратнаяТара.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
|	ЕСТЬNULL(ВозвратТоваровОтПокупателяВозвратнаяТара.Номенклатура.ЕдиницаИзмерения.Код, """"),
|	ЕСТЬNULL(ВозвратТоваровОтПокупателяВозвратнаяТара.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
|	NULL,
|	0,
|	0,
|	ВозвратТоваровОтПокупателяВозвратнаяТара.Цена,
|	ВозвратТоваровОтПокупателяВозвратнаяТара.Сумма,
|	0,
|	NULL,
|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
|	ВЫБОР
|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
|			ТОГДА ЛОЖЬ
|		ИНАЧЕ ИСТИНА
|	КОНЕЦ
|ИЗ
|	ДокументыДляПечати КАК ДокументыДляПечати
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.ВозвратнаяТара КАК ВозвратТоваровОтПокупателяВозвратнаяТара
|		ПО ДокументыДляПечати.Ссылка = ВозвратТоваровОтПокупателяВозвратнаяТара.Ссылка
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
|		ПО (ВозвратТоваровОтПокупателяВозвратнаяТара.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
|			И (ВозвратТоваровОтПокупателяВозвратнаяТара.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ВозвратнаяТара))
|
|УПОРЯДОЧИТЬ ПО
|	ДатаДокумента,
|	Документ,
|	ПорядокТабличнойЧасти,
|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции
