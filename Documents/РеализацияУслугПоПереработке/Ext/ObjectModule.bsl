
&После("ОбработкаЗаполнения")
Процедура УУ_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УЧ_Реализация") Тогда
		ВыборкаСоотв = УЧ_Сервер.ПолучитьСоотвСчетовБУУУ();
		СчетБУ20_02 = Планысчетов.Хозрасчетный.НайтиПоКоду("20.02");
		СчетБУ90_01 = Планысчетов.Хозрасчетный.НайтиПоКоду("90.01.1");
		СчетБУ90_02 = Планысчетов.Хозрасчетный.НайтиПоКоду("90.02.1");
		СчетБУ90_03 = Планысчетов.Хозрасчетный.НайтиПоКоду("90.03");
		СчетБУ003_2 = Планысчетов.Хозрасчетный.НайтиПоКоду("003.02");
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,,"Проведен"); 
		
		ДоговорКонтрагентаДЗ = ДанныеЗаполнения.Договор;
		
		Если ТипЗнч(ДанныеЗаполнения.Контрагент) = Тип("СправочникСсылка.Организации") Тогда  
			ИННОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Контрагент, "ИНН");
			ЭтотОбъект.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН", ИННОрганизации);
			
			Если ЗначениеЗаполнено(ДанныеЗаполнения.Договор) Тогда
				ДоговорКонтрагентаДЗ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Договор, "ДоговорКонтрагента");
				
				Если ЗначениеЗаполнено(ДоговорКонтрагентаДЗ) Тогда 
					ЭтотОбъект.Контрагент = ДоговорКонтрагентаДЗ.Владелец;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЭтотОбъект.СчетУчетаРасчетовСКонтрагентом = Планысчетов.Хозрасчетный.НайтиПоКоду("62.01");
		ЭтотОбъект.СчетУчетаРасчетовПоАвансам = Планысчетов.Хозрасчетный.НайтиПоКоду("62.02");  
		ЭтотОбъект.СуммаВключаетНДС = Истина;
		ЭтотОбъект.ДокументБезНДС = Ложь;
		ЭтотОбъект.ДоговорКонтрагента = ДоговорКонтрагентаДЗ;
		ЭтотОбъект.ТипЦен = ЭтотОбъект.ДоговорКонтрагента.ТипЦен;
		ЭтотОбъект.ЭтоУниверсальныйДокумент = Истина;
		ЭтотОбъект.ВалютаДокумента = ДанныеЗаполнения.Валюта;
		//Ответственным текущий пользователь
		ЭтотОбъект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		//
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект.ВалютаДокумента) Тогда
			ЭтотОбъект.ВалютаДокумента = УЧ_Сервер.НациональнаяВалюта();		
		КонецЕсли; 
						
		Для каждого ТекСтрока Из ДанныеЗаполнения.Услуги Цикл
			НоваяСтрока = ЭтотОбъект.Услуги.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);  
			РеквизитыУслуги = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекСтрока.Субконто1, "ЕдиницаИзмерения, СтавкаНДС"); 
			НоваяСтрока.СтавкаНДС = сабОбщегоНазначенияБУХПовтИсп.СоотвСтавокНДСУУБУХ().Получить(ТекСтрока.СтавкаНДС);
			НоваяСтрока.Номенклатура = ТекСтрока.Субконто1;
			НоваяСтрока.Содержание = ТекСтрока.СодержаниеУслуги;
			НоваяСтрока.ЕдиницаИзмерения = РеквизитыУслуги.ЕдиницаИзмерения;
			НоваяСтрока.СчетУчета = СчетБУ20_02;
			НоваяСтрока.СчетДоходов = СчетБУ90_01;
			НоваяСтрока.СчетРасходов = СчетБУ90_02;
			НоваяСтрока.СчетУчетаНДСПоРеализации = СчетБУ90_03;
		КонецЦикла;
		
		Для каждого ТекСтрока Из ДанныеЗаполнения.МатериалыЗаказчика Цикл
			НоваяСтрока = ЭтотОбъект.МатериалыЗаказчика.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока); 
			СтрокаСоответствияСчета = ВыборкаСоотв.Найти(ТекСтрока.СчетУчета, "СчетУУ");
			
			Если Не СтрокаСоответствияСчета = Неопределено Тогда
				НоваяСтрока.СчетУчета = СтрокаСоответствияСчета.СчетБУ;
			Иначе
				НоваяСтрока.СчетУчета = СчетБУ003_2;
			КонецЕсли;

		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры
