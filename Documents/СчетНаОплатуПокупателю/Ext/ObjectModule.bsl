

&Перед("ОбработкаЗаполнения")
Процедура УУ_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	СчетНаОплатуПокупателюТовары.Номенклатура КАК Номенклатура,
		               |	СУММА(СчетНаОплатуПокупателюТовары.Количество) КАК Количество,
		               |	СУММА(СчетНаОплатуПокупателюТовары.Сумма) КАК Сумма,
		               |	СУММА(СчетНаОплатуПокупателюТовары.СуммаНДС) КАК СуммаНДС
		               |ИЗ
		               |	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателюТовары
		               |ГДЕ
		               |	СчетНаОплатуПокупателюТовары.Ссылка.Проведен = ИСТИНА
		               |	И СчетНаОплатуПокупателюТовары.Ссылка.Заказ = &Заказ
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	СчетНаОплатуПокупателюТовары.Номенклатура";
		
		Запрос.УстановитьПараметр("Заказ", ДанныеЗаполнения);
		
		Результат = Запрос.Выполнить();
		ВыборкаСчета = Результат.Выгрузить();
		
		СуммаПоСчетам = ВыборкаСчета.Итог("Сумма");
		
		//+lud 17/10/22 Проверка повторного создания документов из заказа по обр. №7861 от 12.10.2022 {
		Отказ = сабОперОбщегоНазначения.ПроверкаСозданияНаОснованииНаСервере(ДанныеЗаполнения, "", СтандартнаяОбработка, ТипЗнч(Ссылка));
		Если СуммаПоСчетам >= ДанныеЗаполнения.СуммаДокумента И Отказ.Признак = "##УжеСоздан" Тогда
			ВызватьИсключение "На основании заказа уже введен документы на всю сумму заказа " + Отказ.СписокДокументов;
		ИначеЕсли Отказ.Признак = "##НеПроведен" Тогда
			ВызватьИсключение "Документ заказ клиента не проведен. Ввод на основании не возможен.";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,,"Дата, Номер, ВозвратнаяТара");
		ЭтотОбъект.ВидОперации = Перечисления.ВидыОперацийСчетаПокупателю.ТоварыИУслуги;
		ЭтотОбъект.Заказ = ДанныеЗаполнения;
		ЭтотОбъект.СуммаВключаетНДС = Истина;
		ЭтотОбъект.ДокументБезНДС = Ложь;
		ЭтотОбъект.ДоговорКонтрагента = ДанныеЗаполнения.Договор;
		ЭтотОбъект.ТипЦен = ЭтотОбъект.ДоговорКонтрагента.ТипЦен;
		ЭтотОбъект.СтруктурнаяЕдиница = ЭтотОбъект.Организация.ОсновнойБанковскийСчет;
		ЭтотОбъект.ОрганизацияПолучатель = ЭтотОбъект.Организация;
		//Ответственным текущий пользователь
		ЭтотОбъект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		//
		ЭтотОбъект.Товары.Очистить();
		МассивПустых = Новый Массив;
		Для каждого ТекСтрока Из ДанныеЗаполнения.ТабличнаяЧасть Цикл
			НоваяСтрока = ЭтотОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			НайденныеСтроки = ВыборкаСчета.НайтиСтроки(Новый Структура("Номенклатура", ТекСтрока.Номенклатура));
			Для каждого ТекНайдСтрока Из НайденныеСтроки Цикл
				НоваяСтрока.Количество = НоваяСтрока.Количество - ТекНайдСтрока.Количество;
				НоваяСтрока.Сумма = НоваяСтрока.Сумма - ТекНайдСтрока.Сумма;
				НоваяСтрока.СуммаНДС = НоваяСтрока.СуммаНДС - ТекНайдСтрока.СуммаНДС;
			КонецЦикла;
			Если Не НоваяСтрока.Количество Тогда
				МассивПустых.Добавить(НоваяСтрока);
			КонецЕсли;
			НоваяСтрока.Цена = ?(НоваяСтрока.Количество, НоваяСтрока.Сумма / НоваяСтрока.Количество, 0);
			НоваяСтрока.СтавкаНДС = сабОбщегоНазначенияБУХПовтИсп.СоотвСтавокНДСУУБУХ().Получить(ТекСтрока.СтавкаНДС);
		КонецЦикла;
		
		Для каждого ТекУД Из МассивПустых Цикл
			ЭтотОбъект.Товары.Удалить(ТекУД);	
		КонецЦикла;
		
	КонецЕсли; 
КонецПроцедуры	

&Перед("ПриЗаписи")
Процедура УУ_ПриЗаписи(Отказ)
	
	сабОбщегоНазначения.сабПлатКалПриЗаписиОбъектовПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура УУ_ОбработкаЗаполнения1(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	сабМониторВнедрения.Значение КАК Значение
		|ИЗ
		|	Справочник.сабМониторВнедрения КАК сабМониторВнедрения
		|ГДЕ
		|	сабМониторВнедрения.Наименование = ""СоздаватьСчетаПокупателямСДатойРеализации""";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			Если ВыборкаДетальныеЗаписи.Значение Тогда
				ЭтотОбъект.Дата = ДанныеЗаполнения.Дата;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&После("ПриУстановкеНовогоНомера")
Процедура УУ_ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	БюджетныйНаСервере.ПриУстановкеНовогоНомераПриУстановкеНовогоНомераБУ(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры
