
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если РучнаяКорректировка Тогда		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Движения документа %1 отредактированы вручную и не могут быть автоматически актуализированы'"), ЭтотОбъект);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.КлючДанных = Ссылка;
		Сообщение.Сообщить();		
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Движения.Учетный.Очистить();
	Движения.Учетный.Записать(Истина); 
	
	Если СчетКурсовйРазницы = ПланыСчетов.Учетный.Счет98() Тогда
		Движения.УЧ_ДоходыИРасходыБудПериодов.Записывать = Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	УчетныйОстатки.Счет,
	               |	УчетныйОстатки.Субконто1,
	               |	УчетныйОстатки.Субконто2,
	               |	УчетныйОстатки.Субконто3,
	               |	УчетныйОстатки.СуммаОстаток,
	               |	УчетныйОстатки.ВалютнаяСуммаОстаток,
	               |	УчетныйОстатки.Валюта,
	               |	УчетныйОстатки.Подразделение,
	               |	УчетныйОстатки.СуммаРазвернутыйОстатокДт,
	               |	УчетныйОстатки.СуммаРазвернутыйОстатокКт
	               |ИЗ
	               |	РегистрБухгалтерии.Учетный.Остатки(&Граница, Счет В (&СчетаПереоценки), , Предприятия = &Предприятие) КАК УчетныйОстатки
	               |
	               |ДЛЯ ИЗМЕНЕНИЯ
	               |	РегистрБухгалтерии.Учетный.Остатки";
	Запрос.УстановитьПараметр("Граница",Новый Граница(Дата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СчетаПереоценки", СчетаПодлежащиеПереоценке.ВыгрузитьКолонку("Счет"));
	Запрос.УстановитьПараметр("Предприятие",Предприятие);
	ВалютныеОстатки = Запрос.Выполнить().Выгрузить();
	
	Если ВалютныеОстатки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КэшКурсовВалют = Новый Соответствие;
	ВалютаПроекта = Предприятие.ОсновнаяВалютаУчета;
	
	СтатьяКурсовыхРазниц = Статья;
	
	Для каждого СтрокаОстатка Из ВалютныеОстатки Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаОстатка.Валюта) Тогда
			СвойстваСчета = УправленческийУчетПовтИсп.ПолучитьСвойстваСчета(СтрокаОстатка.Счет);
			Сообщить("Ошибка переоценки валютных счетов.Обнаружены остатки по валютным счетам без указания валюты. Переоценка остатка не производится." + Символы.ПС + "- счет: " + СтрокаОстатка.Счет + " <"+ СтрокаОстатка.Счет.Наименование +">");
			
			Для НомерСубконто = 1 по 3 Цикл
				
				Если СвойстваСчета["ВидСубконто" + НомерСубконто] = Неопределено
					ИЛИ СвойстваСчета["ВидСубконто" + НомерСубконто + "ТолькоОбороты"] Тогда
					Продолжить;
				КонецЕсли;
				
				Сообщить( "	- субконто <" + СвойстваСчета["ВидСубконто" + НомерСубконто + "Наименование"] + "> : """ 
					+ СтрокаОстатка["Субконто" + НомерСубконто] + """");
				КонецЦикла;
				
			Сообщить("Валютный остаток: " + Формат(СтрокаОстатка.ВалютнаяСуммаОстаток, "ЧЦ=15; ЧДЦ=2; ЧН=-") 
				+ " , рублевый остаток: " + Формат(СтрокаОстатка.СуммаОстаток, "ЧЦ=15; ЧДЦ=2; ЧН=-") + "Рекомендуется указать валюту в проводках.");	
			Продолжить;		
		КонецЕсли; 
				
		ПереоцениваютсяРубли = Ложь;
		
		СтруктураКурса = КэшКурсовВалют[СтрокаОстатка.Валюта];
		
		Если СтруктураКурса = Неопределено Тогда
			СтруктураКурса = РегистрыСведений.КурсыВалют.СрезПоследних(Дата, Новый Структура("Валюта", СтрокаОстатка.Валюта))[0];
			КэшКурсовВалют.Вставить(СтрокаОстатка.Валюта, СтруктураКурса);
		КонецЕсли;
		
	    ОстатокВал = СтрокаОстатка.ВалютнаяСуммаОстаток;
		Остаток    = СтрокаОстатка.СуммаОстаток;
				
		КурсВал = сабРаботаСКурсамиВалют.ЗаполнитьДанныеЛокальногоКурсаДляВалюты(СтрокаОстатка.Валюта, ВалютаПроекта, Дата).Курс;
		РасчетныйОстаток = ОстатокВал * КурсВал;
		
		СуммаПереоценки = РасчетныйОстаток - Остаток;
		
		Если СуммаПереоценки = 0 Тогда
			Продолжить;		
		КонецЕсли; 
		
		Проводка = Движения.Учетный.Добавить();
		Проводка.Регистратор = Ссылка;
		Проводка.Период = Дата;
		Проводка.СценарийПлана = Справочники.СценарииПланирования.НайтиПоНаименованию("Факт", Истина);
		Проводка.Предприятия = Предприятие;
		Проводка.Содержание = "Переоценка валютных остатков";
		
		СубконтоОст1 = УЧ_Сервер.ПолучитьСубконто(1, СтрокаОстатка.Счет.ПолучитьОбъект());
		СубконтоОст2 = УЧ_Сервер.ПолучитьСубконто(2, СтрокаОстатка.Счет.ПолучитьОбъект());
		
		Если СуммаПереоценки < 0 Тогда
			Проводка.СчетДт = СчетКурсовйРазницы;
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоДт, СчетКурсовйРазницы , 1, Статья);
			
			Если СчетКурсовйРазницы = ПланыСчетов.Учетный.Счет98() И (СубконтоОст1 = "Кассы" ИЛИ СубконтоОст1 = "Банковские счета") Тогда
				УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоДт, СчетКурсовйРазницы, 1, СтрокаОстатка.Субконто1);
			ИначеЕсли СчетКурсовйРазницы = ПланыСчетов.Учетный.Счет98() И (СубконтоОст2 = "Кассы" ИЛИ СубконтоОст2 = "Банковские счета") Тогда
				УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоДт, СчетКурсовйРазницы, 2, СтрокаОстатка.Субконто2);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(КорПодразделения) И Проводка.СчетДт.УчетПоПодразделениям Тогда 
				Проводка.ПодразделениеДт = КорПодразделения;	
			ИначеЕсли Проводка.СчетДт.УчетПоПодразделениям Тогда 
				Проводка.ПодразделениеДт = СтрокаОстатка.Подразделение;
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаОстатка.Счет;
								
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоКт, СтрокаОстатка.Счет , 1, СтрокаОстатка.Субконто1);
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоКт, СтрокаОстатка.Счет , 2, СтрокаОстатка.Субконто2);
			
			Если Не ЗначениеЗаполнено(СтрокаОстатка.Субконто3) Тогда
				ТекСубконто3 = Справочники.Субконто.ПустаяСсылка();
			Иначе
				ТекСубконто3 = СтрокаОстатка.Субконто3;
			КонецЕсли;
			
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоКт, СтрокаОстатка.Счет , 3, ТекСубконто3);
			
			Если Проводка.СчетКт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконто.НайтиПоНаименованию("Статьи ДДС", Истина)) <> Неопределено и не ЗначениеЗаполнено(Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.НайтиПоНаименованию("Статьи затрат", Истина)]) Тогда 
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.НайтиПоНаименованию("Статьи ДДС", Истина)] = Предприятие.УчетнаяПолитика.КурсовыеРазницыРасход;						
		    КонецЕсли;

			
			Если Проводка.СчетКт.УчетПоПодразделениям Тогда 
				Проводка.ПодразделениеКт = СтрокаОстатка.Подразделение;
			КонецЕсли;
			
			//Если Проводка.СчетКт.УчетПоЦФО Тогда
			//	Проводка.ЦФО = Предприятие;
			//	Проводка.ПодразделениеЦФО = ?(ЗначениеЗаполнено(СтрокаОстатка.Подразделение), СтрокаОстатка.Подразделение, Предприятие.ВидДеятельности);
			//КонецЕсли;

			Проводка.ВалютаКт = СтрокаОстатка.Валюта;
			
			Если ПереоцениваютсяРубли Тогда
				Проводка.ВалютнаяСуммаКт = - СуммаПереоценки;
			Иначе
				Проводка.Сумма = - СуммаПереоценки;
			КонецЕсли;
			
			Проводка.Содержание = "Переоценка валюты " + СтрокаОстатка.Валюта + " на " + строка(Дата)+ ". Курс - " + КурсВал;
		Иначе 
			Проводка.СчетДт = СтрокаОстатка.Счет;
			Проводка.СчетКт = СчетКурсовйРазницы;
			
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоДт, СтрокаОстатка.Счет, 1, СтрокаОстатка.Субконто1);
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоДт, СтрокаОстатка.Счет, 2, СтрокаОстатка.Субконто2);
			
			Если Не ЗначениеЗаполнено(СтрокаОстатка.Субконто3) Тогда
				ТекСубконто3 = Справочники.Субконто.ПустаяСсылка();
			Иначе
				ТекСубконто3 = СтрокаОстатка.Субконто3;
			КонецЕсли;
			
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоДт, СтрокаОстатка.Счет, 3, ТекСубконто3);
			
			Если Проводка.СчетДт.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконто.НайтиПоНаименованию("Статьи ДДС", Истина)) <> Неопределено и не ЗначениеЗаполнено(Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.НайтиПоНаименованию("Статьи затрат", Истина)]) Тогда  
				Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.НайтиПоНаименованию("Статьи ДДС", Истина)] = Предприятие.УчетнаяПолитика.КурсовыеРазницыДоход;						
		   КонецЕсли;
		   
			Если Проводка.СчетДт.УчетПоПодразделениям Тогда 
				Проводка.ПодразделениеДт = СтрокаОстатка.Подразделение;
			КонецЕсли;
			
			//Если Проводка.СчетДт.УчетПоЦФО Тогда
			//	Проводка.ЦФО = Предприятие;
			//	Проводка.ПодразделениеЦФО = ?(ЗначениеЗаполнено(СтрокаОстатка.Подразделение), СтрокаОстатка.Подразделение, Предприятие.ВидДеятельности);
			//КонецЕсли;

			Проводка.ВалютаДт = СтрокаОстатка.Валюта;
			
			УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоКт, СчетКурсовйРазницы, 1, Статья);
			
			Если СчетКурсовйРазницы = ПланыСчетов.Учетный.Счет98() И (СубконтоОст1 = "Кассы" ИЛИ СубконтоОст1 = "Банковские счета") Тогда
				УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоКт, СчетКурсовйРазницы, 1, СтрокаОстатка.Субконто1);
			ИначеЕсли СчетКурсовйРазницы = ПланыСчетов.Учетный.Счет98() И (СубконтоОст2 = "Кассы" ИЛИ СубконтоОст2 = "Банковские счета") Тогда
				УЧ_Сервер.УстановитьСубконто(Проводка.СубконтоКт, СчетКурсовйРазницы, 2, СтрокаОстатка.Субконто2);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(КорПодразделения) И Проводка.СчетКт.УчетПоПодразделениям Тогда 
				Проводка.ПодразделениеКт = КорПодразделения;
			ИначеЕсли Проводка.СчетКт.УчетПоПодразделениям Тогда 
				Проводка.ПодразделениеКт = СтрокаОстатка.Подразделение;
			КонецЕсли;
			
			Если ПереоцениваютсяРубли Тогда
				Проводка.ВалютнаяСуммаКт = СуммаПереоценки;
			Иначе
				Проводка.Сумма = СуммаПереоценки;
			КонецЕсли;
			
			Проводка.Содержание = "Переоценка валюты " + СтрокаОстатка.Валюта + " на " + строка(Дата)+ ". Курс - " + КурсВал;
		КонецЕсли;
		
		Если СчетКурсовйРазницы = ПланыСчетов.Учетный.Счет98() Тогда
			Движение = Движения.УЧ_ДоходыИРасходыБудПериодов.Добавить();
			
			Если СуммаПереоценки < 0 Тогда
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Сумма = - СуммаПереоценки;
			Иначе
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Сумма = СуммаПереоценки;
			КонецЕсли;
			
			Движение.Регистратор = Ссылка;
			Движение.Период = Дата;
			Движение.Предприятие = Предприятие;
			Движение.Подразделение = КорПодразделения;
			Движение.Валюта = СтрокаОстатка.Валюта;
			Движение.Субконто1 = СтрокаОстатка.Субконто1;
			Движение.Субконто2 = СтрокаОстатка.Субконто2;
			Движение.Субконто3 = СтрокаОстатка.Субконто3;
			Движение.Счет = СтрокаОстатка.Счет;
			Движение.СчетБудПериодов = СчетКурсовйРазницы;
			Движение.СтатьяБуд = Статья;
		КонецЕсли;
		
	КонецЦикла;
		
	Движения.Учетный.Записать(Истина);
	
КонецПроцедуры

// Эта функция пересчитывает сумму из валюты ВалютаНач по курсу ПоКурсуНач 
// в валюту ВалютаКон по курсу ПоКурсуКон
//
// Параметры:      
//	Сумма          - сумма, которую следует пересчитать;
//	ВалютаНач      - ссылка на элемент справочника Валют;
//                   определяет валюту, из которой надо пересчитывать;
//	ВалютаКон      - ссылка на элемент справочника Валют;
//                   определяет валюту, в которую надо пересчитывать;
// 	ПоКурсуНач     - курс валюты, из которой надо пересчитать;
// 	ПоКурсуКон     - курс валюты, в которую надо пересчитать;
// 	ПоКратностьНач - кратность валюты, из которой надо пересчитать (по умолчанию = 1);
// 	ПоКратностьКон - кратность валюты, в которую надо пересчитать  (по умолчанию = 1);
//
// Возвращаемое значение: 
//  Сумма, пересчитанная в другую валюту
//
Функция ПересчитатьИзВалютыВВалюту(Сумма, ВалютаНач, ВалютаКон, ПоКурсуНач, ПоКурсуКон,
		ПоКратностьНач = 1, ПоКратностьКон = 1) Экспорт

	Если (ВалютаНач = ВалютаКон) Тогда 	// Считаем, что пересчет не нужен.
		Возврат Сумма;
	ИначеЕсли (ПоКурсуНач = ПоКурсуКон) И (ПоКратностьНач = ПоКратностьКон) Тогда 
		Возврат Сумма;
	КонецЕсли;

	Если ПоКурсуНач = 0 Тогда
		ПоКурсуНач = 1;
	КонецЕсли;
	Если ПоКурсуКон = 0 Тогда
		ПоКурсуКон = 1;
	КонецЕсли;
	Если ПоКратностьНач = 0 Тогда
		ПоКратностьНач = 1;
	КонецЕсли;
	Если ПоКратностьКон = 0 Тогда
		ПоКратностьКон = 1;
	КонецЕсли;
	
	Результат = Окр((Сумма * ПоКурсуНач * ПоКратностьКон) / (ПоКурсуКон * ПоКратностьНач), 2);
	Возврат Результат;

КонецФункции

Процедура ОбработкаУдаленияПроведения(Отказ)
	РучнаяКорректировка = Ложь;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	РучнаяКорректировка = Ложь;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	//из подписки на событие
	БюджетныйНаСервере.ДокументыПередЗаписьюПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	БюджетныйНаСервере.ПриУстановкеНовогоНомераПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	сабОбщегоНазначенияБУХ.сабОбработкиЗаполненияУчпетныхДокументовОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
КонецПроцедуры
