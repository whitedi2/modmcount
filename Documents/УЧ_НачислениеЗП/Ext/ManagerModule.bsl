
Процедура Печать(ТабДок, Ссылка) Экспорт
	//{{_КОНСТРУКТОР_ПЕЧАТИ(Печать)
	Макет = Документы.УЧ_НачислениеЗП.ПолучитьМакет("Печать");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УЧ_НачислениеЗП.Сотрудник КАК Сотрудник,
	|	УЧ_НачислениеЗП.ВидНачисления КАК ВидНачисления,
	|	УЧ_НачислениеЗП.Сумма КАК Сумма
	|ИЗ
	|	Документ.УЧ_НачислениеЗП.ТабличнаяЧасть КАК УЧ_НачислениеЗП
	|ГДЕ
	|	УЧ_НачислениеЗП.Ссылка В(&Ссылка)
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудник),
	|	СУММА(Сумма)
	|ПО
	|	ВидНачисления";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьТабличнаяЧастьШапка = Макет.ПолучитьОбласть("ТабличнаяЧастьШапка");
	ОбластьТабличнаяЧасть = Макет.ПолучитьОбласть("ТабличнаяЧасть");
	ОбластьСотрудники = Макет.ПолучитьОбласть("СтрокаСотрудник");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	Если ВставлятьРазделительСтраниц Тогда
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	ОбластьЗаголовок.Параметры.Заголовок = "Ведомость по начисленной зарплате №" +Ссылка[0].Номер + " от " + Формат(Ссылка[0].Дата,"ДФ=dd.MM.yyyy"); 
	ТабДок.Вывести(ОбластьЗаголовок);
	
	//Шапка.Параметры.Заполнить(Выборка);
	//ТабДок.Вывести(Шапка);
	Итог =0;
	ТабДок.Вывести(ОбластьТабличнаяЧастьШапка);
	ТабДок.начатьавтогруппировкуСтрок();
	Пока Выборка.Следующий() Цикл
		ОбластьТабличнаяЧасть.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьТабличнаяЧасть,1);
		Итог = итог + Выборка.Сумма;
		ВыборкаСотрудник = Выборка.выбрать();
		
		Пока ВыборкаСотрудник.Следующий() Цикл
			ОбластьСотрудники.Параметры.Заполнить(ВыборкаСотрудник);
			ТабДок.Вывести(ОбластьСотрудники,2);
		КонецЦикла;
	КонецЦикла;
	ТабДок.ЗакончитьАвтоГруппировкуСтрок();
	ОбластьПодвал.Параметры.Итог = Итог;
	ТабДок.Вывести(ОбластьПодвал);
	
	ВставлятьРазделительСтраниц = Истина;
	
	//}}
КонецПроцедуры

Процедура ПечатьРасчетнаяВедомость(ТабДок, Ссылка) Экспорт
	//{{_КОНСТРУКТОР_ПЕЧАТИ(Печать)
	ВидУдержанияНДФЛ = Справочники.ВидыНачисленийОплатыТруда.НайтиПоНаименованию("НДФЛ", Истина);
	
	Макет = Документы.УЧ_НачислениеЗП.ПолучитьМакет("ПечатьРасчетнаяВедомость");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УЧ_НачислениеЗП.Сотрудник КАК Сотрудник,
	|	СУММА(УЧ_НачислениеЗП.Часов) КАК Часов,
	|	УЧ_НачислениеЗП.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВремТЗ
	|ИЗ
	|	Документ.УЧ_НачислениеЗП.ТабличнаяЧасть КАК УЧ_НачислениеЗП
	|ГДЕ
	|	УЧ_НачислениеЗП.Ссылка В(&Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	УЧ_НачислениеЗП.Сотрудник,
	|	УЧ_НачислениеЗП.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремТЗ.Сотрудник КАК Сотрудник,
	|	УчетныйОстаткиИОбороты.Субконто2 КАК ВидНачисления,
	|	СУММА(ЕСТЬNULL(-УчетныйОстаткиИОбороты.СуммаНачальныйОстаток, 0)) КАК СуммаНачальныйОстаток,
	|	СУММА(ЕСТЬNULL(УчетныйОстаткиИОбороты.СуммаОборотКт, 0)) КАК СуммаОборотКт,
	|	СУММА(ЕСТЬNULL(-УчетныйОстаткиИОбороты.СуммаКонечныйОстаток, 0)) КАК СуммаКВыплате,
	|	МАКСИМУМ(ВремТЗ.Часов) КАК Часов,
	|	УчетныйОстаткиИОбороты.Субконто2.Удержание КАК ЭтоУдержание,
	|	СУММА(ЕСТЬNULL(УчетныйОстаткиИОбороты.СуммаОборотДт, 0)) КАК СуммаОборотДт
	|ПОМЕСТИТЬ ВремПоВидамНач
	|ИЗ
	|	ВремТЗ КАК ВремТЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Учетный.ОстаткиИОбороты(
	|				&ДатаНачала,
	|				&ДатаОкончания,
	|				,
	|				,
	|				Счет В (&Счет70),
	|				,
	|				Предприятия = &Предприятие
	|					И Субконто1 В
	|						(ВЫБРАТЬ
	|							ВремТЗ.Сотрудник КАК Сотрудник
	|						ИЗ
	|							ВремТЗ КАК ВремТЗ)) КАК УчетныйОстаткиИОбороты
	|		ПО ВремТЗ.Сотрудник = УчетныйОстаткиИОбороты.Субконто1
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетныйОстаткиИОбороты.Субконто2,
	|	ВремТЗ.Сотрудник,
	|	УчетныйОстаткиИОбороты.Субконто2.Удержание
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УЧ_НачислениеЗПТабличнаяЧасть.Сотрудник,
	|	&ВидУдержанияНДФЛ,
	|	0,
	|	0,
	|	-ЕСТЬNULL(УЧ_НачислениеЗПТабличнаяЧасть.СуммаНДФЛ, 0),
	|	0,
	|	ИСТИНА,
	|	ЕСТЬNULL(УЧ_НачислениеЗПТабличнаяЧасть.СуммаНДФЛ, 0)
	|ИЗ
	|	Документ.УЧ_НачислениеЗП.ТабличнаяЧасть КАК УЧ_НачислениеЗПТабличнаяЧасть
	|ГДЕ
	|	УЧ_НачислениеЗПТабличнаяЧасть.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремПоВидамНач.Сотрудник КАК Сотрудник,
	|	СУММА(ВремПоВидамНач.СуммаОборотДт) КАК СуммаОборотДт,
	|	СУММА(ВремПоВидамНач.СуммаОборотКт) КАК СуммаОборотКт,
	|	СУММА(ВремПоВидамНач.СуммаНачальныйОстаток) КАК СуммаНачальныйОстаток,
	|	СУММА(ВремПоВидамНач.СуммаКВыплате) КАК СуммаКВыплате,
	|	СУММА(ВремПоВидамНач.Часов) КАК Часов
	|ПОМЕСТИТЬ ВремПоВидамНачГрупп
	|ИЗ
	|	ВремПоВидамНач КАК ВремПоВидамНач
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремПоВидамНач.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УЧ_НачислениеЗПУдержания.Сотрудник КАК Сотрудник,
	|	УЧ_НачислениеЗПУдержания.ВидУдержания КАК ВидУдержания,
	|	СУММА(ЕСТЬNULL(УЧ_НачислениеЗПУдержания.Сумма, 0)) КАК СуммаОборотДт
	|ПОМЕСТИТЬ ВремПоВидамУд
	|ИЗ
	|	Документ.УЧ_НачислениеЗП.Удержания КАК УЧ_НачислениеЗПУдержания
	|ГДЕ
	|	УЧ_НачислениеЗПУдержания.Ссылка В(&Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	УЧ_НачислениеЗПУдержания.Сотрудник,
	|	УЧ_НачислениеЗПУдержания.ВидУдержания
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УЧ_НачислениеЗПТабличнаяЧасть.Сотрудник,
	|	&ВидУдержанияНДФЛ,
	|	СУММА(ЕСТЬNULL(УЧ_НачислениеЗПТабличнаяЧасть.СуммаНДФЛ, 0))
	|ИЗ
	|	Документ.УЧ_НачислениеЗП.ТабличнаяЧасть КАК УЧ_НачислениеЗПТабличнаяЧасть
	|ГДЕ
	|	УЧ_НачислениеЗПТабличнаяЧасть.Ссылка В(&Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	УЧ_НачислениеЗПТабличнаяЧасть.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремПоВидамУд.Сотрудник КАК Сотрудник,
	|	СУММА(ВремПоВидамУд.СуммаОборотДт) КАК СуммаОборотДт
	|ПОМЕСТИТЬ ВремПоВидамУдГруп
	|ИЗ
	|	ВремПоВидамУд КАК ВремПоВидамУд
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремПоВидамУд.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремПоВидамУд.Сотрудник КАК Сотрудник,
	|	ВремПоВидамУд.ВидУдержания КАК ВидУдержания,
	|	СУММА(ВремПоВидамУд.СуммаОборотДт) КАК СуммаОборотДт
	|ПОМЕСТИТЬ ВремПоВидамУдИАвансы
	|ИЗ
	|	ВремПоВидамУд КАК ВремПоВидамУд
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремПоВидамУд.Сотрудник,
	|	ВремПоВидамУд.ВидУдержания
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВремТЗ.Сотрудник,
	|	""Аванс"",
	|	СУММА(ЕСТЬNULL(ВремПоВидамНачГрупп.СуммаОборотДт, 0) - ЕСТЬNULL(ВремПоВидамУдГруп.СуммаОборотДт, 0) - ЕСТЬNULL(ВремПоВидамНачГрупп.СуммаНачальныйОстаток, 0))
	|ИЗ
	|	ВремТЗ КАК ВремТЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремПоВидамУдГруп КАК ВремПоВидамУдГруп
	|		ПО ВремТЗ.Сотрудник = ВремПоВидамУдГруп.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремПоВидамНачГрупп КАК ВремПоВидамНачГрупп
	|		ПО ВремТЗ.Сотрудник = ВремПоВидамНачГрупп.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремТЗ.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремПоВидамУдИАвансы.Сотрудник КАК Сотрудник,
	|	СУММА(ВремПоВидамУдИАвансы.СуммаОборотДт) КАК СуммаОборотДт
	|ПОМЕСТИТЬ ВремПоВидамУдИАвансыГруп
	|ИЗ
	|	ВремПоВидамУдИАвансы КАК ВремПоВидамУдИАвансы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремПоВидамУдИАвансы.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремПоВидамНачГрупп.Сотрудник КАК Сотрудник,
	|	СУММА(ВремПоВидамНачГрупп.СуммаКВыплате + ЕСТЬNULL(ВремПоВидамУдИАвансыГруп.СуммаОборотДт, 0) - ВремПоВидамНачГрупп.СуммаОборотКт) КАК СуммаНачальныйОстаток,
	|	СУММА(ВремПоВидамНачГрупп.СуммаКВыплате) КАК СуммаКВыплате,
	|	ВремПоВидамНачГрупп.Часов КАК Часы,
	|	СУММА(ВремПоВидамНачГрупп.СуммаОборотКт) КАК СуммаНачисленияИтого,
	|	ЕСТЬNULL(ВремПоВидамУдИАвансыГруп.СуммаОборотДт, 0) КАК СуммаИтогоУдержания
	|ИЗ
	|	ВремПоВидамНачГрупп КАК ВремПоВидамНачГрупп
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремПоВидамУдИАвансыГруп КАК ВремПоВидамУдИАвансыГруп
	|		ПО ВремПоВидамНачГрупп.Сотрудник = ВремПоВидамУдИАвансыГруп.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремПоВидамНачГрупп.Сотрудник,
	|	ВремПоВидамНачГрупп.Часов,
	|	ЕСТЬNULL(ВремПоВидамУдИАвансыГруп.СуммаОборотДт, 0)";
	
	Запрос.Параметры.Вставить("ДатаНачала", НачалоМесяца(Ссылка[0].Дата));
	Запрос.Параметры.Вставить("ДатаОкончания", КонецМесяца(Ссылка[0].Дата));
	Запрос.Параметры.Вставить("Счет70", ПланыСчетов.Учетный.Счет70());
	Запрос.Параметры.Вставить("Предприятие", Ссылка[0].Предприятие);
	Запрос.Параметры.Вставить("ВидУдержанияНДФЛ", ?(ЗначениеЗаполнено(ВидУдержанияНДФЛ), ВидУдержанияНДФЛ, "НДФЛ"));
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	ВыборкаПакетов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	Выборка = ВыборкаПакетов[7].Выбрать();
	ВыборкаНачислений = ВыборкаПакетов[1].Выгрузить();
	ВыборкаУдержаний = ВыборкаПакетов[5].Выгрузить();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьТабличнаяЧастьШапка = Макет.ПолучитьОбласть("ТабличнаяЧастьШапка|ОбластьНачало");
	ОбластьТабличнаяЧасть = Макет.ПолучитьОбласть("ТабличнаяЧасть|ОбластьНачало");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал|ОбластьНачало");
	
	ОбластьТабличнаяЧастьШапкаНач = Макет.ПолучитьОбласть("ТабличнаяЧастьШапка|ОбластьНачислений");
	ОбластьТабличнаяЧастьНач = Макет.ПолучитьОбласть("ТабличнаяЧасть|ОбластьНачислений");
	ОбластьПодвалНач = Макет.ПолучитьОбласть("Подвал|ОбластьНачислений");
	
	ОбластьТабличнаяЧастьШапкаСеред = Макет.ПолучитьОбласть("ТабличнаяЧастьШапка|ОбластьСередина");
	ОбластьТабличнаяЧастьСеред = Макет.ПолучитьОбласть("ТабличнаяЧасть|ОбластьСередина");
	ОбластьПодвалСеред = Макет.ПолучитьОбласть("Подвал|ОбластьСередина");
	
	ОбластьТабличнаяЧастьШапкаУд = Макет.ПолучитьОбласть("ТабличнаяЧастьШапка|ОбластьУдержаний");
	ОбластьТабличнаяЧастьУд = Макет.ПолучитьОбласть("ТабличнаяЧасть|ОбластьУдержаний");
	ОбластьПодвалУд = Макет.ПолучитьОбласть("Подвал|ОбластьУдержаний");
	
	ОбластьТабличнаяЧастьШапкаКон = Макет.ПолучитьОбласть("ТабличнаяЧастьШапка|ОбластьКонец");
	ОбластьТабличнаяЧастьКон = Макет.ПолучитьОбласть("ТабличнаяЧасть|ОбластьКонец");
	ОбластьПодвалКон = Макет.ПолучитьОбласть("Подвал|ОбластьКонец");
	
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	Если ВставлятьРазделительСтраниц Тогда
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	ОбластьЗаголовок.Параметры.Заголовок = "Расчетная ведомость по заработной плате №" +Ссылка[0].Номер + " от " + Формат(Ссылка[0].Дата,"ДФ=dd.MM.yyyy"); 
	ТабДок.Вывести(ОбластьЗаголовок);
	
	//Шапка.Параметры.Заполнить(Выборка);
	//ТабДок.Вывести(Шапка);
	Итог =0;
		
	Номер = 1;
	
	НачисленияВсе = ВыборкаНачислений.Скопировать();
	НачисленияВсе.Свернуть("ВидНачисления, ЭтоУдержание");
	НачисленияВсе.Сортировать("ВидНачисления");
	
	УдержанияВсе = ВыборкаУдержаний.Скопировать();
	УдержанияВсе.Свернуть("ВидУдержания");
	УдержанияВсе.Сортировать("ВидУдержания");

	
	ТабДок.Вывести(ОбластьТабличнаяЧастьШапка);
	
	Для каждого ТекСтрокаВида Из НачисленияВсе Цикл
		Если НЕ ТекСтрокаВида.ЭтоУдержание Тогда
			ОбластьТабличнаяЧастьШапкаНач.Параметры.Заполнить(ТекСтрокаВида);
			ТабДок.Присоединить(ОбластьТабличнаяЧастьШапкаНач);	
		КонецЕсли;
	КонецЦикла;
	
	ТабДок.Присоединить(ОбластьТабличнаяЧастьШапкаСеред);	
	
	Для каждого ТекСтрокаВида Из УдержанияВсе Цикл
		ОбластьТабличнаяЧастьШапкаУд.Параметры.Заполнить(ТекСтрокаВида);
		ТабДок.Присоединить(ОбластьТабличнаяЧастьШапкаУд);
	КонецЦикла;
	
	ТабДок.Присоединить(ОбластьТабличнаяЧастьШапкаКон);
	
	ИтогоСуммаНачальныйОстаток = 0;
	ИтогоЧасы = 0;
	ИтогоСуммаНачислено = 0;
	ИтогоСуммаНачисленияИтого = 0;
	ИтогоСуммаИтогоУдержания = 0;
	ИтогоСуммаКВыплате = 0;
	
	Пока Выборка.Следующий() Цикл
		
		//начало
		ОбластьТабличнаяЧасть.Параметры.Заполнить(Выборка);
		ОбластьТабличнаяЧасть.Параметры.Номер = Номер;
		ТабДок.Вывести(ОбластьТабличнаяЧасть,1);
		
		//начисления
		Для каждого ТекСтрокаВида Из НачисленияВсе Цикл
			Если Не ТекСтрокаВида.ЭтоУдержание Тогда
				ОбластьТабличнаяЧастьНач.Параметры.СуммаОборотКт = 0;
				НачисленияУдержания = ВыборкаНачислений.НайтиСтроки(Новый Структура("Сотрудник, ВидНачисления", Выборка.Сотрудник, ТекСтрокаВида.ВидНачисления));
				Для каждого ТекСтрока Из НачисленияУдержания Цикл
					Если ТекСтрока.СуммаОборотКт Тогда //начисление
						ОбластьТабличнаяЧастьНач.Параметры.Заполнить(ТекСтрока);
					КонецЕсли;
				КонецЦикла;
				ТабДок.Присоединить(ОбластьТабличнаяЧастьНач,1);
			КонецЕсли;
		КонецЦикла;
		
		//середина
		ОбластьТабличнаяЧастьСеред.Параметры.Заполнить(Выборка);
		ТабДок.Присоединить(ОбластьТабличнаяЧастьСеред,1);
		
		//удержания
		Для каждого ТекСтрокаВида Из УдержанияВсе Цикл
			НачисленияУдержания = ВыборкаУдержаний.НайтиСтроки(Новый Структура("Сотрудник, ВидУдержания", Выборка.Сотрудник, ТекСтрокаВида.ВидУдержания));
			ОбластьТабличнаяЧастьУд.Параметры.СуммаОборотДт = 0;
			Для каждого ТекСтрока Из НачисленияУдержания Цикл
				Если ТекСтрока.СуммаОборотДт Тогда //начисление
					ОбластьТабличнаяЧастьУд.Параметры.Заполнить(ТекСтрока);
				КонецЕсли;
			КонецЦикла;
			ТабДок.Присоединить(ОбластьТабличнаяЧастьУд,1);
		КонецЦикла;
		
		//конец
		ОбластьТабличнаяЧастьКон.Параметры.Заполнить(Выборка);
		ТабДок.Присоединить(ОбластьТабличнаяЧастьКон,1);
		
		ИтогоСуммаНачальныйОстаток = ИтогоСуммаНачальныйОстаток + Выборка.СуммаНачальныйОстаток;
		//ИтогоСуммаНачислено = ИтогоСуммаНачислено + Выборка.СуммаНачислено;
		ИтогоСуммаНачисленияИтого = ИтогоСуммаНачисленияИтого + Выборка.СуммаНачисленияИтого;
		ИтогоСуммаИтогоУдержания = ИтогоСуммаИтогоУдержания + Выборка.СуммаИтогоУдержания;
		ИтогоЧасы = ИтогоЧасы + Выборка.Часы;
		ИтогоСуммаКВыплате = ИтогоСуммаКВыплате + Выборка.СуммаКВыплате;
		
		Номер = Номер + 1;
		
	КонецЦикла;
	
	//ОбластьПодвал.Параметры.Итог = Итог;
	ОбластьПодвал.Параметры.ИтогоСуммаНачальныйОстаток = ИтогоСуммаНачальныйОстаток;
	ОбластьПодвал.Параметры.ИтогоЧасы = ИтогоЧасы;
	ОбластьПодвал.Параметры.ИтогоСуммаНачислено = ИтогоСуммаНачислено;
	ТабДок.Вывести(ОбластьПодвал);
	
	Для каждого ТекСтрокаВида Из НачисленияВсе Цикл
		Если НЕ ТекСтрокаВида.ЭтоУдержание Тогда
			ОбластьПодвалНач.Параметры.Заполнить(ТекСтрокаВида);
			ТабДок.Присоединить(ОбластьПодвалНач);	
		КонецЕсли;
	КонецЦикла;
	
	ОбластьПодвалСеред.Параметры.ИтогоСуммаНачисленияИтого = ИтогоСуммаНачисленияИтого;
	ТабДок.Присоединить(ОбластьПодвалСеред);	
	
	Для каждого ТекСтрокаВида Из УдержанияВсе Цикл
		ОбластьПодвалУд.Параметры.Заполнить(ТекСтрокаВида);
		ТабДок.Присоединить(ОбластьПодвалУд);
	КонецЦикла;
	
	ОбластьПодвалКон.Параметры.ИтогоСуммаИтогоУдержания = ИтогоСуммаИтогоУдержания;
	ОбластьПодвалКон.Параметры.ИтогоСуммаКВыплате = ИтогоСуммаКВыплате;
	ТабДок.Присоединить(ОбластьПодвалКон);
	
	ВставлятьРазделительСтраниц = Истина;
	
	//}}
КонецПроцедуры
