
&Перед("ПередЗаписью")
Процедура УУ_ПередЗаписью1(Отказ, РежимЗаписи, РежимПроведения)

	Если Модифицированность() Тогда
		Если ДополнительныеСвойства.Свойство("НеДобавлятьЗаписьВРегистрИзмененных") Тогда
			Если Не ДополнительныеСвойства.НеДобавлятьЗаписьВРегистрИзмененных Тогда
				сабОбщегоНазначенияБУХ.УстановитьМодифицированностьБУДокумента(ЭтотОбъект, Ссылка, РежимЗаписи);
			КонецЕсли;
		Иначе
			сабОбщегоНазначенияБУХ.УстановитьМодифицированностьБУДокумента(ЭтотОбъект, Ссылка, РежимЗаписи);
		КонецЕсли;
	КонецЕсли;

	Если НЕ ЭтоНовый() Тогда
		Если (ОбменДанными.Загрузка Или ЭтотОбъект.ДополнительныеСвойства.Свойство("ЭтоОбмен")) И АвтообновленияЗаблокированы Тогда
			ИгнорироватьБлокАвтообновления = Справочники.сабМониторВнедрения.ПолучитьЗначениеНастройки("сабИгнорироватьБлокировкуАвтообновленийДокументов");
			Если Не ИгнорироватьБлокАвтообновления Тогда
				ЭтотОбъект.Прочитать();
				ОбменДанными.Загрузка = Не ЭтотОбъект.ДополнительныеСвойства.Свойство("ЭтоОбмен");   
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура УУ_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УЧ_ВыпускПродукции") Тогда
		//НоменклатураУслугПоПереработке = Справочники.Номенклатура.НайтиПоНаименованию("Услуги по переработке"); 
		Счет_10_01 = ПланыСчетов.Хозрасчетный.СырьеИМатериалы;
		Счет_10_07 = ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку; 
		СтатьяСписанияМатериалов = Справочники.СтатьиЗатрат.СписаниеМатериалов;
		ЭтотОбъект.Контрагент = ДанныеЗаполнения.Контрагент;
		ЭтотОбъект.ДоговорКонтрагента = ДанныеЗаполнения.Договор;   
		ЭтотОбъект.СчетУчетаРасчетовПоАвансам = ПланыСчетов.Хозрасчетный.РасчетыПоАвансамВыданным;
		ЭтотОбъект.СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
		ЭтотОбъект.СчетЗатрат = ПланыСчетов.Хозрасчетный.ОсновноеПроизводство;  
		
		Если ДанныеЗаполнения.ТабличнаяЧасть.Количество() Тогда
			ЭтотОбъект.НоменклатурнаяГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.ТабличнаяЧасть[0].Номенклатура, "НоменклатурнаяГруппа");
		КонецЕсли;
		
		ЭтотОбъект.Склад = ?(ЗначениеЗаполнено(ДанныеЗаполнения.Склад), ДанныеЗаполнения.Склад, ДанныеЗаполнения.СкладМатериалов);
		ЭтотОбъект.СчетУчетаРасчетовПоТаре = ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчиками;
		
		Для Каждого ТекСтрокаТЧ Из ДанныеЗаполнения.ТабличнаяЧасть Цикл
			НоваяСтрокаПродукции = ЭтотОбъект.Продукция.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПродукции, ТекСтрокаТЧ);  
			НоваяСтрокаПродукции.Счет = ?(ЗначениеЗаполнено(ТекСтрокаТЧ.СчетУчета), ТекСтрокаТЧ.СчетУчета, Счет_10_01);
			
			//НоваяСтрокаУслуг = ЭтотОбъект.Услуги.Добваить;
			//НоваяСтрокаУслуг.Номенклатура = НоменклатураУслугПоПереработке;
		КонецЦикла;
		
		Для Каждого ТекСтрокаТЧ Из ДанныеЗаполнения.Материалы Цикл
			НоваяСтрокаМатериалов = ЭтотОбъект.ИспользованныеМатериалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаМатериалов, ТекСтрокаТЧ); 
			НоваяСтрокаМатериалов.Номенклатура = ТекСтрокаТЧ.Материал;
			НоваяСтрокаМатериалов.СчетУчета = Счет_10_07;
			НоваяСтрокаМатериалов.СтатьяЗатрат = СтатьяСписанияМатериалов;  
			
			СтрокиПродукции = ДанныеЗаполнения.ТабличнаяЧасть.НайтиСтроки(Новый Структура("УИД", ТекСтрокаТЧ.УИДТЧ));
			
			Если СтрокиПродукции.Количество() Тогда
				НоваяСтрокаМатериалов.Продукция = СтрокиПродукции[0].Номенклатура; 
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры
