Функция СчетаЗакрытия() Экспорт
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Учетный.Счет9002());
	МассивСчетов.Добавить(ПланыСчетов.Учетный.Счет9003());
	МассивСчетов.Добавить(ПланыСчетов.Учетный.Счет91());
	Возврат МассивСчетов;
КонецФункции // ()

Процедура ЗаполнитьПродукцию(ИмяКоманды, Предприятие, Дата, ТЧДока, ЗаголовокСубконто3) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныйОбороты.Субконто1 КАК Номенклатура,
		|	НЕОПРЕДЕЛЕНО КАК Субконто2,
		|	УчетныйОбороты.КоличествоОборотДт КАК КоличествоРеализовано,
		|	УчетныйОбороты.ПодразделениеКор КАК ВидДеятельности,
		|	ВЫБОР
		|		КОГДА УчетныйОбороты1.КоличествоОборот = 0
		|			ТОГДА 0
		|		ИНАЧЕ УчетныйОбороты.КоличествоОборот / УчетныйОбороты1.КоличествоОборот
		|	КОНЕЦ * 100 КАК Процент,
		|	УчетныйОбороты.Субконто3 КАК Субконто3,
		|	УчетныйОбороты.КоличествоОборот КАК Количество,
		|	0 КАК Сумма,
		|	УчетныйОбороты.КорСубконто1 КАК НоменклатурнаяГруппа,
		|	ИСТИНА КАК ЭтоПроизводство,
		|	""Количество"" КАК ПризнакРасчета
		|ПОМЕСТИТЬ Продукция
		|ИЗ
		|	РегистрБухгалтерии.Учетный.Обороты(
		|			&НачПериода,
		|			&КонПериода,
		|			,
		|			Счет = &Счет43,
		|			,
		|			Предприятия = &ВыбПредприятие
		|				И СценарийПлана = &СценарийФакт,
		|			КорСчет = &Счет40,
		|			) КАК УчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Учетный.Обороты(
		|				&НачПериода,
		|				&КонПериода,
		|				,
		|				Счет = &Счет43,
		|				,
		|				Предприятия = &ВыбПредприятие
		|					И СценарийПлана = &СценарийФакт,
		|				КорСчет = &Счет40,
		|				) КАК УчетныйОбороты1
		|		ПО УчетныйОбороты.Предприятия = УчетныйОбороты1.Предприятия
		|			И (ВЫБОР
		|				КОГДА УчетныйОбороты.КорСчет.Код = ""40""
		|					ТОГДА УчетныйОбороты.КорСубконто1 = УчетныйОбороты1.КорСубконто1
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УчетныйОбороты.Субконто1,
		|	УчетныйОбороты.Субконто2,
		|	УчетныйОбороты.КоличествоОборотДт,
		|	УчетныйОбороты.ПодразделениеКор,
		|	ВЫБОР
		|		КОГДА &РасчетСумма
		|			ТОГДА ВЫБОР
		|					КОГДА УчетныйОбороты1.СуммаОборотДт = 0
		|						ТОГДА 0
		|					ИНАЧЕ УчетныйОбороты.СуммаОборотДт / УчетныйОбороты1.СуммаОборотДт
		|				КОНЕЦ * 100
		|		ИНАЧЕ ВЫБОР
		|				КОГДА УчетныйОбороты1.КоличествоОборот = 0
		|					ТОГДА 0
		|				ИНАЧЕ УчетныйОбороты.КоличествоОборот / УчетныйОбороты1.КоличествоОборот
		|			КОНЕЦ * 100
		|	КОНЕЦ,
		|	УчетныйОбороты.Субконто3,
		|	УчетныйОбороты.КоличествоОборот,
		|	0,
		|	УчетныйОбороты.КорСубконто1,
		|	ИСТИНА,
		|	""Количество""
		|ИЗ
		|	РегистрБухгалтерии.Учетный.Обороты(
		|			&НачПериода,
		|			&КонПериода,
		|			,
		|			Счет = &Счет43,
		|			,
		|			Предприятия = &ВыбПредприятие
		|				И СценарийПлана = &СценарийФакт
		|				И ВЫБОР
		|					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИП(Справочник.Номенклатура)
		|						ТОГДА КорСубконто1 = Субконто1
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ,
		|			КорСчет.Код = ""20"",
		|			) КАК УчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Учетный.Обороты(
		|				&НачПериода,
		|				&КонПериода,
		|				,
		|				Счет = &Счет43,
		|				,
		|				Предприятия = &ВыбПредприятие
		|					И СценарийПлана = &СценарийФакт
		|					И ВЫБОР
		|						КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИП(Справочник.Номенклатура)
		|							ТОГДА КорСубконто1 = Субконто1
		|						ИНАЧЕ ИСТИНА
		|					КОНЕЦ,
		|				КорСчет.Код = ""20"",
		|				) КАК УчетныйОбороты1
		|		ПО УчетныйОбороты.Предприятия = УчетныйОбороты1.Предприятия
		|ГДЕ
		|	УчетныйОбороты.КоличествоОборотДт > 0
		|	И УчетныйОбороты1.КоличествоОборотДт > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УчетныйОбороты.Субконто1 КАК Субконто1,
		|	УчетныйОбороты.СуммаОборот КАК КоличествоРеализовано,
		|	УчетныйОбороты.Подразделение КАК ВидДеятельности,
		|	ВЫБОР
		|		КОГДА УчетныйОбороты1.СуммаОборот = 0
		|			ТОГДА 0
		|		ИНАЧЕ УчетныйОбороты.СуммаОборот / УчетныйОбороты1.СуммаОборот
		|	КОНЕЦ * 100 КАК Процент,
		|	УчетныйОбороты.Субконто3 КАК Субконто3,
		|	&Счет9002 КАК СчетЗатрат,
		|	0 КАК Количество,
		|	УчетныйОбороты.СуммаОборот КАК Сумма,
		|	NULL КАК НоменклатурнаяГруппа,
		|	ЛОЖЬ КАК ЭтоПроизводство,
		|	УчетныйОбороты.Субконто2 КАК Субконто2,
		|	""Сумма"" КАК ПризнакРасчета
		|ИЗ
		|	РегистрБухгалтерии.Учетный.Обороты(
		|			&НачПериода,
		|			&КонПериода,
		|			,
		|			Счет = &СчетРасчета,
		|			,
		|			Предприятия = &ВыбПредприятие
		|				И НЕ Субконто1 В
		|						(ВЫБРАТЬ
		|							Продукция.Номенклатура КАК Номенклатура
		|						ИЗ
		|							Продукция КАК Продукция)
		|				И СценарийПлана = &СценарийФакт
		|				И ТИПЗНАЧЕНИЯ(Субконто1) = ТИП(Справочник.Номенклатура)
		|				И НЕ Субконто1 = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|			,
		|			) КАК УчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Учетный.Обороты(
		|				&НачПериода,
		|				&КонПериода,
		|				,
		|				Счет = &СчетРасчета,
		|				,
		|				Предприятия = &ВыбПредприятие
		|					И НЕ Субконто1 В
		|							(ВЫБРАТЬ
		|								Продукция.Номенклатура КАК Номенклатура
		|							ИЗ
		|								Продукция КАК Продукция)
		|					И СценарийПлана = &СценарийФакт
		|					И ТИПЗНАЧЕНИЯ(Субконто1) = ТИП(Справочник.Номенклатура)
		|					И НЕ Субконто1 = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|				,
		|				) КАК УчетныйОбороты1
		|		ПО УчетныйОбороты.Предприятия = УчетныйОбороты1.Предприятия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Продукция.Номенклатура,
		|	Продукция.КоличествоРеализовано,
		|	Продукция.ВидДеятельности,
		|	Продукция.Процент,
		|	Продукция.Субконто3,
		|	&Счет9002,
		|	Продукция.Количество,
		|	Продукция.Сумма,
		|	Продукция.НоменклатурнаяГруппа,
		|	Продукция.ЭтоПроизводство,
		|	Продукция.Субконто2,
		|	Продукция.ПризнакРасчета
		|ИЗ
		|	Продукция КАК Продукция";

	Запрос.УстановитьПараметр("НачПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонПериода", КонецМесяца(Дата)-1);
	Запрос.УстановитьПараметр("ВыбПредприятие", Предприятие);
	Запрос.УстановитьПараметр("Счет9002", ПланыСчетов.Учетный.Счет9002());
	Запрос.УстановитьПараметр("Счет43", ПланыСчетов.Учетный.Счет43());
	Запрос.УстановитьПараметр("Счет40", ПланыСчетов.Учетный.Счет40());
	Запрос.УстановитьПараметр("СценарийФакт", Справочники.СценарииПланирования.СценарийФакт());
	Запрос.УстановитьПараметр("СчетРасчета", ?(ИмяКоманды = "ЗаполнитьПродукциюВыручка", ПланыСчетов.Учетный.НайтиПоКоду("90.01"), ПланыСчетов.Учетный.НайтиПоКоду("20")));
	Запрос.УстановитьПараметр("РасчетСумма", ИмяКоманды = "ЗаполнитьПродукциюСумма");

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ТЧДока.Очистить();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Субконто1) Тогда
			Продолжить;		
		КонецЕсли;
		НоваяСтрока = ТЧДока.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Субконто3) Тогда
			ЗаголовокСубконто3 = ВыборкаДетальныеЗаписи.Субконто3.Метаданные().Синоним;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТЧДока.Количество() Тогда
		НоваяСтрока = ТЧДока.Добавить();
		НоваяСтрока.СчетЗатрат = ПланыСчетов.Учетный.Счет9002();
		НоваяСтрока.Субконто1 = Справочники.Номенклатура.НайтиПоНаименованию("Простой", Истина);
		НоваяСтрока.Процент = 100;
	КонецЕсли;
	
КонецПроцедуры

