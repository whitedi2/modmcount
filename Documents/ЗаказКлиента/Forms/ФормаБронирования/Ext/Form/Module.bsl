
&НаСервере
Процедура ЗаполнитьТаблицуПодбора()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БронированиеТоваров.Регистратор КАК Заказ,
		|	СУММА(БронированиеТоваров.Количество) КАК Количество,
		|	БронированиеТоваров.Склад КАК Склад
		|ПОМЕСТИТЬ ВТБронирование
		|ИЗ
		|	РегистрСведений.БронированиеТоваров КАК БронированиеТоваров
		|ГДЕ
		|	БронированиеТоваров.Регистратор <> &Регистратор
		|	И БронированиеТоваров.Номенклатура = &Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	БронированиеТоваров.Регистратор,
		|	БронированиеТоваров.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТБронирование.Количество) КАК Количество,
		|	ВТБронирование.Склад КАК Склад
		|ПОМЕСТИТЬ ВТБронированиеСгрупп
		|ИЗ
		|	ВТБронирование КАК ВТБронирование
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТБронирование.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УчетныйОстатки.Субконто1 КАК Номенклатура,
		|	УчетныйОстатки.Субконто2 КАК Склад,
		|	УчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ВЫБОР
		|		КОГДА УчетныйОстатки.Субконто2 = &ТекущийСклад
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ПолеПорядок,
		|	ЕСТЬNULL(ВТБронированиеСгрупп.Количество, 0) КАК Забронировано
		|ИЗ
		|	РегистрБухгалтерии.Учетный.Остатки(
		|			,
		|			Счет В ИЕРАРХИИ (&СчетаУчета),
		|			,
		|			Субконто1 = &Номенклатура
		|				И СценарийПлана = &СценарийПлана
		|				И Предприятия = &Предприятие) КАК УчетныйОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБронированиеСгрупп КАК ВТБронированиеСгрупп
		|		ПО УчетныйОстатки.Субконто2 = ВТБронированиеСгрупп.Склад
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПолеПорядок";
	СчетаУчета = Новый Массив;
	СчетаУчета.Добавить(ПланыСчетов.Учетный.Счет10());
	СчетаУчета.Добавить(ПланыСчетов.Учетный.Счет41());
	СчетаУчета.Добавить(ПланыСчетов.Учетный.Счет43());

	Запрос.УстановитьПараметр("Номенклатура", ТекущаяНоменклатура);
	//Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Предприятие", Предприятие);
	Запрос.УстановитьПараметр("СценарийПлана", Справочники.СценарииПланирования.СценарийФакт());
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
	Запрос.УстановитьПараметр("ТекущийСклад", ТекущийСклад);
    Запрос.УстановитьПараметр("Регистратор", ЗаказСсылка);
	РезультатЗапроса = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	ВыборкаЗаказБронирование = РезультатЗапроса[0].Выбрать();
    Пока ВыборкаЗаказБронирование.Следующий() Цикл
		СтрокаЗабронированоПоЗаказамТЗ = ЗабронированоПоЗаказамТЗ.Добавить();
		СтрокаЗабронированоПоЗаказамТЗ.Заказ = ВыборкаЗаказБронирование.Заказ; 
		СтрокаЗабронированоПоЗаказамТЗ.Склад = ВыборкаЗаказБронирование.Склад;
        СтрокаЗабронированоПоЗаказамТЗ.Количество = ВыборкаЗаказБронирование.Количество;
	КонецЦикла;
	Выборка = РезультатЗапроса[2].Выбрать();
	ЕстьОстаткиПоОсновномуСкладу = Ложь;
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицаПодбора = ТаблицаПодбора.Добавить();
		СтрокаТаблицаПодбора.Склад = Выборка.Склад;
		СтрокаТаблицаПодбора.Остаток = Макс(Выборка.КоличествоОстаток,0); 
	    СтрокаТаблицаПодбора.ЗабронированоДругимиЗаказами = Выборка.Забронировано;	
		СтрокаТаблицаПодбора.Доступно = Макс(СтрокаТаблицаПодбора.Остаток - СтрокаТаблицаПодбора.ЗабронированоДругимиЗаказами,0);
		Если Выборка.Склад = ТекущийСклад Тогда
			СтрокаТаблицаПодбора.Забронировать = Забронировано;
			Доступно = СтрокаТаблицаПодбора.Доступно;
			ЕстьОстаткиПоОсновномуСкладу = Истина;
		КонецЕсли;
	КонецЦикла;
	Если Не ЕстьОстаткиПоОсновномуСкладу Тогда 
		Если ТаблицаПодбора.Количество() > 0 Тогда
			СтрокаТаблицаПодбора = ТаблицаПодбора.Вставить(0);
		Иначе
			СтрокаТаблицаПодбора = ТаблицаПодбора.Добавить();
		КонецЕсли;
		СтрокаТаблицаПодбора.Склад = ТекущийСклад;
		СтрокаТаблицаПодбора.Остаток = 0; 
		СтрокаТаблицаПодбора.ЗабронированоДругимиЗаказами = 0;	
		СтрокаТаблицаПодбора.Доступно = 0;
		СтрокаТаблицаПодбора.Забронировать = Забронировано;
		Доступно = 0;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Номенклатура") Тогда
		ТекущаяНоменклатура = Параметры.Номенклатура;	
	КонецЕсли;
	
	Если Параметры.Свойство("Предприятие") Тогда
		Предприятие = Параметры.Предприятие;	
	КонецЕсли;

	Если Параметры.Свойство("Склад") Тогда
		ТекущийСклад = Параметры.Склад;	
	КонецЕсли;
	
	Если Параметры.Свойство("Количество") Тогда
		Количество = Параметры.Количество;	
	КонецЕсли;  
	
	Если Параметры.Свойство("Забронировано") Тогда
		Забронировано = Параметры.Забронировано;	
	КонецЕсли; 
	
	Если Параметры.Свойство("ЗаказСсылка") Тогда
		ЗаказСсылка = Параметры.ЗаказСсылка;	
	КонецЕсли;

	Если Параметры.Свойство("Период") Тогда
		Период = Параметры.Период;
	Иначе
		Период = ТекущаяДата();
	КонецЕсли;
	
	Элементы.ОтборПоСкладу.Заголовок = "Только склад " + ТекущийСклад;  
	ОтборПоСкладу = Истина;
	ЗаполнитьТаблицуПодбора(); 
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Если Забронировано > Доступно Тогда
		Сообщить("Количество бронирования не может превышать доступный остаток.");
	Иначе
		МассивРезультат = ТаблицаПодбора.НайтиСтроки(Новый Структура("Склад",ТекущийСклад));
		Если МассивРезультат.Количество() > 0 Тогда
			Закрыть(МассивРезультат[0].Забронировать);   
		Иначе
			Закрыть(Неопределено);  
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда) 
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодбораЗабронироватьПриИзменении(Элемент)
	
	Забронировано = Элементы.ТаблицаПодбора.ТекущиеДанные.Забронировать;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаПодбораЗабронированоДругимиЗаказами" Тогда
		СтандартнаяОбработка = Ложь;
		СтруктураПоискаПоСкладу = Новый Структура("Склад",Элементы.ТаблицаПодбора.ТекущиеДанные.Склад);
		МассивСтрокЗабронированоПоЗаказамТЗ = ЗабронированоПоЗаказамТЗ.НайтиСтроки(СтруктураПоискаПоСкладу); 
		МассивСтуктурЗабронированоПоЗаказамТЗ = Новый Массив;
		Для каждого ЭлМассива Из МассивСтрокЗабронированоПоЗаказамТЗ Цикл
			МассивСтуктурЗабронированоПоЗаказамТЗ.Добавить(Новый Структура("Заказ,Количество",ЭлМассива.Заказ,ЭлМассива.Количество));
		КонецЦикла;
		Если МассивСтуктурЗабронированоПоЗаказамТЗ.Количество() > 0 Тогда 
			СтруктураПараметров = Новый Структура("Склад,МассивСтуктурЗабронированоПоЗаказамТЗ,Номенклатура",Элементы.ТаблицаПодбора.ТекущиеДанные.Склад,МассивСтуктурЗабронированоПоЗаказамТЗ,ТекущаяНоменклатура);
			ОткрытьФорму("Документ.ЗаказКлиента.Форма.ФормаБронированияПоДругимЗаказам",СтруктураПараметров,ЭтотОбъект,ЭтотОбъект.УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры
