
&НаКлиенте
Процедура ТЧМаршрутныеЛистыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Элементы.Группа1.Скрыть();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаказы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказКлиентаДок.Ссылка КАК ЗаказКлиента,
	               |	ЕСТЬNULL(сабМаршрутныйЛистТабличнаяЧасть.Ссылка, ЗНАЧЕНИЕ(Документ.сабМаршрутныйЛист.ПустаяСсылка)) КАК МаршрутныйЛист,
	               |	УЧ_Реализация.Ссылка КАК Ссылка,
	               |	ВЫБОР
	               |		КОГДА НЕ УЧ_Реализация.Ссылка ЕСТЬ NULL
	               |				ИЛИ ЗаказКлиентаДок.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.Собран)
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Собран
	               |ИЗ
	               |	Документ.ЗаказКлиента КАК ЗаказКлиентаДок
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.сабМаршрутныйЛист.ТабличнаяЧасть КАК сабМаршрутныйЛистТабличнаяЧасть
	               |		ПО ЗаказКлиентаДок.Ссылка = сабМаршрутныйЛистТабличнаяЧасть.ЗаказКлиента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УЧ_Реализация КАК УЧ_Реализация
	               |		ПО ЗаказКлиентаДок.Ссылка = УЧ_Реализация.ДокОснование
	               |ГДЕ
	               |	ЗаказКлиентаДок.Статус В(&Статусы)
	               |	И НАЧАЛОПЕРИОДА(ЗаказКлиентаДок.ДатаПоступления, ДЕНЬ) = &Дата
	               |	И (НЕ сабМаршрутныйЛистТабличнаяЧасть.Ссылка ЕСТЬ NULL
	               |			ИЛИ ЗаказКлиентаДок.СпособДоставки = &Самовывоз)";
	
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыЗаказовКлиентов.Новый);
	Статусы.Добавить(Перечисления.СтатусыЗаказовКлиентов.КОтгрузке);
	Статусы.Добавить(Перечисления.СтатусыЗаказовКлиентов.Собран);
	
	Запрос.УстановитьПараметр("Дата", НаДату);
	Запрос.УстановитьПараметр("Статусы", Статусы);
	Запрос.УстановитьПараметр("Самовывоз", Справочники.СпособыДоставки.Самовывоз);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ТЧЗаказы.Очистить();
	ТЧМаршрутныеЛисты.Очистить();
	
	ТЧМаршрутныеЛисты.Добавить();//пустой МЛ для Самовывоза
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТЧЗаказы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Если Не ТЧМаршрутныеЛисты.НайтиСтроки(Новый Структура("МаршрутныйЛист", Выборка.МаршрутныйЛист)).Количество() Тогда
			НоваяСтрокаМЛ = ТЧМаршрутныеЛисты.Добавить();	
			ЗаполнитьЗначенияСвойств(НоваяСтрокаМЛ, Выборка);	
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьТЧТоваровНаСервере(ТЧЗаказы.Выгрузить().ВыгрузитьКолонку("ЗаказКлиента"));

КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НаДату = ТекущаяДата();
	ЗаполнитьЗаказы();
КонецПроцедуры


&НаКлиенте
Процедура ТЧМаршрутныеЛистыПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.ТЧМаршрутныеЛисты.ДанныеСтроки(Элементы.ТЧМаршрутныеЛисты.ТекущаяСтрока);
	Если ТекДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	Элементы.ТЧЗаказы.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("МаршрутныйЛист", ТекДанные.МаршрутныйЛист));
КонецПроцедуры


&НаКлиенте
Процедура НаДатуПриИзменении(Элемент)
	ЗаполнитьЗаказы();
КонецПроцедуры


&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	//ТекДанные = Элементы.ТЧЗаказы.ДанныеСтроки(Элементы.ТЧЗаказы.ТекущаяСтрока);
	//Если ТекДанные = Неопределено Тогда
	//	Возврат;	
	//КонецЕсли;
	 	 
	 НайденныеСтроки = ТЧТовары.НайтиСтроки(Новый Структура("ЗаказКлиента, Номенклатура", ЗаказКлиента, Номенклатура));
	 Для каждого ТекСтрока Из НайденныеСтроки Цикл
		 ТекСтрока.КоличествоСобрано = ТекСтрока.КоличествоСобрано + Количество;
		 ТекСтрока.СерияНоменклатуры = СерияНоменклатуры;
	 КонецЦикла;
	 
	 Количество = 0;
	 Штрихкод = "";
	 Номенклатура = Неопределено;
	 СерияНоменклатуры = Неопределено;
	 
	 ЭтаФорма.ТекущийЭлемент = Элементы.Штрихкод;
	 
КонецПроцедуры


&НаКлиенте
Процедура ТЧЗаказыПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.ТЧЗаказы.ДанныеСтроки(Элементы.ТЧЗаказы.ТекущаяСтрока);
	Если ТекДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	Элементы.ТЧТовары.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("ЗаказКлиента", ТекДанные.ЗаказКлиента));
	ЗаказКлиента = ТекДанные.ЗаказКлиента;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЧТоваровНаСервере(ЗаказКлиента)
	
	ТЧТовары.Очистить();	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказКлиентаТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	               |	ЗаказКлиентаТабличнаяЧасть.Номенклатура КАК Номенклатура,
	               |	ЗаказКлиентаТабличнаяЧасть.Количество КАК Количество,
	               |	ЗаказКлиентаТабличнаяЧасть.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗаказКлиентаТабличнаяЧасть.КоличествоВУпаковке КАК КоличествоВУпаковке,
	               |	ЗаказКлиентаТабличнаяЧасть.КоличествоУпаковок КАК КоличествоУпаковок,
	               |	ЕСТЬNULL(УЧ_РеализацияТовары.Количество, 0) КАК КоличествоСобрано,
	               |	ЗаказКлиентаТабличнаяЧасть.Ссылка КАК ЗаказКлиента
	               |ИЗ
	               |	Документ.ЗаказКлиента.ТабличнаяЧасть КАК ЗаказКлиентаТабличнаяЧасть
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УЧ_Реализация.Товары КАК УЧ_РеализацияТовары
	               |		ПО ЗаказКлиентаТабличнаяЧасть.Ссылка = УЧ_РеализацияТовары.Ссылка.ДокОснование
	               |			И ЗаказКлиентаТабличнаяЧасть.НомерСтроки = УЧ_РеализацияТовары.НомерСтроки
	               |ГДЕ
	               |	ЗаказКлиентаТабличнаяЧасть.Ссылка В(&Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка", ЗаказКлиента);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выгрузить();
	
	Для каждого ТекСтрока Из Выборка Цикл
		НоваяСтрока = ТЧТовары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТЧЗаказыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Элементы.Группа4.Скрыть();
	ЭтаФорма.ТекущийЭлемент = Элементы.Штрихкод;
КонецПроцедуры


