
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МЛ") Тогда
		Заголовок = Заголовок + " (" + Параметры.МЛ + ")";  
	КонецЕсли;
	Если Параметры.Свойство("АдресВх") Тогда
		Если ЭтоАдресВременногоХранилища(Параметры.АдресВх) Тогда
			ТЗВетис = ПолучитьИзВременногоХранилища(Параметры.АдресВх);
			Если ТипЗнч(ТЗВетис) = Тип("ТаблицаЗначений") Тогда
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	СтатусыДокументовВЕТИС.Документ КАК Документ,
				|	Представление(СтатусыДокументовВЕТИС.Статус) КАК Статус,
				|	Представление(СтатусыДокументовВЕТИС.ДальнейшееДействие1) КАК ДальнейшееДействие
				|ИЗ
				|	РегистрСведений.СтатусыДокументовВЕТИС КАК СтатусыДокументовВЕТИС
				|ГДЕ
				|	СтатусыДокументовВЕТИС.Документ В(&ДокументыВетис)";
				Запрос.УстановитьПараметр("ДокументыВетис",ТЗВетис.ВыгрузитьКолонку("СформированныйДокументВетис"));
				РезультатЗапроса = Запрос.Выполнить();
				Выборка = РезультатЗапроса.Выбрать();
				НомерСтроки = 0;
				Для каждого СтрокаТЗВетис Из ТЗВетис Цикл
					НомерСтроки = НомерСтроки + 1;
					НоваяСтрокаСозданныеВетис = СозданныеВетис.Добавить();
					НоваяСтрокаСозданныеВетис.НомерСтроки = НомерСтроки; 
					НоваяСтрокаСозданныеВетис.ДокументРеализации = СтрокаТЗВетис.ДокументРеализации; 
					НоваяСтрокаСозданныеВетис.СформированныйДокументВетис = СтрокаТЗВетис.СформированныйДокументВетис;
					Если ЗначениеЗаполнено(СтрокаТЗВетис.СтатусДокументаВетис) Тогда
						НоваяСтрокаСозданныеВетис.СтатусДокументаВетис = СтрокаТЗВетис.СтатусДокументаВетис;
					КонецЕсли;  
					Если ЗначениеЗаполнено(СтрокаТЗВетис.Комментарий) Тогда
						НоваяСтрокаСозданныеВетис.Комментарий = СтрокаТЗВетис.Комментарий;
                    КонецЕсли;
					Выборка.Сбросить();
					Если Выборка.НайтиСледующий(Новый Структура("Документ",НоваяСтрокаСозданныеВетис.СформированныйДокументВетис)) Тогда
						НоваяСтрокаСозданныеВетис.СтатусДокументаВетис = Выборка.Статус; 
						НоваяСтрокаСозданныеВетис.ДальнейшееДействие = Выборка.ДальнейшееДействие;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			УдалитьИзВременногоХранилища(Параметры.АдресВх);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СозданныеВетисВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если Поле.Имя = "СозданныеВетисДокументРеализации" Тогда 
		Если ЗначениеЗаполнено(ТекДанные.ДокументРеализации) Тогда
			ПоказатьЗначение(,ТекДанные.ДокументРеализации);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "СозданныеВетисСтатусДокументаВетис" ИЛИ Поле.Имя = "СозданныеВетисДальнейшееДействие" 
		ИЛИ Поле.Имя = "СозданныеВетисСформированныйДокументВетис" Тогда
		Если ЗначениеЗаполнено(ТекДанные.СформированныйДокументВетис) Тогда
			ПоказатьЗначение(,ТекДанные.СформированныйДокументВетис);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "СозданныеВетисКомментарий"  Тогда
		Если ЗначениеЗаполнено(ТекДанные.Комментарий) Тогда
			ПоказатьЗначение(,ТекДанные.Комментарий);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
